#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт 
	ЮТТесты
		.ДобавитьТестовыйНабор("Тестирование общего модуля ""МФПовтИсп""")
			.ДобавитьТест("ЗначенияСтандартныхПеречислений");
	;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗначенияСтандартныхПеречислений

Процедура ЗначенияСтандартныхПеречислений() Экспорт 
	// Дано
	ПроверяемыеТипы = ПроверяемыеТипы();
	Исключения = Исключения();
	
	// Когда
	Типы = Новый Массив;
	ЗначенияСтандартныхПеречислений = МФПовтИсп.ЗначенияСтандартныхПеречислений();
	Для Каждого ЭлементКоллекции Из ПроверяемыеТипы Цикл
		Если 
			Исключения.Найти(ЭлементКоллекции.Ключ) = Неопределено 
			И ЗначенияСтандартныхПеречислений.Получить(ЭлементКоллекции.Ключ) = Неопределено
		Тогда
			Типы.Добавить("  * " + ЭлементКоллекции.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	// Тогда
	ТекстОшибки = "Отсутствует описание значений типов:" 
		+ Символы.ПС 
		+ СтрСоединить(Типы, Символы.ПС) 
		+ Символы.ПС 
	;
	
	ЮТест.ОжидаетЧто(Типы, ТекстОшибки)
		.ИмеетДлину(0)
	;
КонецПроцедуры

Функция ПроверяемыеТипы()
	Результат = Новый Соответствие;
	
	ОписаниеСвойствЭлементовФормы = МФЭлементыФормыПовтИсп.ОписанияСвойств();
	Для Каждого ЭлементФормы Из ОписаниеСвойствЭлементовФормы Цикл
		СвойстваЭлемента = ЭлементФормы.Значение.Свойства;
		Для Каждого СвойствоЭлемента Из СвойстваЭлемента Цикл
			ТипыСвойства = СвойствоЭлемента.Значение.ТипыСвойства;
			Для Каждого ТипСвойства Из ТипыСвойства Цикл
				Результат.Вставить(ТипСвойства.Тип, ТипСвойства.Тип);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция Исключения()
	Результат = Новый Массив;
	
	Результат.Добавить(Тип("Строка"));
	Результат.Добавить(Тип("ТаблицаФормы"));
	Результат.Добавить(Тип("Число"));
	Результат.Добавить(Тип("СочетаниеКлавиш"));
	Результат.Добавить(Тип("Цвет"));
	Результат.Добавить(Тип("Шрифт"));
	Результат.Добавить(Тип("Картинка"));
	Результат.Добавить(Тип("Рамка"));
	Результат.Добавить(Тип("СписокЗначений"));
	Результат.Добавить(Тип("СвязьПоТипу"));
	Результат.Добавить(МФПовтИсп.ТипСвязиПараметровВыбора());
	Результат.Добавить(МФПовтИсп.ТипПараметрыВыбора());
	Результат.Добавить(МФПовтИсп.ТипДатаНеопределено());
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецОбласти
