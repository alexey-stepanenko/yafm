//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДеревоФорм = РеквизитФормыВЗначение("ДеревоФормКонфигурации");
	МФИнструментыРазработчика.СформироватьДеревоФорм(ДеревоФорм);
	ЗначениеВРеквизитФормы(ДеревоФорм, "ДеревоФормКонфигурации");
	ФормаМодифицируется = Истина;
	Шаблоны = МФИнструментыРазработчика.СформироватьШаблоныПоМакету(
		Обработки.МФГенерацияАвтотестов.ПолучитьМакет("ШаблоныМодуляТестирования")
	);
	ЯзыкГенерацииТекстовМодулей = МФПовтИсп.РусскийЯзык();
	ИменаПараметровГенерацииРасширения = Обработки.МФГенерацияАвтотестов.ИменаПараметровГенерацииРасширения();
	ПрочитатьНастройкиГенерацииРасширения();
	//ИменаСвойствГенерацииРасширения = Обработки.МФГенерацияАвтотестов.ИменаСвойствГенерацииРасширения();
	Объект.ИмяРасширения = МФИнструментыРазработчикаКлиентСервер.ИмяРасширенияТестирования();
	ТекущаяВерсияРасширения = МФИнструментыРазработчикаКлиентСервер.ВерсияРасширенияТестирования();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ПустаяСтрока(ПутьКФайлуЗапуска1С) Тогда
		СистемнаяИнформация = Новый СистемнаяИнформация;
		ЭтоWindows =
			(СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86)
			Или (СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64)
		;
		СуффиксФайлаЗапуска = "";
		Если ЭтоWindows Тогда
			СуффиксФайлаЗапуска = ".exe";
		КонецЕсли;
		Объект.ПутьКФайлуЗапуска1С = СтрШаблон(
			"%1%2%3",
			КаталогПрограммы(),
			"1cv8",
			СуффиксФайлаЗапуска
		);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ТестируемыеФормы

&НаКлиенте
Процедура ТестируемыеФормыПриАктивизацииСтроки(Элемент)
	УстановитьПараметрыТекущейФормы();
	УстановитьВидимостьДоступностьЭлементов();
	ОбновитьОформлениеЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ТестируемыеФормыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	//Элемент.ТекущиеДанные.ФормаСуществует = Истина;
	УстановитьПараметрыТекущейФормы();
КонецПроцедуры

&НаКлиенте
Процедура ТестируемыеФормыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСвойстваТестируемойФормы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ТестируемыеФормыИмяФормыНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	Если Элементы.ТестируемыеФормы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ДеревоФорм", ДеревоФормКонфигурации);
	ПараметрыПодбора.Вставить(
		"ВыбраннаяФорма",
		Элементы.ТестируемыеФормы.ТекущиеДанные.ИмяФормы
	);
	ПараметрыПодбора.Вставить(
		"ВыбранныеФормы",
		ВыбранныеФормы(Элементы.ТестируемыеФормы.ТекущиеДанные.ИмяФормы)
	);
	
	// BSLLS:NestedFunctionInParameters-off
	ОткрытьФорму(
		"ОбщаяФорма.МФВыборФормы",
		ПараметрыПодбора, 
		ЭтаФорма, 
		ЭтаФорма, 
		, 
		, 
		Новый ОписаниеОповещения(
			"ПослеВыбораТестируемойФормы",
			ЭтаФорма,
			Элементы.ТестируемыеФормы.ТекущиеДанные
		), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	// BSLLS:NestedFunctionInParameters-on
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПараметрыГенерацииРасширения(Команда)
	ПараметрыОткрытияФормы = Новый Структура;
	ИменаПараметров = СтрРазделить(ИменаПараметровГенерацииРасширения, ",");
	Для Каждого ИмяПараметра Из ИменаПараметров Цикл
		ПараметрыОткрытияФормы.Вставить(ИмяПараметра, Объект[ИмяПараметра]);
	КонецЦикла;
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеЗакрытияФормыПараметровГенерацииРасширения", ЭтаФорма);
	ОткрытьФорму(
		"Обработка.МФГенерацияАвтотестов.Форма.ДиалогПараметрыГенерацииРасширения",
		ПараметрыОткрытияФормы,
		,
		УникальныйИдентификатор,
		,
		,
		ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьМодуль(Команда)
	Если ПустаяСтрока(ИмяМодуля) Или ПустаяСтрока(ТекстМодуля) Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ФильтрДиалога = Новый Массив;
	ДобавитьФильтрДиалога(ФильтрДиалога, "Все файлы", "*.*");
	ДобавитьФильтрДиалога(ФильтрДиалога, "Модули BSL", "*.bsl");
	Диалог.Фильтр = НСтр(СтрШаблон("ru='%1'", СтрСоединить(ФильтрДиалога, "|")));
	Диалог.ПолноеИмяФайла = ИмяМодуля + ".bsl";
	
	Диалог.Показать(Новый ОписаниеОповещения("ПослеВыбораФайлаДляСохраненияМодуля", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВсеМодули(Команда)
	ПродолжитьРаботу = Ложь;
	Для Каждого СтрокаТаблицы Из ТестируемыеФормы Цикл
		Если Не ПустаяСтрока(СтрокаТаблицы.ИмяМодуля) И Не ПустаяСтрока(СтрокаТаблицы.ТекстМодуля) Тогда
			ПродолжитьРаботу = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПродолжитьРаботу Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ФильтрДиалога = Новый Массив;
	Диалог.Показать(Новый ОписаниеОповещения("ПослеВыбораКаталогаДляСохраненияМодулей", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРасширениеСТестами(Команда)
	ДоступностьФормироватьМодулиСТестами = Ложь;
	Для Каждого СтрокаТаблицы Из ТестируемыеФормы Цикл
		Если Не (ПустаяСтрока(СтрокаТаблицы.ИмяМодуля) Или ПустаяСтрока(СтрокаТаблицы.ТекстМодуля)) Тогда
			ДоступностьФормироватьМодулиСТестами = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ФормироватьМодулиЯдраТестирования = Истина;
	
	СвойстваГенерацииРасширения = Новый Структура;
	Для Каждого ИмяСвойства Из СтрРазделить(ИменаСвойствГенерацииРасширения, ",") Цикл
		СвойстваГенерацииРасширения.Вставить(ИмяСвойства, ЭтаФорма[ИмяСвойства]);
	КонецЦикла;
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ПослеУстановкиСвойствГенерацииРасширения", ЭтаФорма);
	ОткрытьФорму(
		"Обработка.МФГенерацияАвтотестов.Форма.ДиалогРасширениеСТестами",
		СвойстваГенерацииРасширения,
		, , , , 
		ОписаниеОповещенияОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРасширениеСТестами1(Команда)
	Модули = Новый Массив;
	Для Каждого СтрокаТаблицы Из ТестируемыеФормы Цикл
		Если Не (ПустаяСтрока(СтрокаТаблицы.ИмяМодуля) Или ПустаяСтрока(СтрокаТаблицы.ТекстМодуля)) Тогда
			Модули.Добавить(Новый Структура(
				"Имя, Текст",
				СтрокаТаблицы.ИмяМодуля,
				СтрокаТаблицы.ТекстМодуля
			));
		КонецЕсли;
	КонецЦикла;
	
	Если Модули.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Модули", Модули);
	ОткрытьФорму(
		"Обработка.МФГенерацияАвтотестов.Форма._РасширениеСТестами",
		ПараметрыФормы,
		, , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
КонецПроцедуры

&НаКлиенте
Процедура ПросмотретьФорму(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьИзРасширения(Команда)
	Если ТестируемыеФормы.Количество() > 0 Тогда
		РезультатВопроса = Ждать ВопросАсинх(
			НСтр("ru='Перед загрузкой тестов из расширения текущий список тестируемых форм будет очищен. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет,
			30,
			КодВозвратаДиалога.Нет,
			НСтр("ru='Вопрос'"),
			КодВозвратаДиалога.Нет
		);
		Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = НСтр("ru='Файлы расширений конфигурации (*.cfe)|*.cfe|Все файлы (*.*)|*.*|'");
	РезультатВыбора = Ждать Диалог.ВыбратьАсинх();
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИмяФайлаРасширения = РезультатВыбора[0];
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайлаРасширения);
	Попытка
		СвойстваРасширения = СвойстваРасширения(ДвоичныеДанные);
	Исключение
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Файл ""%1"" не является расширением'"),
			ИмяФайлаРасширения
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	ИменаПараметров = СтрРазделить(ИменаПараметровГенерацииРасширения, ",");
	ГенераторРасширения = ПолучитьФорму("Обработка.МФГенерацияАвтотестов.Форма.ГенераторРасширения");
	ПараметрыИнициализации = ГенераторРасширения.НовыйПараметрыИнициализации();
	Для Каждого ПараметрИнициализации Из ПараметрыИнициализации Цикл
		Если ИменаПараметров.Найти(ПараметрИнициализации.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ПараметрыИнициализации[ПараметрИнициализации.Ключ] = ЭтаФорма[ПараметрИнициализации.Ключ];
	КонецЦикла;
	ПараметрИнициализации.ИмяРасширения = СвойстваРасширения.Имя;
	ГенераторРасширения.Инициализировать(ПараметрыИнициализации);
	ТестыИзРасширения = ГенераторРасширения.ТестыИзРасширения(ИмяФайлаРасширения);
	//КаталогИсходныхКодовРасширения = ПолучитьИмяВременногоФайла();
	//ПромежуточныеДанные = Новый Массив;
	//ПромежуточныеДанные.Добавить(КаталогИсходныхКодовРасширения);
	//Попытка
	//	Ждать ВременнаяИБ.Подготовить();
	//	Ждать ВременнаяИБ.ЗагрузитьРасширениеИзФайла(ИмяФайлаРасширения);
	//	Ждать ВременнаяИБ.ВыгрузитьРасширениеВИсходныеКоды(КаталогИсходныхКодовРасширения);
	//Исключение
	//	Ждать ОчиститьПромежуточныеДанные(ВременнаяИБ, ПромежуточныеДанные);
	//	Инфо = ИнформацияОбОшибке();
	//	ВызватьИсключение Инфо.Описание;
	//КонецПопытки;
	//
	//ПутьКФайлуМанифест = ПутьКОбъекту(
	//	КаталогИсходныхКодовРасширения,
	//	"CommonTemplates,MANIFEST,Ext,Template.txt"
	//);
	//
	//ФайлМанифеста = Новый Файл(ПутьКФайлуМанифест);
	//ФайлМанифестаСуществует = Ждать ФайлМанифеста.СуществуетАсинх();
	//Если Не ФайлМанифестаСуществует Тогда
	//	Ждать ОчиститьПромежуточныеДанные(ВременнаяИБ, ПромежуточныеДанные);
	//	ТекстИсключения = СтрШаблон(
	//		НСтр("ru='Расширение ""%1"" не содержит данных о тестах'"),
	//		ИмяФайлаРасширения
	//	);
	//	
	//	ВызватьИсключение ТекстИсключения;
	//КонецЕсли;
	//
	//ТекстовыйДокумент = Новый ТекстовыйДокумент;
	//Ждать ТекстовыйДокумент.ПрочитатьАсинх(ПутьКФайлуМанифест);
	//ЧтениеJSON = Новый ЧтениеJSON;
	//ЧтениеJSON.УстановитьСтроку(ТекстовыйДокумент.ПолучитьТекст());
	//Попытка
	//	ТестыИзРасширения = ПрочитатьJSON(ЧтениеJSON, Ложь);
	//Исключение
	//	Инфо = ИнформацияОбОшибке();
	//	Ждать ОчиститьПромежуточныеДанные(ВременнаяИБ, ПромежуточныеДанные);
	//	ТекстИсключения = СтрШаблон(
	//		НСтр("ru='Ошибка при чтении данных о тестах в расширении ""%1"": %2'"),
	//		ИмяФайлаРасширения,
	//		Инфо.Описание
	//	);
	//	
	//	ВызватьИсключение ТекстИсключения;
	//КонецПопытки;
	//
	//ТестируемыеФормы.Очистить();
	//Ждать ОчиститьПромежуточныеДанные(ВременнаяИБ, ПромежуточныеДанные);
КонецПроцедуры

// TODO 2025-07-04: Переделать на использовании формы ГенераторРасширения
&НаКлиенте
Асинх Процедура ЗагрузитьИзРасширения1(Команда)
	Если ТестируемыеФормы.Количество() > 0 Тогда
		РезультатВопроса = Ждать ВопросАсинх(
			НСтр("ru='Перед загрузкой из расширения необходимо очистить список тестируемых форм. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет,
			30,
			КодВозвратаДиалога.Нет,
			НСтр("ru='Вопрос'"),
			КодВозвратаДиалога.Нет
		);
		Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = НСтр("ru='Файлы расширений конфигурации (*.cfe)|*.cfe|Все файлы (*.*)|*.*|'");
	РезультатВыбора = Ждать Диалог.ВыбратьАсинх();
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИмяФайлаРасширения = РезультатВыбора[0];
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайлаРасширения);
	Попытка
		СвойстваРасширения = СвойстваРасширения(ДвоичныеДанные);
	Исключение
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Файл ""%1"" не является расширением'"),
			ИмяФайлаРасширения
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	ИменаПараметров = СтрРазделить(ИменаПараметровГенерацииРасширения, ",");
	ВременнаяИБ = ПолучитьФорму("Обработка.МФГенерацияАвтотестов.Форма.ВременнаяИнформационнаяБаза");
	ПараметрыИнициализации = ВременнаяИБ.НовыйПараметрыИнициализации();
	Для Каждого ПараметрИнициализации Из ПараметрыИнициализации Цикл
		Если ИменаПараметров.Найти(ПараметрИнициализации.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ПараметрыИнициализации[ПараметрИнициализации.Ключ] = ЭтаФорма[ПараметрИнициализации.Ключ];
	КонецЦикла;
	ПараметрыИнициализации.ИмяРасширения = СвойстваРасширения.Имя;
	ВременнаяИБ.Инициализировать(ПараметрыИнициализации);
	КаталогИсходныхКодовРасширения = ПолучитьИмяВременногоФайла();
	ПромежуточныеДанные = Новый Массив;
	ПромежуточныеДанные.Добавить(КаталогИсходныхКодовРасширения);
	Попытка
		Ждать ВременнаяИБ.Подготовить();
		Ждать ВременнаяИБ.ЗагрузитьРасширениеИзФайла(ИмяФайлаРасширения);
		Ждать ВременнаяИБ.ВыгрузитьРасширениеВИсходныеКоды(КаталогИсходныхКодовРасширения);
	Исключение
		Ждать ОчиститьПромежуточныеДанные(ВременнаяИБ, ПромежуточныеДанные);
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение Инфо.Описание;
	КонецПопытки;
	
	ПутьКФайлуМанифест = ПутьКОбъекту(
		КаталогИсходныхКодовРасширения,
		"CommonTemplates,MANIFEST,Ext,Template.txt"
	);
	
	ФайлМанифеста = Новый Файл(ПутьКФайлуМанифест);
	ФайлМанифестаСуществует = Ждать ФайлМанифеста.СуществуетАсинх();
	Если Не ФайлМанифестаСуществует Тогда
		Ждать ОчиститьПромежуточныеДанные(ВременнаяИБ, ПромежуточныеДанные);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Расширение ""%1"" не содержит данных о тестах'"),
			ИмяФайлаРасширения
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	Ждать ТекстовыйДокумент.ПрочитатьАсинх(ПутьКФайлуМанифест);
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстовыйДокумент.ПолучитьТекст());
	Попытка
		ТестыИзРасширения = ПрочитатьJSON(ЧтениеJSON, Ложь);
	Исключение
		Инфо = ИнформацияОбОшибке();
		Ждать ОчиститьПромежуточныеДанные(ВременнаяИБ, ПромежуточныеДанные);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при чтении данных о тестах в расширении ""%1"": %2'"),
			ИмяФайлаРасширения,
			Инфо.Описание
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	ТестируемыеФормы.Очистить();
	Ждать ОчиститьПромежуточныеДанные(ВременнаяИБ, ПромежуточныеДанные);
КонецПроцедуры

&НаКлиенте
Асинх Функция ОчиститьПромежуточныеДанные(ВременнаяИБ, ФайлыКУдалению)
	Ждать ВременнаяИБ.Освободить();
	Для Каждого ФайлКУдалению Из ФайлыКУдалению Цикл
		//Ждать УдалитьФайлыАсинх(ФайлКУдалению);
	КонецЦикла;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкиРаботыСРасширением

&НаСервере
Процедура ПрочитатьНастройкиГенерацииРасширения()
	ИменаПараметров = СтрРазделить(ИменаПараметровГенерацииРасширения, ",");
	Для Каждого ИмяПараметра Из ИменаПараметров Цикл
		ИмяСохраненногоПараметра = СтрШаблон(
			"%1%2",
			ПрефиксСохраняемыхПараметров(),
			ИмяПараметра
		);
		ЗначениеПараметра = ХранилищеОбщихНастроек.Загрузить(ИмяСохраненногоПараметра);
		Если ЗначениеПараметра = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Объект[ИмяПараметра] = ЗначениеПараметра;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиГенерацииРасширения(СохраняемыеНастройки)
	Для Каждого Элемент Из СохраняемыеНастройки Цикл
		ХранилищеОбщихНастроек.Сохранить(Элемент.Ключ, , Элемент.Значение);
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрефиксСохраняемыхПараметров()
	Возврат "__YAFM_";
КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияФормыПараметровГенерацииРасширения(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Элемент Из РезультатЗакрытия Цикл
		Объект[Элемент.Ключ] = Элемент.Значение;
	КонецЦикла;
	СохраняемыеНастройки = Новый Структура;
	ИменаПараметров = СтрРазделить(ИменаПараметровГенерацииРасширения, ",");
	Для Каждого ИмяПараметра Из ИменаПараметров Цикл
		ИмяСохраняемогоПараметра = СтрШаблон(
			"%1%2",
			ПрефиксСохраняемыхПараметров(),
			ИмяПараметра
		);
		СохраняемыеНастройки.Вставить(ИмяСохраняемогоПараметра, Объект[ИмяПараметра]);
	КонецЦикла;
	СохранитьНастройкиГенерацииРасширения(СохраняемыеНастройки);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура УстановитьВидимостьДоступностьЭлементов()
	Элементы.ИмяТестируемойФормы.Доступность =Не ПустаяСтрока(ИмяТестируемойФормы);
	Элементы.ОткрытьФорму.Доступность = Не ПустаяСтрока(ИмяТестируемойФормы);
	Элементы.ИмяМакета.Доступность = Не ПустаяСтрока(ИмяМакета);
	Элементы.ИмяМодуля.Доступность = Не ПустаяСтрока(ИмяМодуля);
	Элементы.ТекстМодуля.Доступность = Не ПустаяСтрока(ТекстМодуля);
	Элементы.ТекстМакета.Доступность = Не ПустаяСтрока(ТекстМакета);
КонецПроцедуры

&НаСервере
Процедура ОбновитьОформлениеЭлементов()
	Если ФормаМодифицируется Тогда
		Элементы.ГруппаИмяФормы.ЦветФона = Новый Цвет();
		Элементы.ИмяТестируемойФормы.ЦветТекстаЗаголовка = Новый Цвет();
		Элементы.СтатусФормы.Видимость = Ложь;
	Иначе
		Элементы.ГруппаИмяФормы.ЦветФона = ЦветаСтиля.МФЦветОтсутствующихОбъектов;
		Элементы.ИмяТестируемойФормы.ЦветТекстаЗаголовка = ЦветаСтиля.МФЦветВыделенныхНадписей;
		Элементы.СтатусФормы.Видимость = Истина;
	КонецЕсли;
	Если Не МакетСуществует Тогда
		Элементы.ГруппаИмяМакета.ЦветФона = ЦветаСтиля.МФЦветОтсутствующихОбъектов;
		Элементы.ИмяМакета.ЦветТекстаЗаголовка = ЦветаСтиля.МФЦветВыделенныхНадписей;
		Элементы.СтатусМакета.Видимость = Истина;
		Элементы.СтатусМакета.Заголовок = НСтр("ru='Макет не существует'")
	ИначеЕсли Не МакетСовпадает Тогда
		Элементы.ГруппаИмяМакета.ЦветФона = ЦветаСтиля.МФЦветРазличающихсяМакетов;
		Элементы.ИмяМакета.ЦветТекстаЗаголовка = ЦветаСтиля.МФЦветВыделенныхНадписей;
		Элементы.СтатусМакета.Видимость = Истина;
		Элементы.СтатусМакета.Заголовок = 
			НСтр("ru='Автотесты сформированы на основе данных в кэше. Если необходимо"
				+ " сформировать на основе данных из конфигурации - актуализируйте кэш"
				+ " по данной форме.'"
			)
		;
	Иначе
		Элементы.ГруппаИмяМакета.ЦветФона = Новый Цвет();
		Элементы.ИмяМакета.ЦветТекстаЗаголовка = Новый Цвет();
		Элементы.СтатусМакета.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ВыбранныеФормы(ВыбраннаяФорма)
	Результат = Новый Массив;
	
	Для Каждого Элемент Из ТестируемыеФормы Цикл
		Если
			ПустаяСтрока(Элемент.ИмяФормы)
			Или Результат.Найти(Элемент.ИмяФормы) <> Неопределено
			Или Элемент.ИмяФормы = ВыбраннаяФорма
		Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(Элемент.ИмяФормы);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПослеВыбораТестируемойФормы(РезультатЗакрытия, ТекущиеДанные) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТестируемаяФорма = РезультатЗакрытия.ВыбраннаяФорма;
	ДеревоФормКонфигурации.ПолучитьЭлементы().Очистить();
	МФИнструментыРазработчикаКлиентСервер.ЗаполнитьВетки(
		ДеревоФормКонфигурации, 
		РезультатЗакрытия.ДеревоФормКонфигурации
	);
	ТекущиеДанные.ИмяФормы = ТестируемаяФорма;
	
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыФормыИзКэша(_ИмяФормы, Шаблоны, АдресДанныхДляГенерацииАвтотестов, ЯзыкГенерации)
	Результат = РегистрыСведений.МФМакеты.КэшФорм(
		"ИмяМакета,Макет,МакетСуществует,МакетСовпадает",
		РегистрыСведений.МФМакеты.РежимОтбораФорма(),
		_ИмяФормы
	).Получить(_ИмяФормы);
	
	Если Результат = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Элемент Из Результат Цикл
		Если ТипЗнч(Элемент.Значение) <> Тип("ХранилищеЗначения") Тогда
			Продолжить;
		КонецЕсли;
		Результат[Элемент.Ключ] = Элемент.Значение.Получить();
	КонецЦикла;
	
	ИмяМодуля = "_" + СтрЗаменить(_ИмяФормы, ".", "_");
	МФИнструментыРазработчикаКлиентСерверПереопределяемый.СформироватьИмяМодуляПоИмениФормы(
		ИмяМодуля,
		_ИмяФормы
	);
	Результат.Вставить("ИмяМодуля", ИмяМодуля);
	
	ТекстМодуля = "";
	Попытка
		ТекстМодуля = ТекстМодуляТестированияПоМакету(
			_ИмяФормы,
			Результат.ИмяМакета,
			Шаблоны,
			АдресДанныхДляГенерацииАвтотестов,
			ЯзыкГенерации
		);
	Исключение
	КонецПопытки;
	Результат.Вставить("ТекстМодуля", ТекстМодуля);
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура УстановитьПараметрыТекущейФормы()
	ТекущиеДанные = Элементы.ТестируемыеФормы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ПустаяСтрока(ТекущиеДанные.ИмяФормы) Тогда
		ИмяТестируемойФормы = "";
		ИмяМакета = "";
		ИмяМодуля = "";
		ТекстМакета = "";
		ТекстМодуля = "";
		ФормаМодифицируется = Истина;
		МакетСуществует = Истина;
		МакетСовпадает = Истина;
	Иначе
		ИмяТестируемойФормы = ТекущиеДанные.ИмяФормы;
		ИмяМакета = ТекущиеДанные.ИмяМакета;
		ИмяМодуля = ТекущиеДанные.ИмяМодуля;
		ТекстМакета = ТекущиеДанные.ТекстМакета;
		ТекстМодуля = ТекущиеДанные.ТекстМодуля;
		ФормаМодифицируется = ТекущиеДанные.ФормаМодифицируется;
		МакетСуществует = ТекущиеДанные.МакетСуществует;
		МакетСовпадает = ТекущиеДанные.МакетСовпадает;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСвойстваТестируемойФормы(Элемент)
	ТекущиеДанные = Элементы.ТестируемыеФормы.ТекущиеДанные;
	ТекущиеДанные.ФормаМодифицируется = Ложь;
	ТекущиеДанные.МакетСуществует = Истина;
	ТекущиеДанные.МакетСовпадает = Истина;
	ТекущиеДанные.ИмяМакета = "";
	ТекущиеДанные.ИмяМодуля = "";
	ТекущиеДанные.ТекстМакета = "";
	ТекущиеДанные.ТекстМодуля = "";
	Если
		ПустаяСтрока(ТекущиеДанные.ИмяФормы)
		Или ТекущиеДанные.ФормаОтсутствует
	Тогда
		Возврат;
	КонецЕсли;
	
	АдресДанныхДляГенерацииАвтотестов = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
	МФИнструментыРазработчикаКлиент.ПолучитьФормуПоИмени(ТекущиеДанные.ИмяФормы, АдресДанныхДляГенерацииАвтотестов);
	// TODO 2025-04-12: Сделать выбор языка генерации текста модуля (русский/английский)
	ПараметрыТестируемойФормы = ПараметрыФормыИзКэша(
		ТекущиеДанные.ИмяФормы,
		Шаблоны,
		АдресДанныхДляГенерацииАвтотестов,
		ЯзыкГенерацииТекстовМодулей
	);
	
	Если ПараметрыТестируемойФормы <> Неопределено Тогда
		ТекущиеДанные.ФормаМодифицируется = Истина;
		ТекущиеДанные.МакетСуществует = ПараметрыТестируемойФормы.МакетСуществует;
		ТекущиеДанные.МакетСовпадает = ПараметрыТестируемойФормы.МакетСовпадает;
		ТекущиеДанные.ИмяМакета = ПараметрыТестируемойФормы.ИмяМакета;
		ТекущиеДанные.ИмяМодуля = ПараметрыТестируемойФормы.ИмяМодуля;
		ТекущиеДанные.ТекстМакета = ПараметрыТестируемойФормы.Макет;
		ТекущиеДанные.ТекстМодуля = ПараметрыТестируемойФормы.ТекстМодуля;
	КонецЕсли;
	
	ТекущиеДанные.СтрокаСформирована = Истина;
	
	ТестируемыеФормыПриАктивизацииСтроки(Элемент);
КонецПроцедуры

#Область ГенерацияТекстаМодуляТестирования

&НаСервереБезКонтекста
Функция ТекстМодуляТестированияПоМакету(ИмяФормы, ИмяМакета, Шаблоны, АдресДанныхДляГенерацииАвтотестов, ЯзыкГенерации)
	Результат = "";
	
	ДанныеДляГенерацииАвтотестов = ПолучитьИзВременногоХранилища(АдресДанныхДляГенерацииАвтотестов);
	
	ИсполняемыеСценарии = "";
	СоздатьИсполняемыеСценарииПроверкиРеквизитов(
		ДанныеДляГенерацииАвтотестов.Реквизиты,
		ИсполняемыеСценарии, 
		Шаблоны,
		ЯзыкГенерации
	);
	СоздатьИсполняемыеСценарииПроверкиЭлементовФормы(
		ДанныеДляГенерацииАвтотестов.Элементы,
		ИсполняемыеСценарии, 
		Шаблоны,
		ЯзыкГенерации
	);
	
	ШаблонМодуля = Шаблоны.ТекстМодуля;
	
	Результат = СтрЗаменить(ШаблонМодуля, "&&ИсполняемыеСценарии", СокрП(ИсполняемыеСценарии));
	Результат = СтрЗаменить(Результат, "&&ИмяФормы", ИмяФормы);
	
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Процедура СоздатьИсполняемыеСценарииПроверкиРеквизитов(ОписаниеМодификацийРеквизитов, ИсполняемыеСценарии, Шаблоны, ЯзыкГенерации)
	Если ОписаниеМодификацийРеквизитов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Тесты = "";
	Для Каждого Элемент Из ОписаниеМодификацийРеквизитов Цикл
		// Добавляем тест проверки существования реквизита
		ДобавитьТест(
			Тесты, 
			"РеквизитСуществует",
			"Реквизит с именем """"&&ИмяРеквизита"""" существует",
			"""&&ИмяРеквизита""",
			Шаблоны
		);
		
		Для Каждого ОписаниеТипаРеквизита Из Элемент.Значение.ТипыРеквизита Цикл
			Если МФПовтИсп.ЭтоНаименованиеТипаЧисло(ОписаниеТипаРеквизита.Ключ) Тогда
				КвалификаторыЧисла = ОписаниеТипаРеквизита.Значение.Получить("Тип").КвалификаторыЧисла;
				Модификатор = "";
				ПараметрДопустимыйЗнак = "Любой";
				ПараметрРазрядность = Формат(КвалификаторыЧисла.Разрядность, "ЧН=0; ЧГ=");
				ПараметрРазрядностьДробнойЧасти = Формат(КвалификаторыЧисла.РазрядностьДробнойЧасти, "ЧН=0; ЧГ=");
				Если КвалификаторыЧисла.ДопустимыйЗнак = ДопустимыйЗнак.Неотрицательный Тогда
					ПараметрДопустимыйЗнак = "Неотрицательный";
					Модификатор = "положительное ";
				КонецЕсли;
				ШаблонЗаголовкаТеста = "Типы реквизита с именем """"&&ИмяРеквизита"""" содержат %1число длиной """"%2"""" и дробной частью """"%3""""";
				ЗаголовокТеста = СтрШаблон(
					ШаблонЗаголовкаТеста,
					Модификатор,
					ПараметрРазрядность,
					ПараметрРазрядностьДробнойЧасти,
				);
				ДобавитьТест(
					Тесты, 
					"ТипыРеквизитаСодержатЧисло",
					ЗаголовокТеста,
					СтрШаблон(
						"""&&ИмяРеквизита"",ДопустимыйЗнак.%1,%2,%3",
						ПараметрДопустимыйЗнак,
						ПараметрРазрядность,
						ПараметрРазрядностьДробнойЧасти
					),
					Шаблоны
				);
			ИначеЕсли МФПовтИсп.ЭтоНаименованиеТипаСтрока(ОписаниеТипаРеквизита.Ключ) Тогда
				КвалификаторыСтроки = ОписаниеТипаРеквизита.Значение.Получить("Тип").КвалификаторыСтроки;
				Модификатор = "";
				ПараметрДопустимаяДлина = "Переменная";
				ПараметрДлина = Формат(КвалификаторыСтроки.Длина, "ЧН=0; ЧГ=");
				Если КвалификаторыСтроки.ДопустимаяДлина = ДопустимаяДлина.Фиксированная Тогда
					ПараметрДопустимыйЗнак = "Фиксированная";
					Модификатор = "фиксированную ";
				КонецЕсли;
				ШаблонЗаголовкаТеста = "Типы реквизита с именем """"&&ИмяРеквизита"""" содержат %1строку длиной """"%2"""" символов";
				ЗаголовокТеста = СтрШаблон(
					ШаблонЗаголовкаТеста,
					Модификатор,
					ПараметрДлина
				);
				ДобавитьТест(
					Тесты, 
					"ТипыРеквизитаСодержатСтроку",
					ЗаголовокТеста,
					СтрШаблон(
						"""&&ИмяРеквизита"",ДопустимаяДлина.%1,%2",
						ПараметрДопустимаяДлина,
						ПараметрДлина
					),
					Шаблоны
				);
			ИначеЕсли МФПовтИсп.ЭтоНаименованиеТипаДата(ОписаниеТипаРеквизита.Ключ) Тогда
				КвалификаторыДаты = ОписаниеТипаРеквизита.Значение.Получить("Тип").КвалификаторыДаты;
				Если КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Дата Тогда
					ПараметрЧастиДаты = "Дата";
					Модификатор = "дату";
				ИначеЕсли КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Время Тогда
					ПараметрЧастиДаты = "Время";
					Модификатор = "время";
				Иначе
					ПараметрЧастиДаты = "ДатаВремя";
					Модификатор = "дату и время";
				КонецЕсли;
				ШаблонЗаголовкаТеста = "Типы реквизита с именем """"&&ИмяРеквизита"""" содержат %1";
				ЗаголовокТеста = СтрШаблон(ШаблонЗаголовкаТеста, Модификатор);
				ДобавитьТест(
					Тесты, 
					"ТипыРеквизитаСодержатДату",
					ЗаголовокТеста,
					СтрШаблон(
						"""&&ИмяРеквизита"",ЧастиДаты.%1",
						ПараметрЧастиДаты
					),
					Шаблоны
				);
			ИначеЕсли МФПовтИсп.ЭтоНаименованиеТипаДвоичныеДанные(ОписаниеТипаРеквизита.Ключ) Тогда
				// TODO 2025-03-31: Добавить реализацию
			Иначе
				ТипРеквизитаСтрокой = ОписаниеТипаРеквизита.Значение.Получить("Наименование");
				ДобавитьТест(
					Тесты, 
					"ТипыРеквизитаСодержатПрочийТип",
					СтрШаблон(
						"Типы реквизита с именем """"&&ИмяРеквизита"""" содержат прочий тип """"%1""""",
						ТипРеквизитаСтрокой
					),
					СтрШаблон(
						"""&&ИмяРеквизита"",""%1""",
						ТипРеквизитаСтрокой
					),
					Шаблоны
				);
			КонецЕсли;
		КонецЦикла;
		
		Тесты = СтрЗаменить(Тесты, "&&ИмяРеквизита", Элемент.Значение.ИмяРеквизита);
	КонецЦикла;
	Тесты = СокрП(Тесты);
	
	Шаблон = Шаблоны.ИсполняемыеСценарии;
	ТекущиеИсполняемыеСценарии = СтрЗаменить(Шаблон, "&&ИмяТестовогоНабора", Шаблоны.ИмяТестовогоНабораРеквизиты);
	ТекущиеИсполняемыеСценарии = СтрЗаменить(ТекущиеИсполняемыеСценарии, "&&Тесты", Тесты);
	ТекущиеИсполняемыеСценарии = СтрЗаменить(
		ТекущиеИсполняемыеСценарии,
		"&&ИмяПроцедурыПеред",
		"ПодготовкаДанныхДляТестированияРеквизитов"
	);
	
	ИсполняемыеСценарии = СтрШаблон("%1%2%3", ИсполняемыеСценарии, ТекущиеИсполняемыеСценарии, Символы.ПС);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьИсполняемыеСценарииПроверкиЭлементовФормы(ОписаниеМодификацийЭлементов, ИсполняемыеСценарии, Шаблоны, ЯзыкГенерации)
	Если ОписаниеМодификацийЭлементов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеТиповЭлементовФормыСтроке = МФТестированиеКлиентСерверПовтИсп.СоответствиеТиповЭлементовФормыСтроке();
	
	Тесты = "";
	Для Каждого Элемент Из ОписаниеМодификацийЭлементов Цикл
		// Добавляем тест проверки существования элемента
		ДобавитьТест(
			Тесты, 
			"ЭлементФормыСуществует",
			"Элемент формы с именем """"&&ИмяЭлементаФормы""""",
			"""&&ИмяЭлементаФормы""",
			Шаблоны
		);
		
		// Добавляем тест проверки типа элемента
		ДобавитьТест(
			Тесты, 
			"ТипаЭлементаФормыРавен",
			"Тип элемента с именем """"&&ИмяЭлементаФормы"""" равен """"&&ТипЭлементаФормы""""",
			"""&&ИмяЭлементаФормы"",Тип(""&&ТипЭлементаФормы"")",
			Шаблоны
		);
		Тесты = СтрЗаменить(
			Тесты,
			"&&ТипЭлементаФормы",
			СоответствиеТиповЭлементовФормыСтроке.Получить(Элемент.Значение.ТипЭлемента)
		);
		
		Если Элемент.Значение.ИмяРодителя <> Неопределено Тогда
			// Добавляем тест проверки родителя элемента
			ДобавитьТест(
				Тесты, 
				"ИмяРодителяЭлементаФормыРавно",
				"Имя родителя элемента с именем """"&&ИмяЭлементаФормы"""" равно """"&&ИмяРодителяЭлементаФормы""""",
				"""&&ИмяЭлементаФормы"",""&&ИмяРодителяЭлементаФормы""",
				Шаблоны
			);
			Тесты = СтрЗаменить(
				Тесты,
				"&&ИмяРодителяЭлементаФормы",
				Элемент.Значение.ИмяРодителя
			);
		КонецЕсли;
		
		ОписаниеСвойствЭлементаФормы = МФЭлементыФормыПовтИсп.ОписанияСвойств().Получить(Элемент.Значение.ТипЭлемента);
		Для Каждого СвойствоЭлемента Из Элемент.Значение.ПредставленияСвойствЭлемента Цикл
			ИмяСвойства= СвойствоЭлемента.Ключ;
			ЗначениеСвойства = Элемент.Значение.СвойстваЭлемента[
				ОписаниеСвойствЭлементаФормы.СоответствиеСвойств.Получить(ИмяСвойства)
			];
			ЗначениеСвойстваЭлементаФормы = ЗначениеСвойстваЭлементаФормы(ЗначениеСвойства, ЯзыкГенерации);
			
			ДобавитьТест(
				Тесты, 
				"СвойствоЭлементаФормыРавно",
				"Свойство """"&&ИмяСвойстваЭлементаФормы"""" элемента с именем """"&&ИмяЭлементаФормы"""" равно """"&&ПредставлениеЗначенияСвойства""""",
				"""&&ИмяЭлементаФормы"",""&&ИмяСвойстваЭлементаФормы"",""&&ЗначениеСвойстваЭлементаФормы""",
				Шаблоны
			);
			Тесты = СтрЗаменить(
				Тесты,
				"&&ИмяСвойстваЭлементаФормы",
				ИмяСвойства
			);
			Тесты = СтрЗаменить(
				Тесты,
				"&&ПредставлениеЗначенияСвойства",
				СвойствоЭлемента.Значение
			);
			Тесты = СтрЗаменить(
				Тесты,
				"&&ЗначениеСвойстваЭлементаФормы",
				ЗначениеСвойстваЭлементаФормы
			);
		КонецЦикла;
		
		Тесты = СтрЗаменить(Тесты, "&&ИмяЭлементаФормы", Элемент.Значение.ИмяЭлемента);
	КонецЦикла;
	Тесты = СокрП(Тесты);
	
	Шаблон = Шаблоны.ИсполняемыеСценарии;
	ТекущиеИсполняемыеСценарии = СтрЗаменить(Шаблон, "&&ИмяТестовогоНабора", Шаблоны.ИмяТестовогоНабораЭлементыФормы);
	ТекущиеИсполняемыеСценарии = СтрЗаменить(ТекущиеИсполняемыеСценарии, "&&Тесты", Тесты);
	ТекущиеИсполняемыеСценарии = СтрЗаменить(
		ТекущиеИсполняемыеСценарии,
		"&&ИмяПроцедурыПеред",
		"ПодготовкаДанныхДляТестированияЭлементовФормы"
	);
	
	ИсполняемыеСценарии = СтрШаблон("%1%2%3", ИсполняемыеСценарии, ТекущиеИсполняемыеСценарии, Символы.ПС);
КонецПроцедуры

#Область ЗначениеСвойстваЭлементаФормы

&НаСервереБезКонтекста
Функция ЗначениеСвойстваЭлементаФормы(ЗначениеСвойства, ЯзыкГенерации)
	Результат = "";
	
	Если ТипЗнч(ЗначениеСвойства) = Тип("Цвет") Тогда
		Результат = ЗначениеСвойстваЭлементаФормыЦвет(ЗначениеСвойства);
	ИначеЕсли ТипЗнч(ЗначениеСвойства) = Тип("Строка") Тогда
		Результат = СтрШаблон(
			"""""%1""""",
			СтрЗаменить(ЗначениеСвойства, """", """""")
		);
	Иначе
		Результат = МФПовтИсп
			.ЗначенияСтандартныхПеречислений()
			.ПредставленияЗначенийПеречисленийПоЯзыкам
			.Получить(ЯзыкГенерации)
			.Получить(ЗначениеСвойства)
		;
		Если Результат = Неопределено Тогда
			ТекстИсключения = СтрШаблон(
				НСтр("ru='Ошибка получения представления значения ""%1"" типа ""%2""'"),
				ЗначениеСвойства,
				ТипЗнч(ЗначениеСвойства)
			);
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеСвойстваЭлементаФормыЦвет(ЗначениеСвойства)
	Результат = "";
	
	Если ЗначениеСвойства.Вид = ВидЦвета.АвтоЦвет Тогда
		Результат = "Новый Цвет()";
	ИначеЕсли ЗначениеСвойства.Вид = ВидЦвета.Абсолютный Тогда
		Результат = СтрШаблон(
			"Новый Цвет(%1, %2, %3)",
			ЗначениеСвойства.Красный,
			ЗначениеСвойства.Зеленый,
			ЗначениеСвойства.Синий
		);
	Иначе
		АбсолютныйЦвет = ЗначениеСвойства.ПолучитьАбсолютный();
		Результат = СтрШаблон(
			"Новый Цвет(%1, %2, %3)",
			АбсолютныйЦвет.Красный,
			АбсолютныйЦвет.Зеленый,
			АбсолютныйЦвет.Синий
		);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

&НаСервереБезКонтекста
Процедура ДобавитьТест(Тесты, ИмяПроцедуры, ЗаголовокТеста, ПараметрыТестаСтрокой, Шаблоны)
	ШаблонПараметраТеста = Шаблоны.ПараметрТеста;
	ПараметрыТеста = "";
	Параметры = СтрРазделить(ПараметрыТестаСтрокой, ",");
	МассивПараметров = Новый Массив;
	Для Каждого Параметр Из Параметры Цикл
		МассивПараметров.Добавить(
			СтрЗаменить(ШаблонПараметраТеста, "&&ПараметрТеста", Параметр)
		);
	КонецЦикла;
	ПараметрыТеста = СтрСоединить(МассивПараметров, "," + Символы.ПС);
	
	ТекущийТест = СтрЗаменить(Шаблоны.Тест, "&&ИмяПроцедуры", ИмяПроцедуры);
	ТекущийТест = СтрЗаменить(ТекущийТест, "&&ЗаголовокТеста", ЗаголовокТеста);
	ТекущийТест = СтрЗаменить(ТекущийТест, "&&ПараметрыТеста", ПараметрыТеста);
	Тесты = СтрШаблон("%1%2%3", Тесты, ТекущийТест, Символы.ПС);
КонецПроцедуры

#КонецОбласти

#Область РаботаСДиалогом

&НаКлиенте
Процедура ДобавитьФильтрДиалога(ФильтрДиалога, ИмяФильтра, ЗначениеФильтра)
	ФильтрДиалога.Добавить(СтрШаблон("%1, (%2)", ИмяФильтра, ЗначениеФильтра));
	ФильтрДиалога.Добавить(ЗначениеФильтра);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаДляСохраненияМодуля(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ВыбранныеФайлы[0];
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеСохраненияМодуля",
		ЭтаФорма
	);
	СохранитьМодульВФайл(ТекстМодуля, ИмяФайла, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияМодуля(Результат, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		ТекстПредупреждения = НСтр("ru='Сохранение завершено успешно!'");
	Иначе
		ТекстПредупреждения = НСтр("ru='Ошибка при сохранении модуля'");
	КонецЕсли;
	ПоказатьПредупреждение(, ТекстПредупреждения, 30, НСтр("Сохранение"));
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКаталогаДляСохраненияМодулей(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.СохраняемыеМодули = Новый Структура;
	Для Каждого СтрокаТаблицы Из ТестируемыеФормы Цикл
		Если ПустаяСтрока(СтрокаТаблицы.ИмяМодуля) Или ПустаяСтрока(СтрокаТаблицы.ТекстМодуля) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтаФорма.СохраняемыеМодули.Вставить(СтрокаТаблицы.ИмяМодуля, Новый Структура(
			"ЗаданиеВыполнено, ФайлСохранен",
			Ложь,
			Ложь
		));
	КонецЦикла;
	
	Для Каждого Элемент Из ЭтаФорма.СохраняемыеМодули Цикл
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеСохраненияМодулей",
			ЭтаФорма,
			Элемент.Ключ
		);
		
		ПолноеИмяФайлаМодуля = СтрШаблон(
			"%1%2%3.bsl",
			ВыбранныеФайлы[0],
			ПолучитьРазделительПути(),
			Элемент.Ключ
		);
		СохранитьМодульВФайл(СтрокаТаблицы.ТекстМодуля, ПолноеИмяФайлаМодуля, ОписаниеОповещения);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияМодулей(Результат, ДополнительныеПараметры) Экспорт
	// TODO 2025-04-22: Сделать одинаковый вызов оповещения для записи одного модуля и для
	// записи всех модулей
	ЭтаФорма.СохраняемыеМодули[ДополнительныеПараметры].ЗаданиеВыполнено = Истина;
	ЭтаФорма.СохраняемыеМодули[ДополнительныеПараметры].ФайлСохранен = Результат;
	
	ВывестиСообщениеОбОкончанииЗаписи = Истина;
	Для Каждого Элемент Из ЭтаФорма.СохраняемыеМодули Цикл
		Если Не Элемент.Значение.ЗаданиеВыполнено Тогда
			ВывестиСообщениеОбОкончанииЗаписи = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ВывестиСообщениеОбОкончанииЗаписи Тогда
		ТекстПредупреждения = НСтр("ru='Сохранение завершено успешно!'");
		ПоказатьПредупреждение(, ТекстПредупреждения, 30, НСтр("Сохранение"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СохранитьМодульВФайл(_ТекстМодуля, ИмяФайла, ОписаниеОповещения = Неопределено)
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(_ТекстМодуля);
	ТекстовыйДокумент.НачатьЗапись(ОписаниеОповещения, ИмяФайла, КодировкаТекста.UTF8);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СвойстваРасширения(ДвоичныеДанные)
	Возврат Обработки.МФГенерацияАвтотестов.СвойстваРасширения(ДвоичныеДанные);
КонецФункции

&НаКлиенте
Функция ПутьКОбъекту(ПутьКБазовомуКаталогу, ИменаОбъектов)
	СоставПутиКЦелевомуОбъекту = СтрРазделить(ИменаОбъектов, ",");
	СоставПутиКЦелевомуОбъекту.Вставить(0, ПутьКБазовомуКаталогу);
	Результат = СтрСоединить(СоставПутиКЦелевомуОбъекту, ПолучитьРазделительПути());
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПослеУстановкиСвойствГенерацииРасширения(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из РезультатЗакрытия Цикл
		ЭтаФорма[Элемент.Ключ] = Элемент.Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
