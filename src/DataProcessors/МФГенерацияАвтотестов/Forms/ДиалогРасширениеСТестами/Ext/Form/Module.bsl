//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("СвойстваГенерацииРасширения") Тогда
		ИменаПараметров = Новый Массив;
		Для Каждого Параметр Из Параметры.СвойстваГенерацииРасширения Цикл
			ИменаПараметров.Добавить(Параметр.Ключ);
			ЭтотОбъект[Параметр.Ключ] = Параметр.Значение;
		КонецЦикла;
		ИменаСвойствГенерацииРасширения = СтрСоединить(ИменаПараметров, ",");
	КонецЕсли;
	
	МФЦветРазличающихсяМакетов = ЦветаСтиля.МФЦветРазличающихсяМакетов;
	Элементы.ФормироватьМодулиСТестами.Доступность = ДоступностьФормироватьМодулиСТестами;
	ВозможностьОбновленияРасширения = Не ПустаяСтрока(ПолныйПутьКФайлуРасширения);
	ТекущаяВерсияРасширения = ВерсияРасширения;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПоказатьРежимыРаботыСРасширением();
	УстановитьВерсиюРасширения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура ПолныйПутьКФайлуРасширенияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = НСтр("ru='Файлы расширений (*.cfe)|*.cfe|Все файлы (*.*)|*.*'");
	Диалог.ПолноеИмяФайла = ПолныйПутьКФайлуРасширения;
	РезультатВыбора = Ждать Диалог.ВыбратьАсинх();
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ВозможностьОбновленияРасширения =
		ВозможностьОбновленияРасширения
		И (Диалог.ПолноеИмяФайла = ПолныйПутьКФайлуРасширения)
	;
	ПолныйПутьКФайлуРасширения = Диалог.ПолноеИмяФайла;
	ПоказатьРежимыРаботыСРасширением();
	ПроверитьВерсиюРасширения();
КонецПроцедуры

&НаКлиенте
Процедура ВерсияРасширенияПриИзменении(Элемент)
	ПроверитьВерсиюРасширения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьРасширение(Команда)
	СгенерироватьРасширение(МФИнструментыРазработчикаКлиент.РежимГенерацииРасширенияСоздание());
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРасширение(Команда)
	СгенерироватьРасширение(МФИнструментыРазработчикаКлиент.РежимГенерацииРасширенияОбновление());
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВерсиюРасширения()
	Если
		ПустаяСтрока(ТекущаяВерсияРасширения)
		Или ТекущаяВерсияРасширения = "0"
	Тогда
		ВерсияРасширения = "1";
	КонецЕсли;
	ПроверитьВерсиюРасширения();
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВерсиюРасширения()
	Элементы.ГруппаВерсияРасширения.ЦветФона = Новый Цвет;
	Элементы.ДекорацияВерсияРасширения.Видимость = Ложь;
	Если ВозможностьОбновленияРасширения И (ТекущаяВерсияРасширения = ВерсияРасширения) Тогда
		Элементы.ГруппаВерсияРасширения.ЦветФона = МФЦветРазличающихсяМакетов;
		Элементы.ДекорацияВерсияРасширения.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРежимыРаботыСРасширением()
	Элементы.ФормаОбновитьРасширение.Видимость = ВозможностьОбновленияРасширения;
	Если ВозможностьОбновленияРасширения Тогда
		Элементы.ФормаОбновитьРасширение.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ФормаСоздатьРасширение.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьРасширение(РежимГенерации)
	ЕстьОшибки = Не ПроверитьЗаполнение();
	Если Не (ФормироватьМодулиЯдраТестирования Или ФормироватьМодулиСТестами) Тогда
		СообщениеПользователю = Новый СообщениеПользователю();
		СообщениеПользователю.Текст = НСтр("ru='Необходимо выбрать хотя бы один вид модулей'");
		СообщениеПользователю.Поле = "ФормироватьМодулиЯдраТестирования";
		СообщениеПользователю.Сообщить();
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыГенерацииРасширения = Новый Структура;
	Для Каждого ИмяПараметра Из СтрРазделить(ИменаСвойствГенерацииРасширения, ",") Цикл
		ПараметрыГенерацииРасширения.Вставить(ИмяПараметра, ЭтаФорма[ИмяПараметра]);
	КонецЦикла;
	ПараметрыГенерацииРасширения.Удалить("ДоступностьФормироватьМодулиСТестами");
	ПараметрыГенерацииРасширения.Вставить("ВерсияРасширения", ВерсияРасширения);
	ПараметрыГенерацииРасширения.Вставить("РежимГенерации", РежимГенерации);
	
	Закрыть(ПараметрыГенерацииРасширения);
КонецПроцедуры

#КонецОбласти
