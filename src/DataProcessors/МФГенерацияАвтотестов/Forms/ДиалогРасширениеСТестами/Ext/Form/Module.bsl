//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИменаПараметров = Обработки.МФГенерацияАвтотестов.ИменаСвойствГенерацииРасширения();
	
	Для Каждого ИмяПараметра Из СтрРазделить(ИменаПараметров, ",") Цикл
		ЗначениеПоля = Неопределено;
		Если Параметры.Свойство(ИмяПараметра, ЗначениеПоля) Тогда
			ЭтаФорма[ИмяПараметра] = ЗначениеПоля;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ФормироватьМодулиЯдраТестирования.Доступность = ДоступностьФормироватьМодулиЯдраТестирования;
	Элементы.ФормироватьМодулиСТестами.Доступность = ДоступностьФормироватьМодулиСТестами;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура ПолныйПутьКФайлуРасширенияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = НСтр("ru='Файлы расширений (*.cfe)|*.cfe|Все файлы (*.*)|*.*'");
	Диалог.ПолноеИмяФайла = ПолныйПутьКФайлуРасширения;
	РезультатВыбора = Ждать Диалог.ВыбратьАсинх();
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПолныйПутьКФайлуРасширения = Диалог.ПолноеИмяФайла;
	Ждать ПроверитьСуществованиеФайлаРасширения();
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПолныйПутьКФайлуРасширенияПриИзменении(Элемент)
	Ждать ПроверитьСуществованиеФайлаРасширения();
КонецПроцедуры

&НаКлиенте
Асинх Процедура РежимРаботыПриИзменении(Элемент)
	Ждать ПроверитьСуществованиеФайлаРасширения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СгенерироватьРасширение(Команда)
	ПараметрыГенерацииРасширения = Новый Структура;
	
	Для Каждого ИмяПараметра Из ИменаПараметров Цикл
		ПараметрыГенерацииРасширения.Вставить(ИмяПараметра, ЭтаФорма[ИмяПараметра]);
	КонецЦикла;
	
	Закрыть(ПараметрыГенерацииРасширения);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Функция ПроверитьСуществованиеФайлаРасширения()
	Если ПустаяСтрока(ПолныйПутьКФайлуРасширения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ФайлРасширения = Новый Файл(ПолныйПутьКФайлуРасширения);
	ФайлСуществует = Ждать ФайлРасширения.СуществуетАсинх();
	Если Не ФайлСуществует Тогда
		РежимРаботыГенерацииРасширения = 0;
	КонецЕсли;
КонецФункции

#КонецОбласти
