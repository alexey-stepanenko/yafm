//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОписаниеПеременных

&НаКлиенте
Перем ПутьКФайлуЗапуска1С;

&НаКлиенте
Перем ВидИнформационнойБазы;

&НаКлиенте
Перем СтрокаСоединенияКСуществующейИБ;

&НаКлиенте
Перем ПользовательСуществующейИБ;

&НаКлиенте
Перем ПарольПользователяСуществующейИБ;

&НаКлиенте
Перем ПутьКВременнойФайловойИБ;

&НаКлиенте
Перем ИмяРасширения;

&НаКлиенте
Перем ВерсияРасширения;

&НаКлиенте
Перем СтрокаСоединения;

&НаКлиенте
Перем ОсновнаяСтрокаЗапуска;

&НаКлиенте
Перем ПромежуточныеДанные;

&НаКлиенте
Перем БазовыйКаталог;

&НаКлиенте
Перем ПутьККаталогуИсходников;

&НаКлиенте
Перем ПутьККаталогуОбщихМодулей;

&НаКлиенте
Перем МодулиТестирования;

&НаКлиенте
Перем ПолныйПутьКФайлуРасширения;

&НаКлиенте
Перем ТекстШаблонаОписанияМодуля;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура ЗакрытьФорму() Экспорт
	ЗакрытьФорму = Истина;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Функция НовыйПараметрыИнициализации() Экспорт
	Результат = Новый Структура;
	
	ИменаПараметров = СтрРазделить(ИменаПараметровГенерацииРасширения, ",");
	Для Каждого ИмяПараметра Из ИменаПараметров Цикл
		Результат.Вставить(ИмяПараметра, Неопределено);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Инициализировать(ПараметрыИнициализации) Экспорт
	ПутьКФайлуЗапуска1С = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ПутьКФайлуЗапуска1С");
	ВидИнформационнойБазы = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ВидИнформационнойБазы");
	СтрокаСоединенияКСуществующейИБ = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "СтрокаСоединенияКСуществующейИБ");
	ПользовательСуществующейИБ = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ПользовательСуществующейИБ");
	ПарольПользователяСуществующейИБ = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ПарольПользователяСуществующейИБ");
	ПутьКВременнойФайловойИБ = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ПутьКВременнойФайловойИБ");
	ИмяРасширения = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ИмяРасширения", "");
	ВерсияРасширения = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ВерсияРасширения", "");
КонецПроцедуры

&НаКлиенте
Асинх Функция ПроверитьВозможностьИспользования() Экспорт
	Прогресс = 0;
	Элементы.Прогресс.МаксимальноеЗначение = 2;
	УстановитьСтатус(НСтр("ru='Подготовка временной информационной базы'"));
	ПодключитьОбработчикОжидания("ПроверитьВозможностьИспользования_Подготовить", 0.1, Истина);
КонецФункции

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами(_ПолныйПутьКФайлуРасширения, _МодулиТестирования) Экспорт
	МодулиТестирования = _МодулиТестирования;
	ПолныйПутьКФайлуРасширения = _ПолныйПутьКФайлуРасширения;
	Прогресс = 0;
	Элементы.Прогресс.МаксимальноеЗначение = 7;
	УстановитьСтатус(НСтр("ru='Подготовка временной информационной базы'"));
	ПодключитьОбработчикОжидания("СоздатьРасширениеСТестами_Подготовить", 0.1, Истина);
КонецФункции

// СвойстваРасширения - Структура
&НаКлиенте
Асинх Функция ДополнитьСвойстваРасширения(СвойстваРасширения, ИмяФайлаРасширения) Экспорт
	Ждать Подготовить();
	КаталогИсходныхКодовРасширения = ПолучитьИмяВременногоФайла();
	ПромежуточныеДанные = Новый Массив;
	ПромежуточныеДанные.Добавить(КаталогИсходныхКодовРасширения);
	Попытка
		Ждать ЗагрузитьРасширениеИзФайла(ИмяФайлаРасширения);
		Ждать ВыгрузитьРасширениеВИсходныеКоды(КаталогИсходныхКодовРасширения);
	Исключение
		Ждать Освободить();
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение Инфо.Описание;
	КонецПопытки;
	
	ПутьКФайлуМанифест = ПутьКОбъекту(
		КаталогИсходныхКодовРасширения,
		"CommonTemplates,MANIFEST,Ext,Template.txt"
	);
	
	ФайлМанифеста = Новый Файл(ПутьКФайлуМанифест);
	ФайлМанифестаСуществует = Ждать ФайлМанифеста.СуществуетАсинх();
	Если Не ФайлМанифестаСуществует Тогда
		//Ждать Освободить(ПромежуточныеДанные);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Расширение ""%1"" не содержит данных о тестах'"),
			ИмяФайлаРасширения
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	Ждать ТекстовыйДокумент.ПрочитатьАсинх(ПутьКФайлуМанифест);
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстовыйДокумент.ПолучитьТекст());
	Попытка
		Манифест = ПрочитатьJSON(ЧтениеJSON, Ложь);
		СвойстваРасширения.Вставить("ВерсияПодсистемы", Манифест["version"]);
		Тесты = Новый Массив;
		Для Каждого Элемент Из Манифест["tests"] Цикл
			Тесты.Добавить(Новый Структура(
				"ИмяФормы, ИмяМодуля",
				Элемент["form"],
				Элемент["module"]
			));
		КонецЦикла;
		СвойстваРасширения.Вставить("Тесты", Тесты);
	Исключение
		Инфо = ИнформацияОбОшибке();
		Ждать Освободить();
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при чтении данных о тестах в расширении ""%1"" (Общий макет ""MANIFEST""): %2'"),
			ИмяФайлаРасширения,
			Инфо.Описание
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	Ждать Освободить();
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИменаПараметровГенерацииРасширения = Обработки.МФГенерацияАвтотестов.ИменаПараметровГенерацииРасширения();
	ШаблонРасширенияДляТестов = Обработки.МФГенерацияАвтотестов.ПолучитьМакет("ШаблонРасширенияДляТестов");
	ЗакрытьФорму = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы Тогда
		Освободить();
	ИначеЕсли Не ЗакрытьФорму Тогда
		Отказ = Истина;
	Иначе
		// Пустой блок
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Инициализировать

&НаКлиенте
Функция ЗначениеПараметраИнициализации(ПараметрыИнициализации, ИмяПараметра, ЗначениеПоУмолчанию = Неопределено)
	Результат = ЗначениеПоУмолчанию;
	
	Если ПараметрыИнициализации.Свойство(ИмяПараметра) Тогда
		Результат = ПараметрыИнициализации[ИмяПараметра];
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область ПроверитьВозможностьИспользования

&НаКлиенте
Асинх Функция ПроверитьВозможностьИспользования_Подготовить()
	Попытка
		Ждать Подготовить();
		
		Прогресс = Прогресс + 1;
		УстановитьСтатус(НСтр("ru='Освобождение временной информационной базы'"));
		ПодключитьОбработчикОжидания("ПроверитьВозможностьИспользования_Освободить", 0.1, Истина);
	Исключение
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаПроверкиПодключения", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Асинх Функция ПроверитьВозможностьИспользования_Освободить()
	Попытка
		Ждать Освободить();
		
		Прогресс = Прогресс + 1;
		ПодключитьОбработчикОжидания("ПроверитьВозможностьИспользования_Завершить", 0.1, Истина);
	Исключение
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаПроверкиПодключения", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Процедура ПроверитьВозможностьИспользования_Завершить()
	Оповестить("УспешноеЗавершениеПроверкиПодключения");
КонецПроцедуры

#КонецОбласти

#Область СоздатьРасширениеСТестами

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами_Подготовить()
	Попытка
		ПромежуточныеДанные = Новый Массив;
		Ждать Подготовить();
		Если ВидИнформационнойБазы = 0 Тогда
			ОсновнаяСтрокаЗапуска = СтрокаСоединения(СтрШаблон(
				"File=""%1""",
				ПутьКВременнойФайловойИБ
			));
		Иначе
			ОсновнаяСтрокаЗапуска = СтрокаСоединения(
				СтрокаСоединенияКСуществующейИБ,
				ПользовательСуществующейИБ,
				ПарольПользователяСуществующейИБ
			);
		КонецЕсли;
		
		Прогресс = Прогресс + 1;
		УстановитьСтатус(НСтр("ru='Формирование исходников шаблона расширения'"));
		ПодключитьОбработчикОжидания("СоздатьРасширениеСТестами_ФормированиеИсходниковРасширения", 0.1, Истина);
	Исключение
		Освободить();
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаСозданияРасширенияСТестами", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами_ФормированиеИсходниковРасширения()
	Попытка
		// Сохранить макет расширения во временный файл
		ИмяФайлаМакетаРасширения = ПолучитьИмяВременногоФайла();
		Сообщить(ИмяФайлаМакетаРасширения);
		ПромежуточныеДанные.Добавить(ИмяФайлаМакетаРасширения);
		Ждать ШаблонРасширенияДляТестов.ЗаписатьАсинх(ИмяФайлаМакетаРасширения);
		
		// Подключить временный файл как расширение во временную базу
		Ждать ПодключитьШаблонРасширения(ИмяФайлаМакетаРасширения);
		
		// Создаем каталог исходников расширения
		БазовыйКаталог = ПолучитьИмяВременногоФайла();
		ПутьККаталогуИсходников = ПутьКОбъекту(БазовыйКаталог, ИмяРасширения);
		ПромежуточныеДанные.Добавить(ПутьККаталогуИсходников);
		
		// Выгрузить исходный код расширения
		Ждать ВыгрузитьИсходныйКодШаблонаРасширения();
		
		// Удалить ConfigDumpInfo.xml
		ПутьКConfigDumpInfo = ПутьКОбъекту(ПутьККаталогуИсходников, "ConfigDumpInfo.xml");
		Ждать УдалитьФайлыАсинх(ПутьКConfigDumpInfo);
		
		Прогресс = Прогресс + 1;
		УстановитьСтатус(НСтр("ru='Формирование шаблона описания модуля с тестом'"));
		ПодключитьОбработчикОжидания("СоздатьРасширениеСТестами_ФормированиеШаблонаОписанияМодуляСТестом", 0.1, Истина);
	Исключение
		Освободить();
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаСозданияРасширенияСТестами", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами_ФормированиеШаблонаОписанияМодуляСТестом()
	Попытка
		// Сформировать шаблон описания модуля с тестом
		// * Ищем файл описания общего модуля, который не является захваченным из основной конфигурации.
		ПутьККаталогуОбщихМодулей = ПутьКОбъекту(ПутьККаталогуИсходников, "CommonModules");
		ФайлыОписанийМодулей = Ждать НайтиФайлыАсинх(ПутьККаталогуОбщихМодулей, "*.xml");
		ПолноеИмяИсходногоФайла = "";
		Для Каждого ФайлОписанияМодуля Из ФайлыОписанийМодулей Цикл
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			Ждать ТекстовыйДокумент.ПрочитатьАсинх(ФайлОписанияМодуля.ПолноеИмя);
			ТекстМодуля = ТекстовыйДокумент.ПолучитьТекст();
			Если СтрНайти(ТекстМодуля, "<ObjectBelonging>Adopted</ObjectBelonging>") = 0 Тогда
				ПолноеИмяИсходногоФайла = ФайлОписанияМодуля.ПолноеИмя;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ПустаяСтрока(ПолноеИмяИсходногоФайла) Тогда
			ВызватьИсключение НСтр("ru='Невозможно сформировать шаблон модуля с тестом'");
		КонецЕсли;
		
		// * Вставляем имена переменных
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		Ждать ТекстовыйДокумент.ПрочитатьАсинх(ПолноеИмяИсходногоФайла);
		ТекстШаблонаОписанияМодуля = ТекстовыйДокумент.ПолучитьТекст();
		ЗаменитьВхожденияНаПеременную(ТекстШаблонаОписанияМодуля, "<CommonModule uuid=""", "<CommonModule uuid=""&&GUID0&&"">");
		ЗаменитьВхожденияНаПеременную(ТекстШаблонаОписанияМодуля, "<Name>", "<Name>&&ModuleName&&</Name>");
		ЗаменитьВхожденияНаПеременную(ТекстШаблонаОписанияМодуля, "<v8:content>", "<v8:content>&&ModuleName&&</v8:content>");
		
		// Вставляем имена переменных в Configuration.xml
		ПолноеИмяИсходногоФайла = ПутьКОбъекту(ПутьККаталогуИсходников, "Configuration.xml");
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		Ждать ТекстовыйДокумент.ПрочитатьАсинх(ПолноеИмяИсходногоФайла);
		ТекстШаблонаОписанияКонфигурации = ТекстовыйДокумент.ПолучитьТекст();
		ЗаменитьВхожденияНаПеременную(ТекстШаблонаОписанияКонфигурации, "<Configuration uuid=""", "<Configuration uuid=""&&GUID0&&"">");
		ЗаменитьВхожденияНаПеременную(ТекстШаблонаОписанияКонфигурации, "<Name>", "<Name>&&Name&&</Name>");
		ЗаменитьВхожденияНаПеременную(ТекстШаблонаОписанияКонфигурации, "<v8:content>", "<v8:content>&&Name&&</v8:content>");
		ЗаменитьВхожденияНаПеременную(ТекстШаблонаОписанияКонфигурации, "<Version>", "<Version>&&Version&&</Version>");
		ЗаменитьВхожденияНаСписокПеременных(ТекстШаблонаОписанияКонфигурации, "<xr:ObjectId>", "<xr:ObjectId>&&GUID&&</xr:ObjectId>", "GUID");
		Состав = СтрРазделить(ТекстШаблонаОписанияКонфигурации, Символы.ПС);
		ИндексВставки = -1;
		Для Сч = 1 По Состав.Количество() Цикл
			Если СтрНайти(Состав[Сч - 1], "<ChildObjects>") > 0 Тогда
				ИндексВставки = Сч;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ИндексВставки <> -1 Тогда
			Состав.Вставить(ИндексВставки, "&&Modules&&");
		КонецЕсли;
		ТекстШаблонаОписанияКонфигурации = СтрСоединить(Состав, Символы.ПС);
		ТекстовыйДокумент.УстановитьТекст(ТекстШаблонаОписанияКонфигурации);
		ТекстовыйДокумент.ЗаписатьАсинх(ПолноеИмяИсходногоФайла);
		
		Прогресс = Прогресс + 1;
		УстановитьСтатус(НСтр("ru='Формирование модулей с тестами'"));
		ПодключитьОбработчикОжидания("СоздатьРасширениеСТестами_ФормированиеМодулейСТестами", 0.1, Истина);
	Исключение
		Освободить();
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаСозданияРасширенияСТестами", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами_ФормированиеМодулейСТестами()
	Попытка
		Для Каждого МодульТестирования Из МодулиТестирования Цикл
			ОписаниеМодуляТестирования = СтрЗаменить(ТекстШаблонаОписанияМодуля, "&&ModuleName&&", МодульТестирования.Имя);
			ИмяФайлаОписаниеМодуляТестирования = ПутьКОбъекту(ПутьККаталогуОбщихМодулей, МодульТестирования.Имя + ".xml");
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			ТекстовыйДокумент.УстановитьТекст(ОписаниеМодуляТестирования);
			Ждать ТекстовыйДокумент.ЗаписатьАсинх(ИмяФайлаОписаниеМодуляТестирования);
			
			ПутьККаталогуМодуля = ПутьКОбъекту(ПутьККаталогуОбщихМодулей, МодульТестирования.Имя + ",Ext");
			Ждать СоздатьКаталогАсинх(ПутьККаталогуМодуля);
			
			ИмяФайлаМодуляТестирования = ПутьКОбъекту(
				ПутьККаталогуМодуля,
				"Module.bsl"
			);
			ТекстовыйДокумент.УстановитьТекст(МодульТестирования.Текст);
			Ждать ТекстовыйДокумент.ЗаписатьАсинх(ИмяФайлаМодуляТестирования);
		КонецЦикла;
		
		Прогресс = Прогресс + 1;
		УстановитьСтатус(НСтр("ru='Дополнительная обработка исходников расширения'"));
		ПодключитьОбработчикОжидания("СоздатьРасширениеСТестами_ДополнительнаяОбработкаИсходников", 0.1, Истина);
	Исключение
		Освободить();
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаСозданияРасширенияСТестами", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами_ДополнительнаяОбработкаИсходников()
	Попытка
		// Вставка в xml файлы значений переменных (переменные задаются конструкцией "&&<Имя переменной>&&")
		Модули = Новый Массив;
		Тесты = Новый Массив;
		ШаблонОписанияМодуля = "			<CommonModule>%1</CommonModule>";
		ШаблонОписанияТеста = "				<xr:Item xsi:type=""xr:MDObjectRef"">CommonModule.%1</xr:Item>";
		Для Каждого МодульТестирования Из МодулиТестирования Цикл
			Модули.Добавить(СтрШаблон(ШаблонОписанияМодуля, МодульТестирования.Имя));
			Тесты.Добавить(СтрШаблон(ШаблонОписанияТеста, МодульТестирования.Имя));
		КонецЦикла;
		ОбщиеПеременные = Новый Соответствие;
		ОбщиеПеременные.Вставить("&&Name&&", ИмяРасширения);
		ОбщиеПеременные.Вставить("&&Version&&", ВерсияРасширения);
		ОбщиеПеременные.Вставить("&&Modules&&", СтрСоединить(Модули, Символы.ПС));
		ОбщиеПеременные.Вставить("&&Tests&&", СтрСоединить(Тесты, Символы.ПС));
		
		ФайлыОписаний = Ждать НайтиФайлыАсинх(ПутьККаталогуИсходников, "*.xml", Истина);
		Для Каждого ФайлОписания Из ФайлыОписаний Цикл
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			Ждать ТекстовыйДокумент.ПрочитатьАсинх(ФайлОписания.ПолноеИмя);
			Описание = ТекстовыйДокумент.ПолучитьТекст();
			
			Для Каждого ОбщаяПеременная Из ОбщиеПеременные Цикл
				Описание = СтрЗаменить(Описание, ОбщаяПеременная.Ключ, ОбщаяПеременная.Значение);
			КонецЦикла;
			
			ГУИДы = ГУИДыИзОписания(Описание);
			Для Каждого ГУИД Из ГУИДы Цикл
				Описание = СтрЗаменить(Описание, ГУИД.Ключ, ГУИД.Значение);
			КонецЦикла;
			
			ТекстовыйДокумент.УстановитьТекст(Описание);
			Ждать ТекстовыйДокумент.ЗаписатьАсинх(ФайлОписания.ПолноеИмя);
		КонецЦикла;
		
		Прогресс = Прогресс + 1;
		УстановитьСтатус(НСтр("ru='Загрузка исходников расширения во временную информационную базу'"));
		ПодключитьОбработчикОжидания("СоздатьРасширениеСТестами_ЗагрузкаИсходниковВоВременнуюБазу", 0.1, Истина);
	Исключение
		Освободить();
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаСозданияРасширенияСТестами", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами_ЗагрузкаИсходниковВоВременнуюБазу()
	Попытка
		Ждать ЗагрузитьИсходныйКодРасширения();
		
		Прогресс = Прогресс + 1;
		УстановитьСтатус(НСтр("ru='Сохранение расширения с тестами в файл'"));
		ПодключитьОбработчикОжидания("СоздатьРасширениеСТестами_СохранениеРасширенияСТестамиВФайле", 0.1, Истина);
	Исключение
		Освободить();
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаСозданияРасширенияСТестами", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами_СохранениеРасширенияСТестамиВФайле()
	Попытка
		Ждать СохранитьРасширениеВФайл();
		
		Прогресс = Прогресс + 1;
		ПодключитьОбработчикОжидания("СоздатьРасширениеСТестами_Завершение", 0.1, Истина);
	Исключение
		Освободить();
		Инфо = ИнформацияОбОшибке();
		Оповестить("ОшибкаСозданияРасширенияСТестами", Инфо.Описание);
	КонецПопытки;
КонецФункции

&НаКлиенте
Процедура СоздатьРасширениеСТестами_Завершение()
	Освободить();
	Оповестить("УспешноеЗавершениеСозданияРасширенияСТестами");
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВхожденияНаПеременную(ТекстШаблона, СтрокаПоиска, СтрокаЗамены)
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстШаблона);
	Состав = Новый Массив;
	Для Сч = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		АнализируемаяСтрока = ТекстовыйДокумент.ПолучитьСтроку(Сч);
		Позиция = СтрНайти(АнализируемаяСтрока, СтрокаПоиска);
		Если Позиция = 0 Тогда
			Состав.Добавить(АнализируемаяСтрока);
		Иначе
			НоваяСтрока = Лев(АнализируемаяСтрока, Позиция - 1) + СтрокаЗамены;
			Состав.Добавить(НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	ТекстШаблона = СтрСоединить(Состав, Символы.ПС);
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВхожденияНаСписокПеременных(ТекстШаблона, СтрокаПоиска, СтрокаЗамены, ИмяПеременной)
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстШаблона);
	Состав = Новый Массив;
	НомерПеременной = 1;
	Для Сч = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		АнализируемаяСтрока = ТекстовыйДокумент.ПолучитьСтроку(Сч);
		Позиция = СтрНайти(АнализируемаяСтрока, СтрокаПоиска);
		Если Позиция = 0 Тогда
			Состав.Добавить(АнализируемаяСтрока);
		Иначе
			ИмяВставляемойПеременной = ИмяПеременной + Формат(НомерПеременной, "ЧГ=");
			НоваяСтрока = 
				Лев(АнализируемаяСтрока, Позиция - 1)
				+ СтрЗаменить(СтрокаЗамены, ИмяПеременной, ИмяВставляемойПеременной)
			;
			НомерПеременной = НомерПеременной + 1;
			Состав.Добавить(НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	ТекстШаблона = СтрСоединить(Состав, Символы.ПС);
КонецПроцедуры

&НаКлиенте
Функция ГУИДыИзОписания(Описание)
	Результат = Новый Соответствие;
	ЗначащиеСимволы = "АБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдежзийклмнопрстуфхцчшщъыьэюяABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789";
	
	НачалоБлока = "&&GUID";
	КонецБлока = "&&";
	НачальнаяПозицияПоиска = 1;
	Пока Истина Цикл
		ПозицияНачала = СтрНайти(Описание, НачалоБлока, НаправлениеПоиска.СНачала, НачальнаяПозицияПоиска);
		Если ПозицияНачала = 0 Тогда
			Прервать;
		КонецЕсли;
		
		НачальнаяПозицияПоиска = ПозицияНачала + СтрДлина(НачалоБлока);
		
		ПозицияКонца = СтрНайти(Описание, КонецБлока, НаправлениеПоиска.СНачала, ПозицияНачала + СтрДлина(НачалоБлока));
		Если ПозицияКонца = 0 Или ПозицияКонца - ПозицияНачала > 10 Тогда
			Продолжить;
		КонецЕсли;
		
		ИсходнаяСтрокаНомерГУИДа = Сред(
			Описание, 
			ПозицияНачала + СтрДлина(НачалоБлока),
			ПозицияКонца - ПозицияНачала - СтрДлина(НачалоБлока)
		);
		
		СоставНомера = Новый Массив;
		Для Сч = 1 По СтрДлина(ИсходнаяСтрокаНомерГУИДа) Цикл
			Символ = Сред(ИсходнаяСтрокаНомерГУИДа, Сч, 1);
			Если СтрНайти(ЗначащиеСимволы, Символ) = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СоставНомера.Добавить(Символ);
		КонецЦикла;
		НомерГУИДа = СтрСоединить(СоставНомера, "");
		
		ГУИД = СтрШаблон("&&GUID%1&&", НомерГУИДа);
		Результат.Вставить(ГУИД, Новый УникальныйИдентификатор());
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область _Подготовить

&НаКлиенте
Асинх Функция Подготовить()
	Если ПустаяСтрока(ПутьКФайлуЗапуска1С) Тогда
		ТекстИсключения = НСтр("ru='Не указан путь к файлу запуска платформы ""1С:Предприятия 8""'");
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	Файл = Новый Файл(ПутьКФайлуЗапуска1С);
	ФайлСуществует = Ждать Файл.СуществуетАсинх();
	Если Не ФайлСуществует Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Файл по пути ""%1"" не обнаружен'"), 
			ПутьКФайлуЗапуска1С
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	Если ВидИнформационнойБазы = 0 Тогда
		Ждать ПодготовитьВременнуюИнформационнуюБазу();
	Иначе
		Ждать ПодготовитьСуществующуюИнформационнуюБазу();
	КонецЕсли;
КонецФункции

&НаКлиенте
Асинх Функция ПодготовитьВременнуюИнформационнуюБазу()
	Если ПустаяСтрока(ПутьКВременнойФайловойИБ) Тогда
		ПутьКВременнойФайловойИБ = ПолучитьИмяВременногоФайла();
	КонецЕсли;
	Ждать УдалитьФайлыАсинх(ПутьКВременнойФайловойИБ);
	Ждать СоздатьКаталогАсинх(ПутьКВременнойФайловойИБ);
	
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить("""" + ПутьКФайлуЗапуска1С + """");
	СоставСтрокиЗапуска.Добавить("CREATEINFOBASE");
	СтрокаСоединенияКИБ = СтрШаблон("File=""%1""", ПутьКВременнойФайловойИБ);
	СоставСтрокиЗапуска.Добавить(СтрокаСоединенияКИБ);
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить("/Out " + ИмяФайлаОтветаКонфигуратора);
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	
	Файл = Новый Файл(ИмяФайлаОтветаКонфигуратора);
	ФайлСуществует = Ждать Файл.СуществуетАсинх();
	Если Не ФайлСуществует Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Не удалось запустить файл ""%1""'"),
			ПутьКФайлуЗапуска1С
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	
	Если КодВозврата <> 0 Тогда
		Ждать УдалитьФайлыАсинх(ПутьКВременнойФайловойИБ);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при создании временной информационной базы ""%1"": %2'"),
			ПутьКВременнойФайловойИБ,
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	СтрокаСоединения = СтрокаСоединения(СтрокаСоединенияКИБ);
КонецФункции

&НаКлиенте
Асинх Функция ПодготовитьСуществующуюИнформационнуюБазу()
	СтрокаСоединения = СтрокаСоединения(
		СтрокаСоединенияКСуществующейИБ,
		ПользовательСуществующейИБ,
		ПарольПользователяСуществующейИБ
	);
	
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрокаСоединения);
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить("/DumpDBCfgList -AllExtensions /Out """ + ИмяФайлаОтветаКонфигуратора + """");
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	
	Если КодВозврата = 0 Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(ОтветКонфигуратора);
		Для Сч = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
			Если ВРег(ИмяРасширения) = ВРег(ТекстовыйДокумент.ПолучитьСтроку(Сч)) Тогда
				ТекстИсключения = СтрШаблон(
					НСтр("ru='Информационную базу ""%1"" невозможно использовать для работы с расширением ""%2"" т.к. она уже содержит расширение с таким же именем'"),
					СтрокаСоединенияКСуществующейИБ,
					ИмяРасширения
				);
				
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка проверки информационной базы ""%1"": %2'"),
			СтрокаСоединения,
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область _Освободить

&НаКлиенте
Асинх Функция Освободить()
	Если ВидИнформационнойБазы = 0 Тогда
		Ждать УдалитьФайлыАсинх(ПутьКВременнойФайловойИБ);
	Иначе
		Ждать ОсвободитьСуществующуюИнформационнуюБазу();
	КонецЕсли;
	Если ПромежуточныеДанные <> Неопределено Тогда
		Для Каждого ФайлКУдалению Из ПромежуточныеДанные Цикл
			Ждать УдалитьФайлыАсинх(ФайлКУдалению);
		КонецЦикла;
	КонецЕсли;
КонецФункции

// TODO 2025-07-04: Реализовать
&НаКлиенте
Асинх Функция ОсвободитьСуществующуюИнформационнуюБазу()
	Результат = Неопределено;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область _ТестыИзРасширения

&НаКлиенте
Асинх Функция ЗагрузитьРасширениеИзФайла(ИмяФайлаРасширения)
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрокаСоединения);
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/LoadCfg ""%1"" -Extension %2",
		ИмяФайлаРасширения,
		ИмяРасширения
	));
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить("/Out " + ИмяФайлаОтветаКонфигуратора);
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	
	Если КодВозврата <> 0 Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при загрузке файла расширения: %1'"),
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
КонецФункции

&НаКлиенте
Асинх Функция ВыгрузитьРасширениеВИсходныеКоды(КаталогИсходныхКодов)
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрокаСоединения);
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/DumpConfigToFiles ""%1"" -Extension %2",
		КаталогИсходныхКодов,
		ИмяРасширения
	));
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить("/Out " + ИмяФайлаОтветаКонфигуратора);
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	
	Если КодВозврата <> 0 Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при выгрузке расширения в исходные коды: %1'"),
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область _Общие

&НаКлиенте
Асинх Функция ОтветКонфигуратора(ИмяФайлаОтвета)
	Результат = "";
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	Ждать ТекстовыйДокумент.ПрочитатьАсинх(ИмяФайлаОтвета);
	Результат = ТекстовыйДокумент.ПолучитьТекст();
	Ждать УдалитьФайлыАсинх(ИмяФайлаОтвета);
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СтрокаСоединения(СтрокаСоединенияКИБ, ИмяПользователя = "", Пароль = "")
	Результат = "";
	
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить("""" + ПутьКФайлуЗапуска1С + """");
	СоставСтрокиЗапуска.Добавить("DESIGNER");
	СоставСтрокиЗапуска.Добавить("/IBConnectionString");
	СоставСтрокиЗапуска.Добавить("""" + СтрЗаменить(СтрокаСоединенияКИБ, """", """""") + """");
	Если Не ПустаяСтрока(ИмяПользователя) Тогда
		СоставСтрокиЗапуска.Добавить("/N" + ИмяПользователя);
		Если Не ПустаяСтрока(ПарольПользователяСуществующейИБ) Тогда
			СоставСтрокиЗапуска.Добавить("/P" + Пароль);
		КонецЕсли;
	КонецЕсли;
	СоставСтрокиЗапуска.Добавить("/DisableStartupDialogs");
	
	Результат = СтрСоединить(СоставСтрокиЗапуска, " ");
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПутьКОбъекту(ПутьКБазовомуКаталогу, ИменаОбъектов)
	СоставПутиКЦелевомуОбъекту = СтрРазделить(ИменаОбъектов, ",");
	СоставПутиКЦелевомуОбъекту.Вставить(0, ПутьКБазовомуКаталогу);
	Результат = СтрСоединить(СоставПутиКЦелевомуОбъекту, ПолучитьРазделительПути());
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Асинх Функция ВыполнитьКомандуКонфигуратора(СоставСтрокиЗапуска, ТекстОшибки = Неопределено)
	Если ТекстОшибки = Неопределено Тогда
		ТекстОшибки = "Ошибка";
	КонецЕсли;
	СоставСтрокиЗапуска.Вставить(0, ОсновнаяСтрокаЗапуска);
	
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/Out ""%1""",
		ИмяФайлаОтветаКонфигуратора
	));
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	Если КодВозврата <> 0 Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='%1: %2'"),
			ТекстОшибки,
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура УстановитьСтатус(НовыйСтатус)
	Элементы.Статус.Заголовок = НовыйСтатус;
КонецПроцедуры

#КонецОбласти

#Область _РаботаСКофигуратором

&НаКлиенте
Асинх Функция ПодключитьШаблонРасширения(ИмяФайлаРасширения)
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/LoadCfg ""%1"" -Extension ""%2""",
		ИмяФайлаРасширения,
		ИмяРасширения
	));
	
	Ждать ВыполнитьКомандуКонфигуратора(
		СоставСтрокиЗапуска,
		"Ошибка при загрузке шаблона расширения"
	);
КонецФункции

&НаКлиенте
Асинх Функция ВыгрузитьИсходныйКодШаблонаРасширения()
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/DumpConfigToFiles ""%1"" -Extension ""%2""",
		ПутьККаталогуИсходников,
		ИмяРасширения
	));
	
	Ждать ВыполнитьКомандуКонфигуратора(
		СоставСтрокиЗапуска,
		"Ошибка при выгрузке исходников шаблона расширения"
	);
КонецФункции

&НаКлиенте
Асинх Функция ЗагрузитьИсходныйКодРасширения()
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/LoadConfigFromFiles ""%1"" -AllExtensions",
		БазовыйКаталог
	));
	
	Ждать ВыполнитьКомандуКонфигуратора(
		СоставСтрокиЗапуска,
		"Ошибка при загрузке исходников расширения"
	);
КонецФункции

&НаКлиенте
Асинх Функция СохранитьРасширениеВФайл()
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/DumpCfg ""%1"" -Extension ""%2""",
		ПолныйПутьКФайлуРасширения,
		ИмяРасширения
	));
	
	Ждать ВыполнитьКомандуКонфигуратора(
		СоставСтрокиЗапуска,
		"Ошибка при сохранении расширения в файл"
	);
КонецФункции

#КонецОбласти

#КонецОбласти
