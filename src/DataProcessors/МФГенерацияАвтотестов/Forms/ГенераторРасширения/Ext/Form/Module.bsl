//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОписаниеПеременных

&НаКлиенте
Перем ПутьКФайлуЗапуска1С;

&НаКлиенте
Перем ВидИнформационнойБазы;

&НаКлиенте
Перем СтрокаСоединенияКСуществующейИБ;

&НаКлиенте
Перем ПользовательСуществующейИБ;

&НаКлиенте
Перем ПарольПользователяСуществующейИБ;

&НаКлиенте
Перем ПутьКВременнойФайловойИБ;

&НаКлиенте
Перем ИмяРасширения;

&НаКлиенте
Перем ВерсияРасширения;

&НаКлиенте
Перем СтрокаСоединения;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Функция НовыйПараметрыИнициализации() Экспорт
	Результат = Новый Структура;
	
	ИменаПараметров = СтрРазделить(ИменаПараметровГенерацииРасширения, ",");
	Для Каждого ИмяПараметра Из ИменаПараметров Цикл
		Результат.Вставить(ИмяПараметра, Неопределено);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Инициализировать(ПараметрыИнициализации) Экспорт
	ПутьКФайлуЗапуска1С = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ПутьКФайлуЗапуска1С");
	ВидИнформационнойБазы = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ВидИнформационнойБазы");
	СтрокаСоединенияКСуществующейИБ = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "СтрокаСоединенияКСуществующейИБ");
	ПользовательСуществующейИБ = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ПользовательСуществующейИБ");
	ПарольПользователяСуществующейИБ = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ПарольПользователяСуществующейИБ");
	ПутьКВременнойФайловойИБ = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ПутьКВременнойФайловойИБ");
	ИмяРасширения = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ИмяРасширения", "");
	ВерсияРасширения = ЗначениеПараметраИнициализации(ПараметрыИнициализации, "ВерсияРасширения", "");
КонецПроцедуры

&НаКлиенте
Асинх Функция ПроверитьВозможностьИспользования() Экспорт
	Ждать Подготовить();
	Ждать Освободить();
КонецФункции

// СвойстваРасширения - Структура
&НаКлиенте
Асинх Функция ДополнитьСвойстваРасширения(СвойстваРасширения, ИмяФайлаРасширения) Экспорт
	Ждать Подготовить();
	КаталогИсходныхКодовРасширения = ПолучитьИмяВременногоФайла();
	ПромежуточныеДанные = Новый Массив;
	ПромежуточныеДанные.Добавить(КаталогИсходныхКодовРасширения);
	Попытка
		Ждать ЗагрузитьРасширениеИзФайла(ИмяФайлаРасширения);
		Ждать ВыгрузитьРасширениеВИсходныеКоды(КаталогИсходныхКодовРасширения);
	Исключение
		Ждать Освободить(ПромежуточныеДанные);
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение Инфо.Описание;
	КонецПопытки;
	
	ПутьКФайлуМанифест = ПутьКОбъекту(
		КаталогИсходныхКодовРасширения,
		"CommonTemplates,MANIFEST,Ext,Template.txt"
	);
	
	ФайлМанифеста = Новый Файл(ПутьКФайлуМанифест);
	ФайлМанифестаСуществует = Ждать ФайлМанифеста.СуществуетАсинх();
	Если Не ФайлМанифестаСуществует Тогда
		//Ждать Освободить(ПромежуточныеДанные);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Расширение ""%1"" не содержит данных о тестах'"),
			ИмяФайлаРасширения
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	Ждать ТекстовыйДокумент.ПрочитатьАсинх(ПутьКФайлуМанифест);
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстовыйДокумент.ПолучитьТекст());
	Попытка
		Манифест = ПрочитатьJSON(ЧтениеJSON, Ложь);
		СвойстваРасширения.Вставить("ВерсияПодсистемы", Манифест["version"]);
		Тесты = Новый Массив;
		Для Каждого Элемент Из Манифест["tests"] Цикл
			Тесты.Добавить(Новый Структура(
				"ИмяФормы, ИмяМодуля",
				Элемент["form"],
				Элемент["module"]
			));
		КонецЦикла;
		СвойстваРасширения.Вставить("Тесты", Тесты);
	Исключение
		Инфо = ИнформацияОбОшибке();
		Ждать Освободить(ПромежуточныеДанные);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при чтении данных о тестах в расширении ""%1"" (Общий макет ""MANIFEST""): %2'"),
			ИмяФайлаРасширения,
			Инфо.Описание
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	Ждать Освободить(ПромежуточныеДанные);
КонецФункции

&НаКлиенте
Асинх Функция СоздатьРасширениеСТестами(ПолныйПутьКФайлуРасширения, МодулиТестирования) Экспорт
	// 1. Проверить и подготовить информационную базу
	Ждать Подготовить();
	
	// 2. Создаем каталог исходников расширения
	БазовыйКаталог = ПолучитьИмяВременногоФайла();
	
	СоставИмениКаталога = Новый Массив;
	СоставИмениКаталога.Добавить(БазовыйКаталог);
	СоставИмениКаталога.Добавить(ИмяРасширения);
	
	ПутьККаталогуИсходников = СтрСоединить(СоставИмениКаталога, ПолучитьРазделительПутиКлиента());
	КаталогИсходников = Новый Файл(ПутьККаталогуИсходников);
	
	Ждать СоздатьКаталогАсинх(ПутьККаталогуИсходников);
	
	// 3. Формируем исходники расширения
	// 3.1. Распаковать шаблон исходников в каталог исходников
	Поток = Новый ПотокВПамяти;
	Запись = Новый ЗаписьДанных(Поток);
	Ждать Запись.ЗаписатьАсинх(ШаблонРасширения);
	Ждать Запись.ЗакрытьАсинх();
	Архив = Новый ЧтениеZipФайла(Поток);
	Архив.ИзвлечьВсе(ПутьККаталогуИсходников, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	
	// 3.2. Формирование общих модулей с тестами
	ПутьККаталогуОбщихМодулей = ПутьКОбъекту(ПутьККаталогуИсходников, "CommonModules");
	Для Каждого МодульТестирования Из МодулиТестирования Цикл
		ОписаниеМодуляТестирования = СтрЗаменить(ШаблонМодуляСТестом, "&&ModuleName&&", МодульТестирования.Имя);
		ИмяФайлаОписаниеМодуляТестирования = ПутьКОбъекту(ПутьККаталогуОбщихМодулей, МодульТестирования.Имя + ".xml");
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(ОписаниеМодуляТестирования);
		Ждать ТекстовыйДокумент.ЗаписатьАсинх(ИмяФайлаОписаниеМодуляТестирования);
		
		ПутьККаталогуМодуля = Ждать СоздатьКаталогИсходника(
			ПутьККаталогуОбщихМодулей,
			МодульТестирования.Имя + ",Ext"
		);
		ИмяФайлаМодуляТестирования = ПутьКОбъекту(
			ПутьККаталогуМодуля,
			"Module.bsl"
		);
		ТекстовыйДокумент.УстановитьТекст(МодульТестирования.Текст);
		Ждать ТекстовыйДокумент.ЗаписатьАсинх(ИмяФайлаМодуляТестирования);
	КонецЦикла;
	
	// 3.3. Вставка в xml файлы значений переменных (переменные задаются конструкцией "&&<Имя переменной>&&")
	Модули = Новый Массив;
	Тесты = Новый Массив;
	ШаблонОписанияМодуля = "			<CommonModule>%1</CommonModule>";
	ШаблонОписанияТеста = "				<xr:Item xsi:type=""xr:MDObjectRef"">CommonModule.%1</xr:Item>";
	Для Каждого МодульТестирования Из МодулиТестирования Цикл
		Модули.Добавить(СтрШаблон(ШаблонОписанияМодуля, МодульТестирования.Имя));
		Тесты.Добавить(СтрШаблон(ШаблонОписанияТеста, МодульТестирования.Имя));
	КонецЦикла;
	ОбщиеПеременные = Новый Соответствие;
	ОбщиеПеременные.Вставить("&&Name&&", ИмяРасширения);
	ОбщиеПеременные.Вставить("&&Version&&", ВерсияРасширения);
	ОбщиеПеременные.Вставить("&&Modules&&", СтрСоединить(Модули, Символы.ПС));
	ОбщиеПеременные.Вставить("&&Tests&&", СтрСоединить(Тесты, Символы.ПС));
	
	ФайлыОписаний = Ждать НайтиФайлыАсинх(ПутьККаталогуИсходников, "*.xml", Истина);
	Для Каждого ФайлОписания Из ФайлыОписаний Цикл
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		Ждать ТекстовыйДокумент.ПрочитатьАсинх(ФайлОписания.ПолноеИмя);
		Описание = ТекстовыйДокумент.ПолучитьТекст();
		
		Для Каждого ОбщаяПеременная Из ОбщиеПеременные Цикл
			Описание = СтрЗаменить(Описание, ОбщаяПеременная.Ключ, ОбщаяПеременная.Значение);
		КонецЦикла;
		
		ГУИДы = ГУИДыИзОписания(Описание);
		Для Каждого ГУИД Из ГУИДы Цикл
			Описание = СтрЗаменить(Описание, ГУИД.Ключ, ГУИД.Значение);
		КонецЦикла;
		
		ТекстовыйДокумент.УстановитьТекст(Описание);
		Ждать ТекстовыйДокумент.ЗаписатьАсинх(ФайлОписания.ПолноеИмя);
	КонецЦикла;
	
	// 4. Загрузка исходников расширения во временную базу
	Если ВидИнформационнойБазы = 0 Тогда
		ОсновнаяСтрокаЗапуска = СтрокаСоединения(СтрШаблон(
			"File=""%1""",
			ПутьКВременнойФайловойИБ
		));
	Иначе
		ОсновнаяСтрокаЗапуска = СтрокаСоединения(
			СтрокаСоединенияКСуществующейИБ,
			ПользовательСуществующейИБ,
			ПарольПользователяСуществующейИБ
		);
	КонецЕсли;
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(ОсновнаяСтрокаЗапуска);
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/LoadConfigFromFiles ""%1"" -AllExtensions",
		БазовыйКаталог
	));
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/Out ""%1""",
		ИмяФайлаОтветаКонфигуратора
	));
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	Если КодВозврата <> 0 Тогда
		Ждать УдалениеДанныхДляРаботыСРасширением(ПутьККаталогуИсходников);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при загрузке исходников расширения: %1'"),
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	// 5. Выгрузка расширения
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(ОсновнаяСтрокаЗапуска);
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/DumpCfg ""%1"" -Extension ""%2""",
		ПолныйПутьКФайлуРасширения,
		ИмяРасширения
	));
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/Out ""%1""",
		ИмяФайлаОтветаКонфигуратора
	));
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	ОтветКонфигуратора = ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	Если КодВозврата <> 0 Тогда
		Ждать УдалениеДанныхДляРаботыСРасширением(ПутьККаталогуИсходников);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при выгрузке расширения: %1'"),
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	// 6. Удаляем временные данные
	Ждать УдалениеДанныхДляРаботыСРасширением(БазовыйКаталог);
	Ждать Освободить();
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИменаПараметровГенерацииРасширения = Обработки.МФГенерацияАвтотестов.ИменаПараметровГенерацииРасширения();
	ШаблонРасширения = Обработки.МФГенерацияАвтотестов.ПолучитьМакет("ШаблонРасширения");
	ШаблонМодуляСТестом = Обработки.МФГенерацияАвтотестов.ПолучитьМакет("ШаблонМодуляСТестом").ПолучитьТекст();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Инициализировать

&НаКлиенте
Функция ЗначениеПараметраИнициализации(ПараметрыИнициализации, ИмяПараметра, ЗначениеПоУмолчанию = Неопределено)
	Результат = ЗначениеПоУмолчанию;
	
	Если ПараметрыИнициализации.Свойство(ИмяПараметра) Тогда
		Результат = ПараметрыИнициализации[ИмяПараметра];
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область Подготовить

&НаКлиенте
Асинх Функция Подготовить()
	Если ВидИнформационнойБазы = 0 Тогда
		Ждать ПодготовитьВременнуюИнформационнуюБазу();
	Иначе
		Ждать ПодготовитьСуществующуюИнформационнуюБазу();
	КонецЕсли;
КонецФункции

&НаКлиенте
Асинх Функция ПодготовитьВременнуюИнформационнуюБазу()
	Если ПустаяСтрока(ПутьКВременнойФайловойИБ) Тогда
		ПутьКВременнойФайловойИБ = ПолучитьИмяВременногоФайла();
	КонецЕсли;
	Ждать УдалитьФайлыАсинх(ПутьКВременнойФайловойИБ);
	Ждать СоздатьКаталогАсинх(ПутьКВременнойФайловойИБ);
	
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить("""" + ПутьКФайлуЗапуска1С + """");
	СоставСтрокиЗапуска.Добавить("CREATEINFOBASE");
	СтрокаСоединенияКИБ = СтрШаблон("File=""%1""", ПутьКВременнойФайловойИБ);
	СоставСтрокиЗапуска.Добавить(СтрокаСоединенияКИБ);
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить("/Out " + ИмяФайлаОтветаКонфигуратора);
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	
	Файл = Новый Файл(ИмяФайлаОтветаКонфигуратора);
	ФайлСуществует = Ждать Файл.СуществуетАсинх();
	Если Не ФайлСуществует Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Не удалось запустить файл ""%1""'"),
			ПутьКФайлуЗапуска1С
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	
	Если КодВозврата <> 0 Тогда
		Ждать УдалитьФайлыАсинх(ПутьКВременнойФайловойИБ);
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при создании временной информационной базы ""%1"": %2'"),
			ПутьКВременнойФайловойИБ,
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	СтрокаСоединения = СтрокаСоединения(СтрокаСоединенияКИБ);
КонецФункции

&НаКлиенте
Асинх Функция ПодготовитьСуществующуюИнформационнуюБазу()
	СтрокаСоединения = СтрокаСоединения(
		СтрокаСоединенияКСуществующейИБ,
		ПользовательСуществующейИБ,
		ПарольПользователяСуществующейИБ
	);
	
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрокаСоединения);
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить("/DumpDBCfgList -AllExtensions /Out """ + ИмяФайлаОтветаКонфигуратора + """");
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	
	Если КодВозврата = 0 Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(ОтветКонфигуратора);
		Для Сч = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
			Если ВРег(ИмяРасширения) = ВРег(ТекстовыйДокумент.ПолучитьСтроку(Сч)) Тогда
				ТекстИсключения = СтрШаблон(
					НСтр("ru='Информационную базу ""%1"" невозможно использовать для работы с расширением ""%2"" т.к. она уже содержит расширение с таким же именем'"),
					СтрокаСоединенияКСуществующейИБ,
					ИмяРасширения
				);
				
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка проверки информационной базы ""%1"": %2'"),
			СтрокаСоединения,
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область Освободить

&НаКлиенте
Асинх Функция Освободить(Знач ПромежуточныеДанные = Неопределено)
	Если ВидИнформационнойБазы = 0 Тогда
		Ждать УдалитьФайлыАсинх(ПутьКВременнойФайловойИБ);
	Иначе
		Ждать ОсвободитьСуществующуюИнформационнуюБазу();
	КонецЕсли;
	Если ПромежуточныеДанные = Неопределено Тогда
		ПромежуточныеДанные = Новый Массив;
	КонецЕсли;
	Для Каждого ФайлКУдалению Из ПромежуточныеДанные Цикл
		// TODO 2025-07-07: Снять комментарий в конце разработки
		Ждать УдалитьФайлыАсинх(ФайлКУдалению);
	КонецЦикла;
КонецФункции

// TODO 2025-07-04: Реализовать
&НаКлиенте
Асинх Функция ОсвободитьСуществующуюИнформационнуюБазу()
	Результат = Неопределено;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область ТестыИзРасширения

&НаКлиенте
Асинх Функция ЗагрузитьРасширениеИзФайла(ИмяФайлаРасширения)
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрокаСоединения);
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/LoadCfg ""%1"" -Extension %2",
		ИмяФайлаРасширения,
		ИмяРасширения
	));
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить("/Out " + ИмяФайлаОтветаКонфигуратора);
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	
	Если КодВозврата <> 0 Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при загрузке файла расширения: %1'"),
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
КонецФункции

&НаКлиенте
Асинх Функция ВыгрузитьРасширениеВИсходныеКоды(КаталогИсходныхКодов)
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить(СтрокаСоединения);
	СоставСтрокиЗапуска.Добавить(СтрШаблон(
		"/DumpConfigToFiles ""%1"" -Extension %2",
		КаталогИсходныхКодов,
		ИмяРасширения
	));
	ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
	СоставСтрокиЗапуска.Добавить("/Out " + ИмяФайлаОтветаКонфигуратора);
	СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
	КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
	
	ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
	
	Если КодВозврата <> 0 Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Ошибка при выгрузке расширения в исходные коды: %1'"),
			ОтветКонфигуратора
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область СоздатьРасширение

&НаКлиенте
Асинх Функция УдалениеДанныхДляРаботыСРасширением(ПутьККаталогуИсходников)
	Ждать УдалитьФайлыАсинх(ПутьККаталогуИсходников);
	Если ВидИнформационнойБазы = 0 Тогда
		Ждать УдалитьФайлыАсинх(ПутьКВременнойФайловойИБ);
	Иначе
		ИмяФайлаОтветаКонфигуратора = ПолучитьИмяВременногоФайла();
		СоставСтрокиЗапуска = Новый Массив;
		СоставСтрокиЗапуска.Добавить(СтрокаСоединения(
			СтрокаСоединенияКСуществующейИБ,
			ПользовательСуществующейИБ,
			ПарольПользователяСуществующейИБ
		));
		СоставСтрокиЗапуска.Добавить(СтрШаблон(
			"/DeleteCfg -Extension ""%1""",
			ИмяРасширения
		));
		СоставСтрокиЗапуска.Добавить(СтрШаблон(
			"/Out ""%1""",
			ИмяФайлаОтветаКонфигуратора
		));
		СтрокаЗапуска = СтрСоединить(СоставСтрокиЗапуска, " ");
		КодВозврата = Ждать ЗапуститьПриложениеАсинх(СтрокаЗапуска, , Истина);
		ОтветКонфигуратора = Ждать ОтветКонфигуратора(ИмяФайлаОтветаКонфигуратора);
		Если КодВозврата <> 0 Тогда
			ТекстИсключения = СтрШаблон(
				НСтр("ru='Ошибка при удалении расширения ""%1"" из информационной базы ""%2"": %3. Необходимо удалить его вручную.'"),
				ИмяРасширения,
				СтрокаСоединенияКСуществующейИБ,
				ОтветКонфигуратора
			);
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаКлиенте
Функция ГУИДыИзОписания(Описание)
	Результат = Новый Соответствие;
	ЗначащиеСимволы = "АБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдежзийклмнопрстуфхцчшщъыьэюяABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789";
	
	НачалоБлока = "&&GUID";
	КонецБлока = "&&";
	НачальнаяПозицияПоиска = 1;
	Пока Истина Цикл
		ПозицияНачала = СтрНайти(Описание, НачалоБлока, НаправлениеПоиска.СНачала, НачальнаяПозицияПоиска);
		Если ПозицияНачала = 0 Тогда
			Прервать;
		КонецЕсли;
		
		НачальнаяПозицияПоиска = ПозицияНачала + СтрДлина(НачалоБлока);
		
		ПозицияКонца = СтрНайти(Описание, КонецБлока, НаправлениеПоиска.СНачала, ПозицияНачала + СтрДлина(НачалоБлока));
		Если ПозицияКонца = 0 Или ПозицияКонца - ПозицияНачала > 10 Тогда
			Продолжить;
		КонецЕсли;
		
		ИсходнаяСтрокаНомерГУИДа = Сред(
			Описание, 
			ПозицияНачала + СтрДлина(НачалоБлока),
			ПозицияКонца - ПозицияНачала - СтрДлина(НачалоБлока)
		);
		
		СоставНомера = Новый Массив;
		Для Сч = 1 По СтрДлина(ИсходнаяСтрокаНомерГУИДа) Цикл
			Символ = Сред(ИсходнаяСтрокаНомерГУИДа, Сч, 1);
			Если СтрНайти(ЗначащиеСимволы, Символ) = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СоставНомера.Добавить(Символ);
		КонецЦикла;
		НомерГУИДа = СтрСоединить(СоставНомера, "");
		
		ГУИД = СтрШаблон("&&GUID%1&&", НомерГУИДа);
		Результат.Вставить(ГУИД, Новый УникальныйИдентификатор());
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область _Общие

&НаКлиенте
Асинх Функция ОтветКонфигуратора(ИмяФайлаОтвета)
	Результат = "";
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	Ждать ТекстовыйДокумент.ПрочитатьАсинх(ИмяФайлаОтвета);
	Результат = ТекстовыйДокумент.ПолучитьТекст();
	Ждать УдалитьФайлыАсинх(ИмяФайлаОтвета);
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СтрокаСоединения(СтрокаСоединенияКИБ, ИмяПользователя = "", Пароль = "")
	Результат = "";
	
	СоставСтрокиЗапуска = Новый Массив;
	СоставСтрокиЗапуска.Добавить("""" + ПутьКФайлуЗапуска1С + """");
	СоставСтрокиЗапуска.Добавить("DESIGNER");
	СоставСтрокиЗапуска.Добавить("/IBConnectionString");
	СоставСтрокиЗапуска.Добавить("""" + СтрЗаменить(СтрокаСоединенияКИБ, """", """""") + """");
	Если Не ПустаяСтрока(ИмяПользователя) Тогда
		СоставСтрокиЗапуска.Добавить("/N" + ИмяПользователя);
		Если Не ПустаяСтрока(ПарольПользователяСуществующейИБ) Тогда
			СоставСтрокиЗапуска.Добавить("/P" + Пароль);
		КонецЕсли;
	КонецЕсли;
	СоставСтрокиЗапуска.Добавить("/DisableStartupDialogs");
	
	Результат = СтрСоединить(СоставСтрокиЗапуска, " ");
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПутьКОбъекту(ПутьКБазовомуКаталогу, ИменаОбъектов)
	СоставПутиКЦелевомуОбъекту = СтрРазделить(ИменаОбъектов, ",");
	СоставПутиКЦелевомуОбъекту.Вставить(0, ПутьКБазовомуКаталогу);
	Результат = СтрСоединить(СоставПутиКЦелевомуОбъекту, ПолучитьРазделительПути());
	
	Возврат Результат;
КонецФункции

// Создает подкаталог в каталоге исходников расширения
//
// Параметры:
//  ПутьКБазовомуКаталогу - Строка - Путь к базовому каталогу для построения;
//  ИменаКаталогов - Строка - Путь к целевому каталогу, разделителем является символ запятой ",". Например:
//    "Subsystems,ОбщийМодуль,Ext";
//
&НаКлиенте
Асинх Функция СоздатьКаталогИсходника(ПутьКБазовомуКаталогу, ИменаКаталогов)
	ПутьКЦелевомуКаталогу = ПутьКОбъекту(ПутьКБазовомуКаталогу, ИменаКаталогов);
	Результат = Ждать СоздатьКаталогАсинх(ПутьКЦелевомуКаталогу);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецОбласти
