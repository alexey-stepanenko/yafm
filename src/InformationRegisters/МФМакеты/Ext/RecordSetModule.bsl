//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025-2026 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	Если ЭтотОбъект.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеИмениФормыКоллекцииМетаданных = МФИнструментыРазработчикаПовтИсп
		.СоответствиеИмениФормыКоллекцииМетаданных()
	;
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.ФормаСуществует = Ложь;
		Запись.МакетСуществует = Ложь;
		Запись.МакетСовпадает = Истина;
		
		// Проверка существования формы
		СоставИмениФормы = СтрРазделить(Запись.ИмяФормы, ".");
		ИмяКоллекции = СоставИмениФормы[0];
		Коллекция = Неопределено;
		Если СоответствиеИмениФормыКоллекцииМетаданных.Свойство(ИмяКоллекции, Коллекция) Тогда
			РезультатПоискаФормы = Неопределено;
			Если Коллекция.ЭтоОбщий Тогда
				ИмяФормы = СоставИмениФормы[1];
				РезультатПоискаФормы = Коллекция.Коллекция.Найти(ИмяФормы);
			Иначе
				ИмяОбъекта = СоставИмениФормы[1];
				ИмяФормы = СоставИмениФормы[СоставИмениФормы.Количество() - 1];
				ОбъектМетаданных = Коллекция.Коллекция.Найти(ИмяОбъекта);
				Если ОбъектМетаданных = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				РезультатПоискаФормы = ОбъектМетаданных.Формы.Найти(ИмяФормы);
			КонецЕсли;
			Если РезультатПоискаФормы <> Неопределено Тогда
				Запись.ФормаСуществует = Истина;
			КонецЕсли;
		КонецЕсли;
		
		// Проверка существования макета
		ТекстОшибки = "";
		МакетОписанияМодификаций = Новый ТекстовыйДокумент;
		Запись.МакетСуществует = МФСлужебный.МакетОписанияМодификацийСуществует(
			Запись.ИмяМакета, 
			МакетОписанияМодификаций, 
			ТекстОшибки
		);
		
		Если Не Запись.МакетСуществует Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверка совпадения макетов
		Если Запись.ФормаСуществует Тогда
			Запись.МакетСовпадает = (МакетОписанияМодификаций.ПолучитьТекст() = Запись.Макет.Получить());
		Иначе
			Запись.МакетСовпадает = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
