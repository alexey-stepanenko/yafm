//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция МакетОписанияМодификаций(ИмяФормы, ИмяМакетаОписанияМодификаций) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МФМакеты.Макет КАК Макет
	               |ИЗ
	               |	РегистрСведений.МФМакеты КАК МФМакеты
	               |ГДЕ
	               |	МФМакеты.ИмяФормы = &ИмяФормы
	               |	И МФМакеты.ИмяМакета = &ИмяМакета";
	Запрос.УстановитьПараметр("ИмяФормы", ИмяФормы);
	Запрос.УстановитьПараметр("ИмяМакета", ИмяМакетаОписанияМодификаций);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ТекстШаблона = ВыборкаДетальныеЗаписи.Макет.Получить();
		Результат.УстановитьТекст(ТекстШаблона);
	Иначе
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИмяФормы.Установить(ИмяФормы);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИмяФормы = ИмяФормы;
		НоваяЗапись.ИмяМакета = ИмяМакетаОписанияМодификаций;
		НоваяЗапись.Макет = Новый ХранилищеЗначения("", Новый СжатиеДанных(9));
		
		НаборЗаписей.Записать();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
КонецФункции

Функция КэшФорм() Экспорт
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МФМакеты.ИмяФормы КАК ИмяФормы,
	               |	МФМакеты.ИмяМакета КАК ИмяМакета
	               |ИЗ
	               |	РегистрСведений.МФМакеты КАК МФМакеты
	               |ГДЕ
	               |	МФМакеты.ФормаСуществует
	               |			И МФМакеты.МакетСуществует";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат.Вставить(
			ВыборкаДетальныеЗаписи.ИмяФормы,
			ВыборкаДетальныеЗаписи.ИмяМакета
		);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Процедура ОбновитьМакетОписанияМодификаций(ИмяФормы, ИмяМакета, ТекстМакета) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();
	Сжатие = Новый СжатиеДанных(9);
	Запись.ИмяФормы = ИмяФормы;
	Запись.ИмяМакета = ИмяМакета;
	Запись.Макет = Новый ХранилищеЗначения(
		ТекстМакета,
		Сжатие
	);
	Запись.Записать();
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

Функция ВсеДанныеВКэшеАктуальны() Экспорт
	Результат = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(Подзапрос.Количество) КАК Количество
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		КОЛИЧЕСТВО(МФМакеты.ИмяФормы) КАК Количество
	               |	ИЗ
	               |		РегистрСведений.МФМакеты КАК МФМакеты
	               |	ГДЕ
	               |		НЕ МФМакеты.ФормаСуществует
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		КОЛИЧЕСТВО(МФМакеты.ИмяФормы)
	               |	ИЗ
	               |		РегистрСведений.МФМакеты КАК МФМакеты
	               |	ГДЕ
	               |		НЕ МФМакеты.МакетСуществует
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		КОЛИЧЕСТВО(МФМакеты.ИмяФормы)
	               |	ИЗ
	               |		РегистрСведений.МФМакеты КАК МФМакеты
	               |	ГДЕ
	               |		НЕ МФМакеты.МакетСовпадает) КАК Подзапрос";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = (ВыборкаДетальныеЗаписи.Количество = 0);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции








//Функция ИменаФорм() Экспорт
Функция ИменаФорм()
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МФМакеты.ИмяФормы КАК ИмяФормы
	               |ИЗ
	               |	РегистрСведений.МФМакеты КАК МФМакеты";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат.Добавить(ВыборкаДетальныеЗаписи.ИмяФормы);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// TODO 2025-03-07: проверить
Функция МакетыОписанияМодификаций() Экспорт
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса_МакетыОписанияМодификаций();
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстМакета = ВыборкаДетальныеЗаписи.Макет.Получить();
		Результат.Вставить(
			ВыборкаДетальныеЗаписи.ИмяМакета,
			ТекстМакета
		);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// TODO 2025-03-07: проверить
Процедура УдалитьМакетОписанияМодификаций(ИмяМакетаОписанияМодификаций) Экспорт
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИмяМакета.Установить(ИмяМакетаОписанияМодификаций);
	НаборЗаписей.Записать();
КонецПроцедуры

// TODO 2025-03-07: проверить
Функция ИменаМакетов() Экспорт
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса_ИменаМакетов();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат.Добавить(ВыборкаДетальныеЗаписи.ИмяМакета);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапроса_МакетыОписанияМодификаций()
	Возврат "ВЫБРАТЬ
	        |	МФМакеты.ИмяМакета КАК ИмяМакета,
	        |	МФМакеты.Макет КАК Макет
	        |ИЗ
	        |	РегистрСведений.МФМакеты КАК МФМакеты";
КонецФункции

Функция ТекстЗапроса_ИменаМакетов()
	Возврат "ВЫБРАТЬ
	        |	МФМакеты.ИмяМакета КАК ИмяМакета
	        |ИЗ
	        |	РегистрСведений.МФМакеты КАК МФМакеты
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	ИмяМакета";
КонецФункции

#КонецОбласти

#КонецЕсли
