//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ПустаяСтрока(Параметры.ЗначениеКопирования.ИмяФормы) Тогда
		ТекстМакета = РеквизитФормыВЗначение("Запись").Макет.Получить();
	Иначе
		ТекстМакета = РегистрыСведений.МФМакеты.МакетОписанияМодификаций(
			Запись.ИмяФормы,
			Запись.ИмяМакета
		).ПолучитьТекст();
		Запись.ИмяФормы = "";
		Запись.ФормаСуществует = Истина;
	КонецЕсли;
	ОбновитьОформлениеЭлементов();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Сжатие = Новый СжатиеДанных(9);
	ТекущийОбъект.Макет = Новый ХранилищеЗначения(
		ТекстМакета,
		Сжатие
	);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьОформлениеЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ТекстМакетаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьМакетИзКонфигурации(Команда)
	Если ПустаяСтрока(Запись.ИмяМакета) Тогда
		Возврат;
	КонецЕсли;
	
	МакетИзКонфигурации = Новый ТекстовыйДокумент;
	МакетСуществует = ПолучитьМакетИзКонфигурацииНаСервере(Запись.ИмяМакета, МакетИзКонфигурации);
	Если Не МакетСуществует Тогда
		ПоказатьПредупреждение(
			,
			СтрШаблон(НСтр("ru='Макет ""%1"" не найден!'"), Запись.ИмяМакета),
			,
			НСтр("ru='Ошибка'")
		);
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстМакета) Тогда
		ТекстМакета = МакетИзКонфигурации.ПолучитьТекст();
		Модифицированность = Истина;
	Иначе
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("МакетИзКонфигурации", МакетИзКонфигурации);
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПоказаВопросаОЗагрузкеМакета",
			ЭтаФорма,
			ДополнительныеПараметры
		);
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru='Текущий макет не пустой. Заменить макетом из конфигурации?'"),
			РежимДиалогаВопрос.ДаНет,
			30,
			КодВозвратаДиалога.Нет,
			// TODO 2025-04-06: Заменить заголовок на более подходящий
			НСтр("ru='Заменить'"),
			КодВозвратаДиалога.Нет
		);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьОформлениеЭлементов()
	Если Запись.ФормаСуществует Тогда
		Элементы.ГруппаИмяФормыОбщая.ЦветФона = Новый Цвет();
		Элементы.ИмяФормы.ЦветТекстаЗаголовка = Новый Цвет();
		Элементы.СтатусФормы.Видимость = Ложь;
	Иначе
		Элементы.ГруппаИмяФормыОбщая.ЦветФона = ЦветаСтиля.МФЦветОтсутствующихОбъектов;
		Элементы.ИмяФормы.ЦветТекстаЗаголовка = ЦветаСтиля.МФЦветВыделенныхНадписей;
		Элементы.СтатусФормы.Видимость = Истина;
	КонецЕсли;
	
	Если Не Запись.МакетСуществует Тогда
		Элементы.ГруппаИмяМакетаОбщая.ЦветФона = ЦветаСтиля.МФЦветОтсутствующихОбъектов;
		Элементы.ИмяМакета.ЦветТекстаЗаголовка = ЦветаСтиля.МФЦветВыделенныхНадписей;
		Элементы.СтатусМакета.Видимость = Истина;
		Элементы.СтатусМакета.Заголовок = НСтр("ru='Макет не существует'");
	ИначеЕсли Не Запись.МакетСовпадает Тогда
		Элементы.ГруппаИмяМакетаОбщая.ЦветФона = ЦветаСтиля.МФЦветРазличающихсяМакетов;
		Элементы.ИмяМакета.ЦветТекстаЗаголовка = ЦветаСтиля.МФЦветВыделенныхНадписей;
		Элементы.СтатусМакета.Видимость = Истина;
		Элементы.СтатусМакета.Заголовок = НСтр("ru='Макет в кэше отличается от макета в конфигурации'");
	Иначе
		Элементы.ГруппаИмяМакетаОбщая.ЦветФона = Новый Цвет();
		Элементы.ИмяМакета.ЦветТекстаЗаголовка = Новый Цвет();
		Элементы.СтатусМакета.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьМакетИзКонфигурацииНаСервере(ИмяМакета, Макет)
	Результат = Ложь;
	
	Результат = МФСлужебный.МакетОписанияМодификацийСуществует(
		ИмяМакета,
		Макет
	);
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПослеПоказаВопросаОЗагрузкеМакета(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекстМакета = ДополнительныеПараметры.МакетИзКонфигурации.ПолучитьТекст();
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти
