//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция РеквизитСуществуетВДанныхТестирования(ИмяФормы, ИмяРеквизита, Контекст) Экспорт
	Результат = Истина;
	
	ДанныеДляТестированияФормы = ДанныеДляТестированияФормы(ИмяФормы, Контекст);
	Если Не ДанныеДляТестированияФормы.Реквизиты.Свойство(ИмяРеквизита) Тогда
		Результат =Ложь;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ПолучитьСвойстваЭлементовНедоступныеНаКлиентеИзДанныхТестирования(АдресДанныхДляТестирования) Экспорт
	Результат = Неопределено;
	
	ДанныеДляТестирования = ПолучитьИзВременногоХранилища(АдресДанныхДляТестирования);
	Результат = ДанныеДляТестирования.СвойстваЭлементовНедоступныеНаКлиенте;
	
	Возврат Результат;
КонецФункции

Функция ДанныеТестированияСодержатЧисло(ИмяФормы, ИмяРеквизита, ДопустимыйЗнак, Разрядность, РазрядностьДробнойЧасти, Контекст) Экспорт
	Результат = Истина;
	
	ПроверяемыйТип = Новый ОписаниеТипов(
		"Число",
		Новый КвалификаторыЧисла(Разрядность, РазрядностьДробнойЧасти, ДопустимыйЗнак)
	);
	
	Результат = ДанныеТестированияСодержатТип(ИмяФормы, ИмяРеквизита, ПроверяемыйТип, Контекст);
	
	Возврат Результат;
КонецФункции

Функция ДанныеТестированияСодержатСтроку(ИмяФормы, ИмяРеквизита, СтратегияУправленияРесурсами, Длина, Контекст) Экспорт
	Результат = Истина;
	
	ПроверяемыйТип = Новый ОписаниеТипов(
		"Строка",
		,
		Новый КвалификаторыСтроки(Длина, СтратегияУправленияРесурсами)
	);
	
	Результат = ДанныеТестированияСодержатТип(ИмяФормы, ИмяРеквизита, ПроверяемыйТип, Контекст);
	
	Возврат Результат;
КонецФункции

Функция ДанныеТестированияСодержатДату(ИмяФормы, ИмяРеквизита, ВидДаты, Контекст) Экспорт
	Результат = Истина;
	
	ПроверяемыйТип = Новый ОписаниеТипов(
		"Дата",
		,
		,
		Новый КвалификаторыДаты(ВидДаты)
	);
	
	Результат = ДанныеТестированияСодержатТип(ИмяФормы, ИмяРеквизита, ПроверяемыйТип, Контекст);
	
	Возврат Результат;
КонецФункции

Функция ДанныеТестированияСодержатПрочийТип(ИмяФормы, ИмяРеквизита, ТипСтрокой, Контекст) Экспорт
	Результат = Истина;
	
	ПроверяемыйТип = Новый ОписаниеТипов(ТипСтрокой);
	
	Результат = ДанныеТестированияСодержатТип(ИмяФормы, ИмяРеквизита, ПроверяемыйТип, Контекст);
	
	Возврат Результат;
КонецФункции

Функция ЭтоСвойствоНедоступноеНаКлиенте(ТипЭлементаФормы, ИмяСвойства, ИмяСвойстваДляПолученияЗначения) Экспорт
	Результат = Ложь;
	
	Нашли = МФЭлементыФормыПовтИсп
		.ОписанияСвойств()
		.Получить(ТипЭлементаФормы)
		.СвойстваЭлементаНедоступныеНаКлиенте
		.Найти(ИмяСвойства)
	;
	
	Если Нашли <> Неопределено Тогда
		Результат = Истина;
		ИмяСвойстваДляПолученияЗначения = МФЭлементыФормыПовтИсп
			.ОписанияСвойств()
			.Получить(ТипЭлементаФормы)
			.СоответствиеСвойств
			.Получить(ИмяСвойства)
		;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляТестированияФормы(ИмяФормы, Контекст)
	Результат = Новый Структура;
	
	ИмяДанныеДляТестирования = МФТестированиеКлиентСерверПовтИсп.ИмяПараметраДанныеДляТестирования();
	ДанныеДляТестирования = Контекст[ИмяДанныеДляТестирования];
	АдресДанныхДляТестированияФормы = ДанныеДляТестирования.Получить(ИмяФормы);
	Если АдресДанныхДляТестированияФормы = Неопределено Тогда
		СтрокаИсключения = СтрШаблон(
			МФТестированиеКлиентСерверПовтИсп.ШаблонДанныеДляТестированияФормыНеОбнаружены(),
			ИмяФормы
		);
		
		ВызватьИсключение СтрокаИсключения;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(АдресДанныхДляТестированияФормы);
	
	Возврат Результат;
КонецФункции

Функция ДанныеТестированияСодержатТип(ИмяФормы, ИмяРеквизита, ПроверяемыйТип, Контекст)
	Результат = Истина;
	
	ДанныеДляТестированияФормы = ДанныеДляТестированияФормы(ИмяФормы, Контекст);
	Если Не ДанныеДляТестированияФормы.Реквизиты.Свойство(ИмяРеквизита) Тогда
		СтрокаИсключения = СтрШаблон(
			НСтр("ru='В данных для тестирования формы ""%1"" не обнаружена информация о типах реквизита ""%2""'"),
			ИмяФормы,
			ИмяРеквизита
		);
		
		ВызватьИсключение СтрокаИсключения;
	КонецЕсли;
	
	ОписаниеТиповРеквизита = ДанныеДляТестированияФормы.Реквизиты[ИмяРеквизита].ТипЗначения;
	
	ПроверитьСоставТипов(Результат, ПроверяемыйТип, ОписаниеТиповРеквизита);
	ПроверитьКвалификаторы(Результат, ПроверяемыйТип, ОписаниеТиповРеквизита, МФТестированиеКлиентСерверПовтИсп.ИмяКвалификаторовЧисла());
	ПроверитьКвалификаторы(Результат, ПроверяемыйТип, ОписаниеТиповРеквизита, МФТестированиеКлиентСерверПовтИсп.ИмяКвалификаторовСтроки());
	ПроверитьКвалификаторы(Результат, ПроверяемыйТип, ОписаниеТиповРеквизита, МФТестированиеКлиентСерверПовтИсп.ИмяКвалификаторовДаты());
	ПроверитьКвалификаторы(Результат, ПроверяемыйТип, ОписаниеТиповРеквизита, МФТестированиеКлиентСерверПовтИсп.ИмяКвалификаторовДвоичныхДанных());
	
	Возврат Результат;
КонецФункции

Процедура ПроверитьСоставТипов(Результат, ОписаниеПроверяемогоТипа, ОписаниеТиповРеквизита)
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из ОписаниеПроверяемогоТипа.Типы() Цикл
		Если Не ОписаниеТиповРеквизита.СодержитТип(Элемент) Тогда
			Результат = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПроверитьКвалификаторы(Результат, ОписаниеПроверяемогоТипа, ОписаниеТиповРеквизита, ИмяКвалификаторов)
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеКвалификаторов = МФТестированиеКлиентСерверПовтИсп.ОписаниеКвалификаторов();
	ОписаниеКвалификатора = ОписаниеКвалификаторов[ИмяКвалификаторов];
	Реквизиты = СтрРазделить(ОписаниеКвалификатора.СравниваемыеПоля, ",");
	Для Каждого Реквизит Из Реквизиты Цикл
		ПорядокСравнения = Новый Массив;
		Если ОписаниеКвалификатора.ПорядокСравнения.Свойство(Реквизит, ПорядокСравнения) Тогда
			ИндексЗначенияПроверяемогоТипа = ПорядокСравнения.Найти(ОписаниеПроверяемогоТипа[ИмяКвалификаторов][Реквизит]);
			Если ИндексЗначенияПроверяемогоТипа = Неопределено Тогда
				ИндексЗначенияПроверяемогоТипа = -1;
			КонецЕсли;
			ИндексЗначенияТиповРеквизита = ПорядокСравнения.Найти(ОписаниеТиповРеквизита[ИмяКвалификаторов][Реквизит]);
			Если ИндексЗначенияТиповРеквизита = Неопределено Тогда
				ИндексЗначенияТиповРеквизита = -1;
			КонецЕсли;
			Если
				(ИндексЗначенияПроверяемогоТипа = -1 И ИндексЗначенияТиповРеквизита = -1)
				Или (ИндексЗначенияПроверяемогоТипа > ИндексЗначенияТиповРеквизита)
			Тогда
				Результат = Ложь;
				Прервать;
			КонецЕсли;
		ИначеЕсли ОписаниеПроверяемогоТипа[ИмяКвалификаторов][Реквизит] > ОписаниеТиповРеквизита[ИмяКвалификаторов][Реквизит] Тогда
			Результат = Ложь;
			Возврат;
		Иначе
			// Пустой блок
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
