//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025-2026 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция Версия() Экспорт
	Возврат "1.1.1.6"; // ВЕРСИЯ
КонецФункции

Функция ОписаниеМодификаций(ИмяМакетаОписанияМодификаций, Протокол) Экспорт
	Попытка
		ТекстОшибки = "";
		МакетОписанияМодификаций = Новый ТекстовыйДокумент;
		МакетОписанияМодификацийСуществует = МакетОписанияМодификацийСуществует(
			ИмяМакетаОписанияМодификаций, 
			МакетОписанияМодификаций, 
			ТекстОшибки
		);
		ОбработатьМакетОписанияМодификацийДляРежимаРазработки(
			ИмяМакетаОписанияМодификаций,
			МакетОписанияМодификаций,
			Протокол
		);
		Если Не МакетОписанияМодификацийСуществует Тогда
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
	Исключение
		// BSLLS:NestedFunctionInParameters-off
		Инфо = ИнформацияОбОшибке();
		СтрокаПротокола = СтрШаблон(
			НСтр("ru='Ошибка получения макета по имени ""%1"": %2'"),
			ИмяМакетаОписанияМодификаций,
			Инфо.Описание
		);
		// BSLLS:NestedFunctionInParameters-on
		Протокол.ГенераторПротокола.ДобавитьСтрокуВПротокол(Протокол, СтрокаПротокола);
		
		ВызватьИсключение;
	КонецПопытки;
	
	Результат = ПолучитьОписаниеМодификацииПоМакету(МакетОписанияМодификаций, Протокол);
	
	Возврат Результат;
КонецФункции

// TODO 2026-01-24: Создать описание. МакетОписанияМодификаций - ТекстовыйДокумент
Функция ПолучитьОписаниеМодификацииПоМакету(МакетОписанияМодификаций, Протокол) Экспорт
	Результат = НовыйОписаниеМодификаций();
	
	ИндексСтрокиЗаголовка = 1;
	ОбработатьСтрокуЗаголовка(Результат, МакетОписанияМодификаций, ИндексСтрокиЗаголовка, Протокол);
	ОбработатьСтрокиМакета(Результат, МакетОписанияМодификаций, ИндексСтрокиЗаголовка, Протокол);
	
	Возврат Результат;
КонецФункции

Функция МакетОписанияМодификацийСуществует(ПолноеИмяМакета, Результат = Неопределено, ТекстОшибки = Неопределено) Экспорт
	Макет = Новый ТекстовыйДокумент;
	
	СимволРазделитель = ".";
	СоставИмениМакета = СтрРазделить(ПолноеИмяМакета, СимволРазделитель);
	ИмяКоллекции = СоставИмениМакета[0];
	
	ОписаниеКоллекции = МФПовтИсп.КоллекцииОбъектовМетаданных().Получить(ИмяКоллекции);
	Если ОписаниеКоллекции = Неопределено Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru='Коллекция метаданных с именем ""%1"" не обнаружена'"),
			ИмяКоллекции
		);
		
		Возврат Ложь;
	КонецЕсли;
	
	Если ОписаниеКоллекции.ЭтоОбщая Тогда
		Если СоставИмениМакета.Количество() <> 2 Тогда
			ТекстОшибки = НСтр("ru='Отсутствует имя общего макета'");
			
			Возврат Ложь;
		КонецЕсли;
		
		ИмяМакета = СоставИмениМакета[1];
		Если ОписаниеКоллекции.Коллекция.Найти(ИмяМакета) = Неопределено Тогда
			ТекстОшибки = СтрШаблон(
				НСтр("ru='Общий Макет ""%1"" не обнаружен'"),
				ИмяМакета
			);
			
			Возврат Ложь;
		КонецЕсли;
		
		Макет = ПолучитьОбщийМакет(ИмяМакета);
	Иначе
		Если СоставИмениМакета.Количество() <> 3 Тогда
			ТекстОшибки = НСтр("ru='Отсутствует имя объекта и имя макета'");
			
			Возврат Ложь;
		КонецЕсли;
		
		ИмяОбъектаМетаданных = СоставИмениМакета[1];
		ОбъектМетаданных = ОписаниеКоллекции.Коллекция.Найти(ИмяОбъектаМетаданных);
		Если ОбъектМетаданных = Неопределено Тогда
			ТекстОшибки = СтрШаблон(
				НСтр("ru='Объект метаданных ""%1.%2"" не обнаружен'"),
				ИмяКоллекции,
				ИмяОбъектаМетаданных
			);
			
			Возврат Ложь;
		КонецЕсли;
		
		ИмяМакета = СоставИмениМакета[2];
		Если ОбъектМетаданных.Макеты.Найти(ИмяМакета) = Неопределено Тогда
			ТекстОшибки = СтрШаблон(
				НСтр("ru='Макет ""%1"" объекта метаданных ""%2.%3"" не обнаружен'"),
				ИмяМакета,
				ИмяКоллекции,
				ИмяОбъектаМетаданных
			);
			
			Возврат Ложь;
		КонецЕсли;
		
		Менеджер = Новый (СтрЗаменить(ОбъектМетаданных.ПолноеИмя(), ".", "Менеджер."));
		Макет = Менеджер.ПолучитьМакет(ИмяМакета);
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		Результат = Макет;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Функция МакетОписанияМодификаций(ПолноеИмяМакета, Протокол) Экспорт
	Результат = Новый ТекстовыйДокумент;
	
	СимволРазделитель = ".";
	СоставИмениМакета = СтрРазделить(ПолноеИмяМакета, СимволРазделитель);
	ИмяКоллекции = СоставИмениМакета[0];
	
	ОписаниеКоллекции = МФПовтИсп.КоллекцииОбъектовМетаданных().Получить(ИмяКоллекции);
	Если ОписаниеКоллекции = Неопределено Тогда
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Коллекция метаданных с именем ""%1"" не обнаружена'"),
			ИмяКоллекции
		);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ОписаниеКоллекции.ЭтоОбщая Тогда
		Если СоставИмениМакета.Количество() <> 2 Тогда
			ТекстИсключения = НСтр("ru='Отсутствует имя общего макета'");
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ИмяМакета = СоставИмениМакета[1];
		Если ОписаниеКоллекции.Коллекция.Найти(ИмяМакета) = Неопределено Тогда
			ТекстИсключения = СтрШаблон(
				НСтр("ru='Общий Макет ""%1"" не обнаружен'"),
				ИмяМакета
			);
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		Результат = ПолучитьОбщийМакет(ИмяМакета);
	Иначе
		Если СоставИмениМакета.Количество() <> 3 Тогда
			ТекстИсключения = НСтр("ru='Отсутствует имя объекта и имя макета'");
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ИмяОбъектаМетаданных = СоставИмениМакета[1];
		ОбъектМетаданных = ОписаниеКоллекции.Коллекция.Найти(ИмяОбъектаМетаданных);
		Если ОбъектМетаданных = Неопределено Тогда
			ТекстИсключения = СтрШаблон(
				НСтр("ru='Объект метаданных ""%1.%2"" не обнаружен'"),
				ИмяКоллекции,
				ИмяОбъектаМетаданных
			);
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ИмяМакета = СоставИмениМакета[2];
		Если ОбъектМетаданных.Макеты.Найти(ИмяМакета) = Неопределено Тогда
			ТекстИсключения = СтрШаблон(
				НСтр("ru='Макет ""%1"" объекта метаданных ""%2.%3"" не обнаружен'"),
				ИмяМакета,
				ИмяКоллекции,
				ИмяОбъектаМетаданных
			);
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		Менеджер = Новый (СтрЗаменить(ОбъектМетаданных.ПолноеИмя(), ".", "Менеджер."));
		Результат = Менеджер.ПолучитьМакет(ИмяМакета);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Создает протокол модификации формы на основании генератора протокола
//
// Параметры:
//  ГенераторПротокола - ОбщийМодуль, Неопределено - Общий модуль, реализующий интерфейс генератора протокола модификации формы.
//    При указании значения Неопределено будет использован генератор по умолчанию (общий модуль МФГенераторПротокола)
//    При указании общего модуля он должен содержать следующие процедуры:
//    * ИнициализироватьПротокол
//    * ОбработатьОписаниеОшибки
//    * ДобавитьСтрокуВПротокол
//    * СформироватьТекстПротокола
//    См. дополнительно модуль МФГенераторПротокола.
//    При использовании генератора по умолчанию возможна настройка работы, см. дополнительно
//    общий модуль МФГенераторПротоколаПереопределяемый.
//
// Возвращаемое значение:
//  Структура
//   * ГенераторПротокола - ОбщийМодуль - Значение, переданное в параметре ГенераторПротокола функции.
//   * Форма - ФормаКлиентскогоПриложения - Модифицируемая форма.
//   * ДанныеПротокола - Массив из Строка - Содержимое протокола.
//   * ДополнительныеПараметры - Произвольный - Произвольные дополнительные данные.
//   * ПрерватьМодификациюПриОшибке - Булево - При возникновении первой ошибки процесс модификации
//       прерывается.
//   * ПрерватьВыводФормыПриОшибкахМодификации - Булево - При непустом содержимом данных протокола вывод формы
//       подавляется (параметр Отказ процедуры МодификацияФорм.МодифицироватьФорму устанавливается в Истина).
//
Функция Протокол(ГенераторПротокола) Экспорт
	Результат = НовыйПротокол();
	
	Если ГенераторПротокола = Неопределено Тогда
		ГенераторПротокола = МФГенераторПротокола;
	КонецЕсли;
	Результат.ГенераторПротокола = ГенераторПротокола;
	Результат.ГенераторПротокола.ИнициализироватьПротокол(Результат);
	
	Возврат Результат;
КонецФункции

// РЕЖИМ РАЗРАБОТКИ: &После("ОбработатьДанныеФормы")
// РЕЖИМ РАЗРАБОТКИ: Процедура Разработка_ОбработатьДанныеФормы(Форма, ОписаниеМодификаций)
// РЕЖИМ ТЕСТИРОВАНИЯ: &После("ОбработатьДанныеФормы")
// РЕЖИМ ТЕСТИРОВАНИЯ: Процедура Тестирование_ОбработатьДанныеФормы(Форма, ОписаниеМодификаций)
Процедура ОбработатьДанныеФормы(Форма, ОписаниеМодификаций) Экспорт
// РЕЖИМ РАЗРАБОТКИ: НАЧАЛО БЛОКА
	Если Форма.Параметры.Свойство(МФИнструментыРазработчикаКлиентСервер.ИмяПараметраГенерацияАвтотестов()) Тогда
		АдресДанныхДляГенерацииАвтотестов = Форма.Параметры[
			МФИнструментыРазработчикаКлиентСервер.ИмяПараметраАдресДанныхДляГенерацииАвтотестов()
		];
		
		ПоместитьВоВременноеХранилище(ОписаниеМодификаций, АдресДанныхДляГенерацииАвтотестов);
	КонецЕсли;
// РЕЖИМ РАЗРАБОТКИ: КОНЕЦ БЛОКА
// РЕЖИМ РАЗРАБОТКИ: КонецПроцедуры
// РЕЖИМ ТЕСТИРОВАНИЯ: НАЧАЛО БЛОКА
	Если Не Форма.Параметры.Свойство(МФТестированиеКлиентСерверПовтИсп.ИмяПараметраТестирования()) Тогда
		Возврат;
	КонецЕсли;
	
	АдресДанныхДляТестирования = Форма.Параметры[
		МФТестированиеКлиентСерверПовтИсп.ИмяПараметраАдресДанныхДляТестирования()
	];
	ДанныеДляТестирования = Новый Структура;
	
	//РеквизитыДляТестирования = Новый Структура;
	РеквизитыДляТестирования = Новый Соответствие;
	
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		ДобавитьРеквизитДляТестирования(РеквизитыДляТестирования, РеквизитФормы);
		
		Если РеквизитФормы.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений")) Тогда
			ВременнаяТаблица = Форма[РеквизитФормы.Имя].Выгрузить();
			ОбработатьТаблицуЗначенийДляТестирования(ВременнаяТаблица, РеквизитыДляТестирования, РеквизитФормы.Имя);
		Иначе
			Для Каждого ТипЗначенияРеквизита Из РеквизитФормы.ТипЗначения.Типы() Цикл
				ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначенияРеквизита);
				Если ОбъектМетаданных = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				// TODO 2026-01-21: Избавиться от Попытки
				Попытка
					ТабличныеЧасти = ОбъектМетаданных.ТабличныеЧасти;
				Исключение
					Продолжить;
				КонецПопытки;
				
				Для Каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
					Попытка
						ВременнаяТаблица = Форма[РеквизитФормы.Имя][ТабличнаяЧасть.Имя].Выгрузить();
					Исключение
						Продолжить;
					КонецПопытки;
					ОбработатьТаблицуЗначенийДляТестирования(
						ВременнаяТаблица,
						РеквизитыДляТестирования,
						СтрШаблон("%1.%2", РеквизитФормы.Имя, ТабличнаяЧасть.Имя)
					);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	СвойстваЭлементовНедоступныеНаКлиенте = Новый Структура;
	Для Каждого ЭлементФормы Из ОписаниеМодификаций.Элементы Цикл
		СвойстваЭлементаНедоступныеНаКлиенте = МФЭлементыФормыПовтИсп
			.ОписанияСвойств()
			.Получить(ЭлементФормы.Значение.ТипЭлемента)
			.СвойстваЭлементаНедоступныеНаКлиенте
		;
		Если СвойстваЭлементаНедоступныеНаКлиенте.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначенияСвойств = Новый Структура;
		ЗначениеСвойства = Неопределено;
		Для Каждого ИмяСвойства Из СвойстваЭлементаНедоступныеНаКлиенте Цикл
			Если Не ЭлементФормы.Значение.СвойстваЭлемента.Свойство(ИмяСвойства, ЗначениеСвойства) Тогда
				Продолжить;
			КонецЕсли;
			ЗначенияСвойств.Вставить(ИмяСвойства, ЗначениеСвойства);
		КонецЦикла;
		Если ЗначенияСвойств.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СвойстваЭлементовНедоступныеНаКлиенте.Вставить(ЭлементФормы.Ключ, ЗначенияСвойств);
	КонецЦикла;
	
	ДанныеДляТестирования.Вставить("Реквизиты", РеквизитыДляТестирования);
	ДанныеДляТестирования.Вставить("СвойстваЭлементовНедоступныеНаКлиенте", СвойстваЭлементовНедоступныеНаКлиенте);
	ПоместитьВоВременноеХранилище(ДанныеДляТестирования, АдресДанныхДляТестирования);
// РЕЖИМ ТЕСТИРОВАНИЯ: КОНЕЦ БЛОКА
// РЕЖИМ ТЕСТИРОВАНИЯ: КонецПроцедуры
КонецПроцедуры

// РЕЖИМ ТЕСТИРОВАНИЯ: НАЧАЛО БЛОКА

Процедура ОбработатьТаблицуЗначенийДляТестирования(ВременнаяТаблица, РеквизитыДляТестирования, ИмяРеквизитаФормы)
	Для Каждого КолонкаВременнойТаблицы Из ВременнаяТаблица.Колонки Цикл
		РеквизитДляТестирования = НовыйРеквизитДляТестирования();
		ЗаполнитьЗначенияСвойств(РеквизитДляТестирования, КолонкаВременнойТаблицы);
		РеквизитДляТестирования.Путь = ИмяРеквизитаФормы;
		РеквизитДляТестирования.Вставить("Имя", КолонкаВременнойТаблицы.Имя);
		ДобавитьРеквизитДляТестирования(РеквизитыДляТестирования, РеквизитДляТестирования);
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьРеквизитДляТестирования(РеквизитыДляТестирования, РеквизитФормы)
	РеквизитДляТестирования = НовыйРеквизитДляТестирования();
	ЗаполнитьЗначенияСвойств(РеквизитДляТестирования, РеквизитФормы);
	Ключ = РеквизитФормы.Имя;
	Если Не ПустаяСтрока(РеквизитФормы.Путь) Тогда
		Ключ = СтрШаблон("%1.%2", СокрЛП(РеквизитФормы.Путь), Ключ);
	КонецЕсли;
	РеквизитыДляТестирования.Вставить(Ключ, РеквизитДляТестирования);
КонецПроцедуры

Функция НовыйРеквизитДляТестирования()
	Результат = Новый Структура;
	
	Результат.Вставить("Заголовок", Неопределено);
	Результат.Вставить("Путь", Неопределено);
	Результат.Вставить("СохраняемыеДанные", Неопределено);
	Результат.Вставить("ТипЗначения", Неопределено);
	
	Возврат Результат;
КонецФункции
// РЕЖИМ ТЕСТИРОВАНИЯ: КОНЕЦ БЛОКА
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйПротокол()
	Результат = Новый Структура;
	Результат.Вставить("ГенераторПротокола", Неопределено);
	//Результат.Вставить("Форма", Неопределено);
	Результат.Вставить("ИмяФормы", Неопределено);
	Результат.Вставить("ДанныеПротокола", Неопределено);
	Результат.Вставить("ДополнительныеПараметры", Неопределено);
	Результат.Вставить("ПрерватьМодификациюПриОшибке", Неопределено);
	Результат.Вставить("ПрерватьВыводФормыПриОшибкахМодификации", Неопределено);
	
	Возврат Результат;
КонецФункции

#Область ОбработкаОшибок

Функция НовыйОписаниеОшибки()
	Результат = Новый Структура;
	
	Результат.Вставить("ДополнительнаяИнформация", Неопределено);
	Результат.Вставить("ИмяМодуля", Неопределено);
	Результат.Вставить("ИсходнаяСтрока", Неопределено);
	Результат.Вставить("Код", Неопределено);
	Результат.Вставить("НомерСтроки", Неопределено);
	Результат.Вставить("Описание", Неопределено);
	Результат.Вставить("НомерСтрокиМакета", Неопределено);
	Результат.Вставить("СтрокаМакета", Неопределено);
	
	Возврат Результат;
КонецФункции

Процедура ЗаполнитьОписаниеОшибкиИзИнформацииОбОшибке(ОписаниеОшибки, ИнформацияОбОшибке)
	ЗаполнитьЗначенияСвойств(ОписаниеОшибки, ИнформацияОбОшибке);
КонецПроцедуры

#КонецОбласти

#Область ОписаниеМодификаций

// Конструктор описания модификации формы по текстовому описанию
//
// Возвращаемое значение:
//   Структура:
//     * Версия - Строка - Версия описания. Сейчас может быть любым. Не анализируется.
//     * Команды - Структура - Описания команд формы.
//     * Рамки - Структура - Описания рамок.
//     * РазделительПолей - Строка - Символ-разделитель полей в описании модификации.
//     * Реквизиты - Структура - Описания реквизитов формы.
//     * СочетанияКлавиш - Структура - Описания сочетаний клавиш.
//     * Цвета - Структура - Описания цветов.
//     * Элементы - Структура - Описания добавляемых и/или изменяемых элементов формы.
//
Функция НовыйОписаниеМодификаций()
	Результат = Новый Структура;
	
	Результат.Вставить("Версия", "");
	
	Результат.Вставить("Картинки", Новый Структура);
	Результат.Вставить("Команды", Новый Структура);
	
	Результат.Вставить("Рамки", Новый Структура);
	Результат.Вставить("РазделительПолей", "");
	Результат.Вставить("Реквизиты", Новый Структура);
	
	Результат.Вставить("СочетанияКлавиш", Новый Структура);
	
	Результат.Вставить("Цвета", Новый Структура);
	
	Результат.Вставить("Элементы", Новый Структура);
	
	Возврат Результат;
КонецФункции

Процедура ОбработатьСтрокуЗаголовка(ОписаниеМодификаций, МакетОписанияМодификаций, ИндексСтрокиЗаголовка, Протокол)
	СтрокаЗаголовка = МакетОписанияМодификаций.ПолучитьСтроку(ИндексСтрокиЗаголовка);
	Попытка
		ЗаполнитьПараметрыМодификации(СтрокаЗаголовка, ОписаниеМодификаций);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = НовыйОписаниеОшибки();
		ЗаполнитьОписаниеОшибкиИзИнформацииОбОшибке(ОписаниеОшибки, ИнформацияОбОшибке);
		ОписаниеОшибки.НомерСтрокиМакета = ИндексСтрокиЗаголовка;
		ОписаниеОшибки.СтрокаМакета = СтрокаЗаголовка;
		Протокол.ГенераторПротокола.ОбработатьОписаниеОшибки(Протокол, ОписаниеОшибки);
		
		// TODO 2024-12-02: Добавить что? возможно забыл удалить, обработка ошибки уже есть
		// TODO 2024-11-20: Добавить
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

Процедура ЗаполнитьПараметрыМодификации(СтрокаЗаголовка, ОписаниеМодификаций)
	РазделительПолей = " "; // Пробел
	СоставСтрокиЗаголовка = СтрРазделить(СтрокаЗаголовка, РазделительПолей);
	
	ИндексПоляИдентификатора = 0;
	Если Не МФПовтИсп.ЭтоСтрокаЗаголовкаОписанияМодификаций(СоставСтрокиЗаголовка[ИндексПоляИдентификатора]) Тогда // BSLLS:LineLength-off
		// BSLLS:NestedFunctionInParameters-off
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Неправильный идентификатор заголовка описания модификации: ""%1""'"),
			СоставСтрокиЗаголовка[ИндексПоляИдентификатора]
		);
		// BSLLS:NestedFunctionInParameters-on
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	КоличествоПолейЗаголовка = 3;
	Если СоставСтрокиЗаголовка.Количество() <> КоличествоПолейЗаголовка Тогда
		// BSLLS:NestedFunctionInParameters-off
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Неправильное количество полей заголовка. Ожидалось: %1, обнаружено: %2'"),
			КоличествоПолейЗаголовка,
			СоставСтрокиЗаголовка.Количество()
		);
		// BSLLS:NestedFunctionInParameters-on
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ИндексПоляВерсии = 1;
	ОписаниеМодификаций.Версия = СоставСтрокиЗаголовка[ИндексПоляВерсии];
	
	ИндексПоляРазделитель = 2;
	КодСимволаРазделителя = МФПовтИсп
		.ТипНатуральноеЧисло()
		.ПривестиЗначение(СоставСтрокиЗаголовка[ИндексПоляРазделитель]);
	ОписаниеМодификаций.РазделительПолей = Символ(КодСимволаРазделителя);
КонецПроцедуры

Процедура ОбработатьСтрокиМакета(ОписаниеМодификаций, МакетОписанияМодификаций, ИндексСтрокиЗаголовка, Протокол)
	ИндексПервойСтрокиОписанияМодификаций = ИндексСтрокиЗаголовка + 1;
	Для Сч = ИндексПервойСтрокиОписанияМодификаций По МакетОписанияМодификаций.КоличествоСтрок() Цикл
		СтрокаДокумента = МакетОписанияМодификаций.ПолучитьСтроку(Сч);
		ОписаниеПолей = ОписаниеПолей(СтрокаДокумента, Сч, ОписаниеМодификаций.РазделительПолей);
		Попытка
			ОбработатьПоля(ОписаниеМодификаций, ОписаниеПолей);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = НовыйОписаниеОшибки();
			ЗаполнитьОписаниеОшибкиИзИнформацииОбОшибке(ОписаниеОшибки, ИнформацияОбОшибке);
			ОписаниеОшибки.НомерСтрокиМакета = Сч;
			ОписаниеОшибки.СтрокаМакета = СтрокаДокумента;
			Протокол.ГенераторПротокола.ОбработатьОписаниеОшибки(Протокол, ОписаниеОшибки);
			Если Протокол.ПрерватьМодификациюПриОшибке Тогда
				ВызватьИсключение;
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

Функция ОписаниеПолей(СтрокаДокумента, НомерСтроки, РазделительПолей)
	Поля = Новый Массив;
	ЭлементыСтроки = СтрРазделить(СтрокаДокумента, РазделительПолей, Ложь);
	Для Каждого Элемент Из ЭлементыСтроки Цикл
		Поля.Добавить(СокрЛП(Элемент));
	КонецЦикла;
	
	Возврат Поля;
КонецФункции

#КонецОбласти

#Область ОбработкаПолей

Процедура ОбработатьПоля(ОписаниеМодификаций, ОписаниеПолей)
	Если ОписаниеПолей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Идентификатор = МФОбработкаСтрокСлужебный.Идентификатор(ОписаниеПолей[0]);
	ОбработчикиТиповСтрок = МФПовтИсп.ОбработчикиТиповСтрокОписанияМодификации();
	МодульОбработки = ОбработчикиТиповСтрок.Получить(Идентификатор);
	Если МодульОбработки <> Неопределено Тогда
		МодульОбработки.Обработать(ОписаниеМодификаций, ОписаниеПолей);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область РежимРазработки

// РЕЖИМ РАЗРАБОТКИ: &После("ОбработатьМакетОписанияМодификацийДляРежимаРазработки")
// РЕЖИМ РАЗРАБОТКИ: Процедура Разработка_ОбработатьМакетОписанияМодификацийДляРежимаРазработки(ИмяМакетаОписанияМодификаций, МакетОписанияМодификаций, Протокол)
Процедура ОбработатьМакетОписанияМодификацийДляРежимаРазработки(ИмяМакетаОписанияМодификаций, МакетОписанияМодификаций, Протокол)
// РЕЖИМ РАЗРАБОТКИ: НАЧАЛО БЛОКА
	МакетОписанияМодификацийРазработка = РегистрыСведений
		.МФМакеты
		.МакетОписанияМодификаций(Протокол.ИмяФормы, ИмяМакетаОписанияМодификаций)
	;
	Если ПустаяСтрока(МакетОписанияМодификацийРазработка.ПолучитьТекст()) Тогда
		ТекстМакетаИзКонфигурации = МакетОписанияМодификаций.ПолучитьТекст();
		РегистрыСведений.МФМакеты.ОбновитьМакетОписанияМодификаций(
			Протокол.ИмяФормы,
			ИмяМакетаОписанияМодификаций,
			ТекстМакетаИзКонфигурации
		);
	Иначе
		МакетОписанияМодификаций.УстановитьТекст(МакетОписанияМодификацийРазработка.ПолучитьТекст());
	КонецЕсли;
// РЕЖИМ РАЗРАБОТКИ: КОНЕЦ БЛОКА
// РЕЖИМ РАЗРАБОТКИ: КонецПроцедуры
КонецПроцедуры

#КонецОбласти

#КонецОбласти
