//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025-2026 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция Идентификаторы() Экспорт
	Идентификаторы = Новый Массив;
	Идентификаторы.Добавить("КАРТИНКА");
	
	Возврат Идентификаторы;
КонецФункции

// Обработчик строки, описывающий картинку
//
// Параметры:
//  ОписаниеМодификаций - Структура - См. подробнее МодификацияФормСлужебный.НовыйОписаниеМодификаций()
//  Поля - Массив - значения полей из строки описания модификации формы. Ожидается минимум 3 элемента массива:
//    * [0] - Идентификатор строки. Может содержать значения из массива, возвращаемого функцией Идентификаторы().
//    * [1] - Имя картинки.
//    * [2] - Источник картинки:
//            * БиблиотекаКартинок, PictureLib
//    * [3] - Имя картинки в источнике
//
// Пример строки:
// КАРТИНКА	ПерваяКартинка	БиблиотекаКартинок	ШтрихКод
//
Процедура Обработать(ОписаниеМодификаций, Поля) Экспорт
	МинимальноеКоличествоПолей = 4;
	КоличествоПолей = Поля.Количество();
	МФОбработкаСтрокСлужебный.ПроверитьКоличествоПолейСтроки(КоличествоПолей, МинимальноеКоличествоПолей);
	
	СоставПолейСтроки = МФОбработкаСтрокСлужебный.СоставПолейСтроки(
		Поля, 
		"Имя,Источник,ИмяВИсточнике"
	);
	
	Значение = Неопределено;
	ИсточникКартинки = ВРег(СоставПолейСтроки.Источник);
	
	Если ЭтоБиблиотекаКартинок(ИсточникКартинки) Тогда
		Попытка
			Картинка = БиблиотекаКартинок[СоставПолейСтроки.ИмяВИсточнике];
		Исключение
			// BSLLS:NestedFunctionInParameters-off
			ТекстИсключения = СтрШаблон(
				НСтр("ru='Неизвестное имя картинки в библиотеке картинок: ""%1""'"),
				СоставПолейСтроки.ИмяВИсточнике
			);
			// BSLLS:NestedFunctionInParameters-on
			
			ВызватьИсключение ТекстИсключения;
		КонецПопытки;
		Значение = НовыйЗначениеКартинки();
		Значение.Источник = ИсточникКартинки;
		Значение.ИмяВИсточнике = СоставПолейСтроки.ИмяВИсточнике;
		Значение.Картинка = Картинка;
	Иначе
		// BSLLS:NestedFunctionInParameters-off
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Неизвестный тип источника картинки: ""%1""'"),
			СоставПолейСтроки.Источник
		);
		// BSLLS:NestedFunctionInParameters-on
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Описание = Новый Структура;
	Описание.Вставить("Имя", СоставПолейСтроки.Имя);
	Описание.Вставить("Значение", Значение);
	ОписаниеМодификаций.Картинки.Вставить(
		СоставПолейСтроки.Идентификатор,
		Описание
	);
КонецПроцедуры

Функция ЭтоБиблиотекаКартинок(Идентификатор) Экспорт
	Результат = (ИдентификаторыБиблиотекиКартинок().Найти(Идентификатор) <> Неопределено);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйЗначениеКартинки()
	Результат = Новый Структура;
	
	Результат.Вставить("Источник", Неопределено);
	Результат.Вставить("ИмяВИсточнике", Неопределено);
	Результат.Вставить("Картинка", Неопределено);
	
	Возврат Результат;
КонецФункции

Функция ИдентификаторыБиблиотекиКартинок()
	Результат = Новый Массив;
	
	Результат.Добавить(ВРег("БиблиотекаКартинок"));
	Результат.Добавить(ВРег("PictureLib"));
	
	Возврат Результат;
КонецФункции

#КонецОбласти
