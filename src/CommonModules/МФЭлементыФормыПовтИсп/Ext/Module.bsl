//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция СоответствиеИдентификаторовИТиповЭлементовФормы() Экспорт
	Результат = Новый Соответствие;
	
	Для Каждого Обработчик Из МФЭлементыФормыПовтИсп.ОбработчикиТиповЭлементовФормы() Цикл
		ТипЭлемента = Обработчик.ТипЭлементаФормы();
		Идентификаторы = Обработчик.Идентификаторы();
		Для Каждого Идентификатор Из Идентификаторы Цикл
			Результат.Вставить(Идентификатор, ТипЭлемента);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция ОписанияСвойств() Экспорт 
	Результат = ОписанияСвойствЭлементовФормы();
	ДополнитьОписаниеОбязательныхСвойств(Результат);
	
	Возврат Результат;
КонецФункции

#Область ТолькоДляВызоваИзЭтогоМодуля

Функция ОбработчикиТиповЭлементовФормы() Экспорт
	Результат = Новый Массив;
	
	Результат.Добавить(МФГруппаФормы);
	Результат.Добавить(МФДекорацияФормы);
	Результат.Добавить(МФКнопкаФормы);
	Результат.Добавить(МФПолеФормы);
	Результат.Добавить(МФТаблицаФормы);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписанияСвойствЭлементовФормы()
	Результат = Новый Соответствие;
	
	СинонимыСвойствЭлементов = СинонимыСвойствЭлементов();
	
	Для Каждого Обработчик Из МФЭлементыФормыПовтИсп.ОбработчикиТиповЭлементовФормы() Цикл
		ОписанияСвойств = МФЭлементыФормыСлужебный.НовыйОписаниеСвойствЭлемента();
		ЗависимостиСвойств = Обработчик.ЗависимостиСвойств();
		
		Обработчик.ЗаполнитьРазделОсновные(ОписанияСвойств, СинонимыСвойствЭлементов, ЗависимостиСвойств);
		Обработчик.ЗаполнитьРазделДанные(ОписанияСвойств, СинонимыСвойствЭлементов, ЗависимостиСвойств);
		Обработчик.ЗаполнитьРазделИспользование(ОписанияСвойств, СинонимыСвойствЭлементов, ЗависимостиСвойств);
		Обработчик.ЗаполнитьРазделОформление(ОписанияСвойств, СинонимыСвойствЭлементов, ЗависимостиСвойств);
		Обработчик.ЗаполнитьРазделРасположение(ОписанияСвойств, СинонимыСвойствЭлементов, ЗависимостиСвойств);
		Обработчик.ЗаполнитьРазделСобытия(ОписанияСвойств, СинонимыСвойствЭлементов, ЗависимостиСвойств);
		Обработчик.ЗаполнитьОбязательныеСвойства(ОписанияСвойств);
		
		Результат.Вставить(Обработчик.ТипЭлементаФормы(), ОписанияСвойств);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция СинонимыСвойствЭлементов()
	Результат = Новый Структура;
	Макет = ПолучитьОбщийМакет("МФСинонимыСвойствЭлементов");
	
	РазделительПолей = ";";
	Для Индекс = 2 По Макет.КоличествоСтрок() Цикл
		СтрокаМакета = Макет.ПолучитьСтроку(Индекс);
		Если СокрЛП(СтрокаМакета) = "" Тогда
			Продолжить;
		КонецЕсли;
		ОписаниеСвойства = СтрРазделить(СтрокаМакета, РазделительПолей);
		Ключ = ОписаниеСвойства[0];
		Синонимы = Новый Массив;
		Для Сч = 1 По ОписаниеСвойства.Количество() - 1 Цикл
			Синонимы.Добавить(ОписаниеСвойства[Сч]);
		КонецЦикла;
		Значение = СтрСоединить(Синонимы, ",");
		Результат.Вставить(Ключ, Значение);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Процедура ДополнитьОписаниеОбязательныхСвойств(ОписанияСвойствЭлементов)
	Для Каждого Элемент Из ОписанияСвойствЭлементов Цикл
		ОписаниеЭлемента = Элемент.Значение;
		Для Каждого СоответствиеСвойства Из ОписаниеЭлемента.СоответствиеСвойств Цикл
			Синоним = СоответствиеСвойства.Ключ;
			ОбязательноеСвойство = СоответствиеСвойства.Значение;
			ОписаниеОбязательногоСвойства = ОписаниеЭлемента.ОбязательныеСвойства.Получить(ОбязательноеСвойство);
			Если ОписаниеОбязательногоСвойства = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ОписаниеОбязательногоСвойства.Синонимы.Добавить(Синоним);
		КонецЦикла;
		
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
