//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция Идентификаторы() Экспорт
	Идентификаторы = Новый Массив;
	Идентификаторы.Добавить("ЭЛЕМЕНТ");
	
	Возврат Идентификаторы;
КонецФункции

// Обработчик строки, описывающий элемент формы
//
// Параметры:
//  ОписаниеМодификаций - Структура - См. подробнее МодификацияФормСлужебный.НовыйОписаниеМодификаций()
//  Поля - Массив - значения полей из строки описания модификации формы. Ожидается 3 элемента массива:
//    * [0] - Идентификатор строки. Может содержать значения из массива, возвращаемого функцией Идентификаторы().
//    * [1] - Имя элемента.
//    * [2] - Тип элемента:
//            * ДЕКОРАЦИЯФОРМЫ
//            * ГРУППАФОРМЫ
//            * КНОПКАФОРМЫ
//            * ТАБЛИЦАФОРМЫ
//            * ПОЛЕФОРМЫ
//
// Пример строки:
// ЭЛЕМЕНТ	Группа1	ГРУППАФОРМЫ
//
Процедура Обработать(ОписаниеМодификаций, Поля) Экспорт
	МинимальноеКоличествоПолей = 3;
	КоличествоПолей = Поля.Количество();
	МФОбработкаСтрокСлужебный.ПроверитьКоличествоПолейСтроки(КоличествоПолей, МинимальноеКоличествоПолей);
	
	СоставПолейСтроки = МФОбработкаСтрокСлужебный.СоставПолейСтроки(Поля, "Имя,ИмяТипаЭлемента");
	Попытка
		ИдентификаторТипаЭлемента = ВРег(СоставПолейСтроки.ИмяТипаЭлемента);
		СоответствиеИдентификаторовИТиповЭлементовФормы = МФЭлементыФормыПовтИсп
			.СоответствиеИдентификаторовИТиповЭлементовФормы()
		;
		
		ТипЭлемента = СоответствиеИдентификаторовИТиповЭлементовФормы.Получить(ИдентификаторТипаЭлемента);
		Если ТипЭлемента = Неопределено Тогда
			ВызватьИсключение "";
		КонецЕсли;
	Исключение
		// BSLLS:NestedFunctionInParameters-off
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Некорректный тип элемента формы ""%1""'"), 
			СоставПолейСтроки.ИмяТипаЭлемента
		);
		// BSLLS:NestedFunctionInParameters-on
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	ОписаниеЭлемента = Новый Структура;
	ОписаниеЭлемента.Вставить("ИмяЭлемента", СоставПолейСтроки.Имя);
	ОписаниеЭлемента.Вставить("ТипЭлемента", ТипЭлемента);
	ОписаниеЭлемента.Вставить("ИмяРодителя", Неопределено);
	ОписаниеЭлемента.Вставить("ИмяПоследующегоЭлемента", Неопределено);
	ОписаниеЭлемента.Вставить("СвойстваЭлемента", Новый Структура);
	ОписаниеЭлемента.Вставить("ПредставленияСвойствЭлемента", Новый Структура);
	ОписаниеЭлемента.Вставить("ОбработчикиЭлемента", Новый Структура);
	
	ОписаниеМодификаций.Элементы.Вставить(СоставПолейСтроки.Идентификатор, ОписаниеЭлемента);
КонецПроцедуры

#КонецОбласти
