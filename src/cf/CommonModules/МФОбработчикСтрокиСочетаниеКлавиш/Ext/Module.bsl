//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2025-2026 Alexey A. Stepanenko 
//    * alexey.stepanenko@gmail.com
//    * TG: @AlexeyStepanenko
//    * https://github.com/alexey-stepanenko
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция Идентификаторы() Экспорт
	Идентификаторы = Новый Массив;
	Идентификаторы.Добавить("СОЧЕТАНИЕКЛАВИШ");
	
	Возврат Идентификаторы;
КонецФункции

// Обработчик строки, описывающий сочетание клавиш, которое используется в свойстве СочетаниеКлавиш элементов формы
//
// Параметры:
//  ОписаниеМодификаций - Структура - См. подробнее МодификацияФормСлужебный.НовыйОписаниеМодификаций()
//  Поля - Массив - значения полей из строки описания модификации формы. Ожидается 6 элемента массива:
//    * [0] - Идентификатор строки. Может содержать значения из массива, возвращаемого функцией Идентификаторы().
//    * [1] - Имя сочетания клавиш.
//    * [2] - Значение основной клавиши сочетания. См. дополнительно синтакс помощник "СочетаниеКлавиш.Клавиша"
//    * [3] - Признак использования в сочетании клавиши Alt.
//    * [4] - Признак использования в сочетании клавиши Control (Ctrl).
//    * [5] - Признак использования в сочетании клавиши Shift.
//
// Пример строки:
// СОЧЕТАНИЕКЛАВИШ	ОсновноеСочетание	_2	Ложь	0	TRUE
//
Процедура Обработать(ОписаниеМодификаций, Поля) Экспорт
	МинимальноеКоличествоПолей = 3;
	КоличествоПолей = Поля.Количество();
	МФОбработкаСтрокСлужебный.ПроверитьКоличествоПолейСтроки(КоличествоПолей, МинимальноеКоличествоПолей);
	
	СоставПолейСтроки = МФОбработкаСтрокСлужебный.СоставПолейСтроки(
		Поля, 
		"Имя,ОсновнаяКлавиша,Alt,Control,Shift"
	);
	
	Попытка
		ОсновнаяКлавиша = Клавиша[СоставПолейСтроки.ОсновнаяКлавиша];
	Исключение
		// BSLLS:NestedFunctionInParameters-off
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Некорректное указание основной клавиши ""%1""'"),
			СоставПолейСтроки.ОсновнаяКлавиша
		);
		// BSLLS:NestedFunctionInParameters-on
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	// BSLLS:LatinAndCyrillicSymbolInWord-off
	КлавишаAlt = Ложь;
	КлавишаControl = Ложь;
	КлавишаShift = Ложь;
	
	Если КоличествоПолей > 3 Тогда // BSLLS:MagicNumber-off
		КлавишаAlt = ЗначениеДополнительнойКлавиши(СоставПолейСтроки.Alt, "Alt");
	КонецЕсли;
	Если КоличествоПолей > 4 Тогда // BSLLS:MagicNumber-off
		КлавишаControl = ЗначениеДополнительнойКлавиши(СоставПолейСтроки.Control, "Control");
	КонецЕсли;
	Если КоличествоПолей > 5 Тогда // BSLLS:MagicNumber-off
		КлавишаShift = ЗначениеДополнительнойКлавиши(СоставПолейСтроки.Shift, "Shift");
	КонецЕсли;
	
	СочетаниеКлавиш = Новый СочетаниеКлавиш(
		ОсновнаяКлавиша,
		КлавишаAlt,
		КлавишаControl,
		КлавишаShift
	);
	// BSLLS:LatinAndCyrillicSymbolInWord-on
	
	Описание = Новый Структура;
	Описание.Вставить("ИмяСочетанияКлавиш", СоставПолейСтроки.Имя);
	Описание.Вставить("ЗначениеСочетанияКлавиш", СочетаниеКлавиш);
	ОписаниеМодификаций.СочетанияКлавиш.Вставить(
		СоставПолейСтроки.Идентификатор,
		Описание
	);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗначениеДополнительнойКлавиши(ПредставлениеЗначения, ИмяКлавиши)
	ЗначенияБулево = МФПовтИсп.ЗначенияСтандартныхПеречислений().Значения.Получить(Тип("Булево"));
	Результат = ЗначенияБулево.Получить(ВРег(ПредставлениеЗначения));
	
	Если Результат = Неопределено Тогда
		// BSLLS:NestedFunctionInParameters-off
		ТекстИсключения = СтрШаблон(
			НСтр("ru='Значение дополнительной клавиши ""%1"" отличается от логического: ""%2""'"),
			ИмяКлавиши,
			ПредставлениеЗначения
		);
		// BSLLS:NestedFunctionInParameters-on
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти
