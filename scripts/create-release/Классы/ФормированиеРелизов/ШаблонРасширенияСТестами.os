#Использовать fs
#Использовать v8runner

Перем ИмяРелиза; // Возвращаемое значение
Перем РасширениеФайлаРелиза; // Возвращаемое значение
Перем ОсновнаяПодсистема; // Возвращаемое значение
Перем Подсистемы; // Возвращаемое значение
Перем ИсключаемыеПодсистемы; // Возвращаемое значение

Перем ГУИДы;
Перем ИмяФайлаКонфигурации;
Перем КаталогИсходников;

#Область ПрограммныйИнтерфейс

Процедура Инициализировать(ПараметрыКонструктора) Экспорт
	ИмяРелиза = СтрШаблон(
		"%1-cf-dev-%2",
		ПараметрыКонструктора.ПрефиксИмениФайлаРелиза,
		ПараметрыКонструктора.НомерРелиза
	);

	РасширениеФайлаРелиза = ".cfe";
	ИмяФайлаКонфигурации = ПараметрыКонструктора.ИмяФайлаКонфигурации;
	ОсновнаяПодсистема = "МодификацияФорм";
	Подсистемы = "МодификацияФормПодсистемы.МФИнструментыРазработчика";
	ИсключаемыеПодсистемы = "";
    ГУИДы = Новый Массив;
    Расширение.ДобавитьГУИД(ГУИДы, "9cd510cd-abfc-11d4-9434-004095e12fc7", "dc4a3904-6fe0-4637-9322-754bec6712f6");
    Расширение.ДобавитьГУИД(ГУИДы, "9fcd25a0-4822-11d4-9414-008048da11f9", "9b83ee69-4e22-44d6-a4c9-29bca085741c");
    Расширение.ДобавитьГУИД(ГУИДы, "e3687481-0a87-462c-a166-9f34594f9bba", "9243ca72-754a-4e5d-8e6a-d8811534b1e7");
    Расширение.ДобавитьГУИД(ГУИДы, "9de14907-ec23-4a07-96f0-85521cb6b53b", "9a0ded6f-798d-4e22-b158-8f15f982a359");
    Расширение.ДобавитьГУИД(ГУИДы, "51f2d5d8-ea4d-4064-8892-82951750031e", "319ddbfe-bd2f-4a26-a329-9acba6821bdb");
    Расширение.ДобавитьГУИД(ГУИДы, "e68182ea-4237-4383-967f-90c1e3370bc7", "09c8f4aa-8d07-400b-8d88-a32a6fe668f6");
    Расширение.ДобавитьГУИД(ГУИДы, "fb282519-d103-4dd3-bc12-cb271d631dfc", "625ed510-57ab-4eeb-8703-bb96593a3000");

    ПараметрыКонструктораРасширения = Расширение.НовыйПараметрыКонструктора();
    ПараметрыКонструктораРасширения.ОсновнаяПодсистема = ОсновнаяПодсистема;
    ПараметрыКонструктораРасширения.ИдентификаторБлокаКодаРасширения = Идентификаторы.БлокТестирования();
    ПараметрыКонструктораРасширения.ИмяФайлаКонфигурации = ИмяФайлаКонфигурации;
    ПараметрыКонструктораРасширения.ГУИДы = ГУИДы;
    ПараметрыКонструктораРасширения.ГУИДРасширения = "c7367054-6b50-48da-9de8-9cc5a11f2186";
    ПараметрыКонструктораРасширения.Префикс = "Тест_";
    ПараметрыКонструктораРасширения.ИмяКонфигурации = "ШаблонРасширенияДляТестов";
    
    Расширение.Инициализировать(ПараметрыКонструктораРасширения);
КонецПроцедуры

функция БлокиКУдалению() Экспорт
    Результат = Новый Массив;

    Результат.Добавить(Идентификаторы.БлокРазработки());
    Результат.Добавить(Идентификаторы.УдалитьИзРелиза());

    Возврат Результат;
КонецФункции

Функция ИмяРелиза() Экспорт
	Возврат ИмяРелиза;	
КонецФункции

Функция РасширениеФайлаРелиза() Экспорт
	Возврат РасширениеФайлаРелиза;
КонецФункции

Функция ОсновнаяПодсистема() Экспорт
    Возврат ОсновнаяПодсистема;    
КонецФункции

Функция Подсистемы() Экспорт
    Возврат Подсистемы;    
КонецФункции

Функция ИсключаемыеПодсистемы() Экспорт
    Возврат ИсключаемыеПодсистемы;    
КонецФункции

Процедура УстановитьКаталогИсходников(Каталог) Экспорт
	КаталогИсходников = Каталог;
    Расширение.УстановитьКаталогИсходников(Каталог);
КонецПроцедуры

Процедура СкорректироватьМодули(ОбъектыПоставки) Экспорт
    Расширение.СкорректироватьМодули(ОбъектыПоставки);
КонецПроцедуры

Процедура СкорректироватьФайлОсновнойПодсистемы(ОбъектыПоставки) Экспорт
    Расширение.СкорректироватьФайлОсновнойПодсистемы(ОбъектыПоставки);
КонецПроцедуры

Процедура СкорректироватьФайлКонфигурации(ОбъектыПоставки) Экспорт
    Расширение.СкорректироватьФайлКонфигурации(ОбъектыПоставки);
КонецПроцедуры

Процедура ВыполнитьДополнительныеДействия() Экспорт
    // Исправляем файл Ext/CommandInterface.xml: Оставить главную подсистему
    ПроцессорXML = Новый СериализацияДанныхXML(Ложь);
	ПутьКФайлуКомандногоИнтерфейса = ОбъединитьПути(
        КаталогИсходников,
        "Ext",
        "CommandInterface.xml"
    );
    КомандныйИнтерфейс = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуКомандногоИнтерфейса);
    Элемент = Новый Соответствие;
    Элемент.Вставить("Subsystem", "Subsystem." + ОсновнаяПодсистема);
    Коллекция = КомандныйИнтерфейс
        ._Элементы
        .Получить("CommandInterface")
        ._Элементы
    ;
    Коллекция.Вставить("SubsystemsOrder", Элемент);
    ПроцессорXML.ЗаписатьВФайл(КомандныйИнтерфейс, ПутьКФайлуКомандногоИнтерфейса);

    // Удаляем все файлы из Ext, за исключением командного интерфейса и модуля приложения
    Исключения = "CommandInterface.xml,ManagedApplicationModule.bsl";
    Расширение.ОчиститьКаталогExt(Исключения);

    // Исправляем основной язык
    Расширение.СкорректироватьОсновнойЯзык();

    // Исправлям Модуль управляемого приложения
    ПутьКФайлуМодуля = ОбъединитьПути(
        КаталогИсходников,
        "Ext",
        "ManagedApplicationModule.bsl"
    );
    ТекстовыйДокумент = Новый ТекстовыйДокумент;
    ТекстовыйДокумент.Прочитать(ПутьКФайлуМодуля);
    ТекстМодуля = ТекстовыйДокумент.ПолучитьТекст();
    Расширение.СкорректироватьТекстМодуля(ПутьКФайлуМодуля, ТекстМодуля);
КонецПроцедуры

Процедура СоздатьФайлПоставки(ПутьКФайлуПоставки) Экспорт
    Расширение.СоздатьФайлПоставки(ПутьКФайлуПоставки);
КонецПроцедуры

#КонецОбласти
