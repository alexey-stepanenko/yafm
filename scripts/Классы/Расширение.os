// BSLLS:MissingVariablesDescription-off

#Использовать fs
#Использовать v8runner

Перем Константы;

Перем КаталогИсходников;
Перем ОсновнаяПодсистема;
Перем ИдентификаторБлокаКодаРасширения;
Перем ГУИДы;
Перем ГУИДРасширения;
Перем Префикс;
Перем ИмяКонфигурации;
Перем ПерехваченныеМодули;

Функция НовыйПараметрыКонструктора() Экспорт
	Результат = Новый Структура;

	Результат.Вставить("ОсновнаяПодсистема", Неопределено);
	Результат.Вставить("ИдентификаторБлокаКодаРасширения", Неопределено);
	Результат.Вставить("ГУИДы", Неопределено);
	Результат.Вставить("ГУИДРасширения", Неопределено);
	Результат.Вставить("Префикс", Неопределено);
	Результат.Вставить("ИмяКонфигурации", Неопределено);

	Возврат Результат;
КонецФункции

Процедура Инициализировать(ПараметрыКонструктора) Экспорт
	ОсновнаяПодсистема = ПараметрыКонструктора.ОсновнаяПодсистема;
	ИдентификаторБлокаКодаРасширения = ПараметрыКонструктора.ИдентификаторБлокаКодаРасширения;
	ГУИДы = ПараметрыКонструктора.ГУИДы;
	ГУИДРасширения = ПараметрыКонструктора.ГУИДРасширения;
    Префикс = ПараметрыКонструктора.Префикс;
    ИмяКонфигурации = ПараметрыКонструктора.ИмяКонфигурации;
КонецПроцедуры

Процедура ДобавитьГУИД(ГУИДы, ИдентификаторКласса, ИдентификаторОбъекта) Экспорт
    ПодчиненныеЭлементы = Новый Соответствие;
    ПодчиненныеЭлементы.Вставить("xr:ClassId", ИдентификаторКласса);
    ПодчиненныеЭлементы.Вставить("xr:ObjectId", ИдентификаторОбъекта);
    Элемент = Новый Соответствие;
    Элемент.Вставить("xr:ContainedObject", Новый Структура("_Элементы", ПодчиненныеЭлементы));
    ГУИДЫ.Добавить(Элемент);
КонецПроцедуры

Процедура УстановитьКаталогИсходников(Каталог) Экспорт
	КаталогИсходников = Каталог;
КонецПроцедуры

Процедура СкорректироватьМодули(ОбъектыПоставки) Экспорт
    ПерехваченныеМодули = Новый Соответствие;

    Для Каждого ОбъектПоставки Из ОбъектыПоставки Цикл
        Описание = ОбъектПоставки.Значение;
        ПутьКФайлуМодуля = "";
        ПутьКФайлуОписания = "";
        Если Описание.Тип = "CommonModule" Тогда
            СоставПути = Новый Массив;
            СоставПути.Добавить(КаталогИсходников);
            СоставПути.Добавить("CommonModules");
            СоставПути.Добавить(Описание.Имя);
            СоставПути.Добавить("Ext");
            СоставПути.Добавить("Module.bsl");
            ПутьКФайлуМодуля = СтрСоединить(СоставПути, ПолучитьРазделительПути());

            ПутьКФайлуОписания = ОбъединитьПути(
                КаталогИсходников,
                "CommonModules",
                Описание.Имя + ".xml"
            );
        КонецЕсли;

        Если ПутьКФайлуМодуля = "" Тогда
            Продолжить;
        КонецЕсли;

        СодержимоеФайла = "";
        Если ЭтоФайлДляКорректировки(ПутьКФайлуМодуля, СодержимоеФайла) Тогда
            СкорректироватьТекстМодуля(ПутьКФайлуМодуля, СодержимоеФайла);
            СкорректироватьОписаниеМодуля(ОбъектПоставки.Ключ, ПутьКФайлуОписания, ПерехваченныеМодули);
        КонецЕсли;
    КонецЦикла;
КонецПроцедуры

Процедура СкорректироватьФайлОсновнойПодсистемы(ОбъектыПоставки) Экспорт
    СоставОсновнойПодсистемы = СтрРазделить(ОсновнаяПодсистема, ".");
    ПутьКФайлуОсновнойПодсистемы = ОбъединитьПути(
        КаталогИсходников, 
        "Subsystems",
        СоставОсновнойПодсистемы[СоставОсновнойПодсистемы.Количество() - 1] + ".xml"
    );
    Если СоставОсновнойПодсистемы.Количество() > 1 Тогда
        СоставПути = Новый Массив;
        Для каждого Элемент Из СоставОсновнойПодсистемы Цикл
            СоставПути.Добавить("Subsystems");
            СоставПути.Добавить(Элемент);
        КонецЦикла;
        ПутьКИсходномуФайлуОсновнойПодсистемы = ОбъединитьПути(
            КаталогИсходников, 
            СтрСоединить(СоставПути, ПолучитьРазделительПути()) + ".xml"
        );
        КопироватьФайл(ПутьКИсходномуФайлуОсновнойПодсистемы, ПутьКФайлуОсновнойПодсистемы);
    КонецЕсли;
    ПроцессорXML = Новый СериализацияДанныхXML();

    КонфигурацияПодсистемы = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуОсновнойПодсистемы);
    СкорректироватьОбъектыОсновнойПодсистемы(КонфигурацияПодсистемы, ОбъектыПоставки);
    ПроцессорXML.ЗаписатьВФайл(КонфигурацияПодсистемы, ПутьКФайлуОсновнойПодсистемы);
    ТекстовыйДокумент = Новый ТекстовыйДокумент;
    ТекстовыйДокумент.Прочитать(ПутьКФайлуОсновнойПодсистемы);
    ТекстФайлаОсновнойПодсистемы = СтрЗаменить(
        ТекстовыйДокумент.ПолучитьТекст(),
        "<xr:Item>",
        "<xr:Item  xsi:type=""xr:MDObjectRef"">"
    );
    ТекстовыйДокумент.УстановитьТекст(ТекстФайлаОсновнойПодсистемы);
    ТекстовыйДокумент.Записать(ПутьКФайлуОсновнойПодсистемы);
КонецПроцедуры

Процедура СкорректироватьФайлКонфигурации(ОбъектыПоставки) Экспорт
    ПроцессорXML = Новый СериализацияДанныхXML();

	ПутьКФайлуКонфигурации = ОбъединитьПути(КаталогИсходников, Константы.ИмяФайлаКонфигурации);
    Конфигурация = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуКонфигурации);
    ИзменитьГУИДКонфигурации(Конфигурация);
    ДобавитьСвойстваКонфигурации(Конфигурация);
	СкорректироватьОбъектыКонфигурации(Конфигурация, ОбъектыПоставки);
    Конфигурация
        .Получить("MetaDataObject")
        ._Элементы
        .Получить("Configuration")
        ._Элементы
        .Вставить("InternalInfo", ГУИДы)
    ;
    ПроцессорXML.ЗаписатьВФайл(Конфигурация, ПутьКФайлуКонфигурации);
КонецПроцедуры

Процедура ОчиститьКаталогExt(Исключения) Экспорт
	СоставИсключений = СтрРазделить(Исключения, ",");
    ПутьКОчищаемомуКаталогу = ОбъединитьПути(
        КаталогИсходников,
        "Ext"
    );
    НайденныеФайлы = НайтиФайлы(ПутьКОчищаемомуКаталогу, "*.*");
    Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
        Если СоставИсключений.Найти(НайденныйФайл.Имя) <> Неопределено Тогда
            Продолжить;
        КонецЕсли;
        УдалитьФайлы(НайденныйФайл.ПолноеИмя);
    КонецЦикла;
КонецПроцедуры

Процедура СкорректироватьОсновнойЯзык() Экспорт
    ПроцессорXML = Новый СериализацияДанныхXML(Ложь);
    ПутьКФайлуОсновногоЯзыка = ОбъединитьПути(
        КаталогИсходников,
        "Languages",
        "Русский.xml"
    );
    ОсновнойЯзык = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуОсновногоЯзыка);
    Корень = ОсновнойЯзык
        ._Элементы
        .Получить("MetaDataObject")
        ._Элементы
        .Получить("Language")
    ;
    ИсходныйГУИДЯзыка = Корень._Атрибуты.Получить("uuid");
    НовыйГУИДЯзыка = Строка(Новый УникальныйИдентификатор);
    Корень._Атрибуты.Вставить("uuid", НовыйГУИДЯзыка);

    Свойства = Корень
        ._Элементы
        .Получить("Properties")
        ._Элементы
    ;
    Свойства.Вставить("ObjectBelonging", "Adopted");
    Свойства.Вставить("ExtendedConfigurationObject", ИсходныйГУИДЯзыка);
    ПроцессорXML.ЗаписатьВФайл(ОсновнойЯзык, ПутьКФайлуОсновногоЯзыка);
КонецПроцедуры

Процедура СкорректироватьТекстМодуля(ПутьКФайлуМодуля, СодержимоеФайла) Экспорт
    СоставИсходногоФайла = СтрРазделить(СодержимоеФайла, Символы.ПС);
    СоставРезультирующегоФайла = Новый Массив;
	ТекстРегулярногоВыражения = СтрШаблон(
        Константы.ШаблонРегулярногоВыраженияПоискаБлока,
        ИдентификаторБлокаКодаРасширения
    );
    НашлиБлок = Ложь;
	РегулярноеВыражение = Новый РегулярноеВыражение(ТекстРегулярногоВыражения);
    Для Каждого СтрокаФайла Из СоставИсходногоФайла Цикл
		Совпадения = РегулярноеВыражение.НайтиСовпадения(СтрокаФайла);

        Если (Совпадения.Количество() = 0) И НашлиБлок Тогда
            СоставРезультирующегоФайла.Добавить(СтрокаФайла);
        ИначеЕсли Совпадения.Количество() > 0 Тогда
            ЗначениеВСтроке = Совпадения[0].Группы[3].Значение;
            Если ЗначениеВСтроке = Константы.НачалоБлока Тогда
                НашлиБлок = Истина;
            ИначеЕсли ЗначениеВСтроке = Константы.КонецБлока Тогда
                НашлиБлок = Ложь;
            Иначе
                СоставРезультирующегоФайла.Добавить(ЗначениеВСтроке);
            КонецЕсли;
        Иначе
            Продолжить;
        КонецЕсли;
    КонецЦикла;

    ТекстовыйДокумент = Новый ТекстовыйДокумент;
    ТекстовыйДокумент.Прочитать(ПутьКФайлуМодуля);
    ТекстовыйДокумент.УстановитьТекст(СтрСоединить(СоставРезультирующегоФайла, Символы.ПС));
    ТекстовыйДокумент.Записать(ПутьКФайлуМодуля);
КонецПроцедуры 

Процедура СоздатьФайлПоставки(ПутьКФайлуПоставки) Экспорт
    // Меняем структуру каталога исходных файлов для правильной загрузки
    ФайлыДляПереноса = Новый Массив;
    НайденныеФайлы = НайтиФайлы(КаталогИсходников, "*.*");
    Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
        ФайлыДляПереноса.Добавить(НайденныйФайл.ПолноеИмя);
    КонецЦикла;
    НовыйКаталогИсходников = ОбъединитьПути(
        КаталогИсходников,
        ИмяКонфигурации
    );
    ФС.ОбеспечитьПустойКаталог(НовыйКаталогИсходников);
    Для Каждого ИмяФайлДляПереноса Из ФайлыДляПереноса Цикл
        Файл = Новый Файл(ИмяФайлДляПереноса);
        НовыйПутьФайла = ОбъединитьПути(
            НовыйКаталогИсходников, 
            Файл.Имя
        );
        Если Файл.ЭтоФайл() Тогда
            ПереместитьФайл(ИмяФайлДляПереноса, НовыйПутьФайла);
        Иначе
            СоздатьКаталог(НовыйПутьФайла);
            ФС.КопироватьСодержимоеКаталога(ИмяФайлДляПереноса, НовыйПутьФайла);
		    УдалитьФайлы(ИмяФайлДляПереноса);
        КонецЕсли;
    КонецЦикла;

    Конфигуратор = Новый УправлениеКонфигуратором();
    Конфигуратор.ЗагрузитьРасширениеИзФайлов(КаталогИсходников);
    Конфигуратор.ВыгрузитьРасширениеВФайл(ПутьКФайлуПоставки, ИмяКонфигурации);
	УдалитьФайлы(Конфигуратор.ПутьКВременнойБазе());
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

#Область СкорректироватьМодули

Функция ЭтоФайлДляКорректировки(ПутьКФайлуМодуля, СодержимоеФайла)
    Результат = Ложь;

    ТекстовыйДокумент = Новый ТекстовыйДокумент;
    ТекстовыйДокумент.Прочитать(ПутьКФайлуМодуля);
    ИсходныйТекст = ТекстовыйДокумент.ПолучитьТекст();

    Если СтрНайти(ИсходныйТекст, ИдентификаторБлокаКодаРасширения) > 0 Тогда
        Результат = Истина;
        СодержимоеФайла = ИсходныйТекст;
    КонецЕсли;
    
    Возврат Результат;
КонецФункции

Процедура СкорректироватьОписаниеМодуля(ИдентификаторМодуля, ПутьКФайлуОписания, ПерехваченныеМодули)
    ПроцессорXML = Новый СериализацияДанныхXML(Ложь);
    Описание = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуОписания);
    Корень = Описание
        ._Элементы
        .Получить("MetaDataObject")
        ._Элементы
        .Получить("CommonModule")
    ;
    ИсходныйГУИДМодуля = Корень._Атрибуты.Получить("uuid");
    НовыйГУИДМодуля = Строка(Новый УникальныйИдентификатор);
    Корень._Атрибуты.Вставить("uuid", НовыйГУИДМодуля);

    Свойства = Корень
        ._Элементы
        .Получить("Properties")
        ._Элементы
    ;
    Свойства.Вставить("ObjectBelonging", "Adopted");
    Свойства.Вставить("ExtendedConfigurationObject", ИсходныйГУИДМодуля);

    ПроцессорXML.ЗаписатьВФайл(Описание, ПутьКФайлуОписания);

    ПерехваченныеМодули.Вставить(ИдентификаторМодуля, ИсходныйГУИДМодуля);
КонецПроцедуры

#КонецОбласти

#Область СкорректироватьФайлОсновнойПодсистемы

Процедура СкорректироватьОбъектыОсновнойПодсистемы(КонфигурацияПодсистемы, ОбъектыПоставки)
    // Очищаем список подчиненных подсистем
    Описание = КонфигурацияПодсистемы
        .Получить("MetaDataObject")
        ._Элементы
        .Получить("Subsystem")
        ._Элементы
    ;
    Описание.Вставить("ChildObjects", Неопределено);
    
    // Добавляем объекты
    Описание = КонфигурацияПодсистемы
        .Получить("MetaDataObject")
        ._Элементы
        .Получить("Subsystem")
        ._Элементы
        .Получить("Properties")
    ;

    ОбъектыПоИмени = СортированныеОбъектыПоИмени(ОбъектыПоставки);
    ОбъектыПодсистемы = Новый Массив;
    
    Для Каждого Элемент Из ОбъектыПоИмени Цикл
        Если Элемент = "Subsystem." + ОсновнаяПодсистема Тогда
            Продолжить;
        КонецЕсли;
        ОбъектПоставки = ОбъектыПоставки.Получить(Элемент);
        Если ОбъектПоставки.Тип = "Language" Тогда
            Продолжить;
        КонецЕсли;
        ОбъектПодсистемы = Новый Соответствие;
        ОписаниеОбъекта = Новый Структура;
        ПерехваченныйМодуль = ПерехваченныеМодули.Получить(Элемент);
        Если ПерехваченныйМодуль = Неопределено Тогда
            ОписаниеОбъекта.Вставить("_Значение", Элемент);
        Иначе
            ОписаниеОбъекта.Вставить("_Значение", ПерехваченныйМодуль);
        КонецЕсли;
        
        ОбъектПодсистемы.Вставить("xr:Item", ОписаниеОбъекта);
        ОбъектыПодсистемы.Добавить(ОбъектПодсистемы);
    КонецЦикла;

    Описание.Вставить("Content", ОбъектыПодсистемы);
КонецПроцедуры

Функция СортированныеОбъектыПоИмени(Объекты)
    Результат = Новый Массив;
    СписокЗначений = Новый СписокЗначений;
    Для Каждого ЭлементКоллекции Из Объекты Цикл
        СписокЗначений.Добавить(ЭлементКоллекции.Ключ);
    КонецЦикла;
    СписокЗначений.СортироватьПоЗначению();
    Результат = СписокЗначений.ВыгрузитьЗначения();
    
    Возврат Результат;
КонецФункции

#КонецОбласти

#Область СкорректироватьФайлКонфигурации

Процедура ИзменитьГУИДКонфигурации(Конфигурация)
    Коллекция = Конфигурация
        .Получить("MetaDataObject")
        ._Элементы
        .Получить("Configuration")
    ;
    Атрибуты = Коллекция._Атрибуты;
    Атрибуты.Вставить("uuid", ГУИДРасширения);
КонецПроцедуры

Процедура ДобавитьСвойстваКонфигурации(Конфигурация)
    Коллекция = Конфигурация
        .Получить("MetaDataObject")
        ._Элементы
        .Получить("Configuration")
        ._Элементы
        .Получить("Properties")
    ;
    // Добавляем свойство ObjectBelonging
    Коллекция.Вставить("ObjectBelonging", "Adopted");

    // Исправляем свойство Name
    ИмяСвойства = "Name";
    Коллекция.Вставить(ИмяСвойства, ИмяКонфигурации);

    // Добавляем свойство ConfigurationExtensionPurpose
    Коллекция.Вставить("ConfigurationExtensionPurpose", "AddOn");

    // Добавляем свойство KeepMappingToExtendedConfigurationObjectsByIDs
    Коллекция.Вставить("KeepMappingToExtendedConfigurationObjectsByIDs", "true");

    // Исправляем свойство NamePrefix
    Коллекция.Вставить("NamePrefix", Префикс);

    // Исправляем свойство ConfigurationExtensionCompatibilityMode
    РежимСовместимости = Строка(Коллекция.Получить("CompatibilityMode"));
    Коллекция.Вставить("ConfigurationExtensionCompatibilityMode", РежимСовместимости);
КонецПроцедуры

Процедура СкорректироватьОбъектыКонфигурации(Конфигурация, ОбъектыПоставки)
    Коллекция = Конфигурация
        .Получить("MetaDataObject")
        ._Элементы
        .Получить("Configuration")
        ._Элементы
        .Получить("ChildObjects")
    ;
    Коллекция.Очистить();
    ОбъектыПоИмени = СортированныеОбъектыПоИмени(ОбъектыПоставки);
    Для Каждого ИмяОбъекта Из ОбъектыПоИмени Цикл
        ЭлементКоллекции = ОбъектыПоставки.Получить(ИмяОбъекта);
        Объект = Новый Соответствие;
        Объект.Вставить(ЭлементКоллекции.Тип, ЭлементКоллекции.Имя);
        Коллекция.Добавить(Объект);
    КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

ПутьККаталогуТекущегоСценария = ТекущийСценарий().Каталог;
// BSLLS:NestedFunctionInParameters-off
Константы = ЗагрузитьСценарий(ОбъединитьПути(
	ПутьККаталогуТекущегоСценария,
	"..",
	"Ресурсы",
	"Константы.os"
));
