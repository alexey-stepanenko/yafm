// BSLLS:MissingVariablesDescription-off
// BSLLS:NestedFunctionInParameters-off

#Использовать fs
#Использовать v8runner

Перем Константы;

Перем ИмяРелиза;
Перем РасширениеФайлаРелиза;
Перем ОсновнаяПодсистема;
Перем Подсистемы;
Перем ИсключаемыеПодсистемы;

Перем КаталогИсходников;

#Область ПрограммныйИнтерфейс

Функция ЭтоКонструкторРелиза() Экспорт
	Возврат Истина;
КонецФункции

Процедура Инициализировать(ПараметрыКонструктора) Экспорт
    ИмяРелиза = СтрШаблон(
		"%1-cf-main-%2",
		Константы.ПрефиксИмениФайлаРелиза,
		ПараметрыКонструктора.НомерРелиза
	);

	РасширениеФайлаРелиза = ".cf";
	ОсновнаяПодсистема = "МодификацияФормПодсистемы";
	ИсключаемыеПодсистемы = "";
КонецПроцедуры

функция БлокиКУдалению() Экспорт
    Результат = Новый Массив;

    Результат.Добавить(Константы.БлокРазработки);
    Результат.Добавить(Константы.БлокТестирования);
    Результат.Добавить(Константы.УдалитьИзРелиза);

    Возврат Результат;
КонецФункции

Функция ИмяРелиза() Экспорт
	Возврат ИмяРелиза;	
КонецФункции

Функция РасширениеФайлаРелиза() Экспорт
	Возврат РасширениеФайлаРелиза;
КонецФункции

Функция ОсновнаяПодсистема() Экспорт
    Возврат ОсновнаяПодсистема;    
КонецФункции

Функция Подсистемы() Экспорт
    Если Подсистемы = Неопределено Тогда
        Подсистемы = ПодсистемыПоБазовойПодсистеме(
            "МодификацияФормПодсистемы", 
            "МФИнструментыРазработчика,МФТестирование"
        );
    КонецЕсли;

    Возврат Подсистемы;    
КонецФункции

Функция ИсключаемыеПодсистемы() Экспорт
    Возврат ИсключаемыеПодсистемы;    
КонецФункции

Процедура УстановитьКаталогИсходников(Каталог) Экспорт
	КаталогИсходников = Каталог;
КонецПроцедуры

Процедура СкорректироватьМодули(ОбъектыПоставки) Экспорт
    // Пустая процедура
КонецПроцедуры

Процедура СкорректироватьФайлОсновнойПодсистемы(ОбъектыПоставки) Экспорт
    ПроцессорXML = Новый СериализацияДанныхXML();

    ПутьКФайлуОсновнойПодсистемы = ОбъединитьПути(
        КаталогИсходников, 
        "Subsystems",
        ОсновнаяПодсистема + ".xml"
    );
    КонфигурацияПодсистемы = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуОсновнойПодсистемы);
    СкорректироватьОбъектыОсновнойПодсистемы(КонфигурацияПодсистемы, ОбъектыПоставки);
    ПроцессорXML.ЗаписатьВФайл(КонфигурацияПодсистемы, ПутьКФайлуОсновнойПодсистемы);
    ТекстовыйДокумент = Новый ТекстовыйДокумент;
    ТекстовыйДокумент.Прочитать(ПутьКФайлуОсновнойПодсистемы);
    ТекстФайлаОсновнойПодсистемы = СтрЗаменить(
        ТекстовыйДокумент.ПолучитьТекст(),
        "<xr:Item>",
        "<xr:Item  xsi:type=""xr:MDObjectRef"">"
    );
    ТекстовыйДокумент.УстановитьТекст(ТекстФайлаОсновнойПодсистемы);
    ТекстовыйДокумент.Записать(ПутьКФайлуОсновнойПодсистемы);
КонецПроцедуры

Процедура СкорректироватьФайлКонфигурации(ОбъектыПоставки) Экспорт
    ПроцессорXML = Новый СериализацияДанныхXML();

	ПутьКФайлуКонфигурации = ОбъединитьПути(КаталогИсходников, Константы.ИмяФайлаКонфигурации);
    Конфигурация = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуКонфигурации);
	СкорректироватьОбъектыКонфигурации(Конфигурация, ОбъектыПоставки);
    ПроцессорXML.ЗаписатьВФайл(Конфигурация, ПутьКФайлуКонфигурации);
КонецПроцедуры

Процедура ВыполнитьДополнительныеДействия() Экспорт
    ПутьКОчищаемомуКаталогу = ОбъединитьПути(КаталогИсходников, "Ext");
    ФС.ОбеспечитьПустойКаталог(ПутьКОчищаемомуКаталогу);
КонецПроцедуры

Процедура СоздатьФайлПоставки(ПутьКФайлуПоставки) Экспорт
    Конфигуратор = Новый УправлениеКонфигуратором();
    Конфигуратор.ЗагрузитьКонфигурациюИзФайлов(КаталогИсходников);
    Конфигуратор.ОбновитьКонфигурациюБазыДанных();
    Конфигуратор.СоздатьФайлыПоставки(ПутьКФайлуПоставки);
	УдалитьФайлы(Конфигуратор.ПутьКВременнойБазе());
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодсистемыПоБазовойПодсистеме(БазоваяПодсистема, ИсключаемыеПодсистемы)
    СписокИсключаемыхПодсистем = СтрРазделить(ИсключаемыеПодсистемы, ",");
    СписокПодсистем = Новый Массив;
    ПроцессорXML = Новый СериализацияДанныхXML(Ложь);
    ПутьКФайлуОсновнойПодсистемы = ОбъединитьПути(
        КаталогИсходников,
        "Subsystems",
        БазоваяПодсистема + ".xml"
    );
    КонфигурацияПодсистемы = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуОсновнойПодсистемы);
	ОбъектКонфигурации = Новый ОбъектКонфигурации();
	Ветка = ОбъектКонфигурации.НайтиВеткуКонфигурации(КонфигурацияПодсистемы, "MetaDataObject,Subsystem,ChildObjects"); // BSLLS:LineLength-off
    Описание = Ветка._Элементы;
    ТипЗначенияОписания = ТипЗнч(Описание);
    Если ТипЗначенияОписания = Тип("Массив") Тогда
        Для Каждого Элемент Из Описание Цикл
            ОбработатьПодчиненнуюПодсистему(Элемент, СписокПодсистем, БазоваяПодсистема, СписокИсключаемыхПодсистем);
        КонецЦикла;
    ИначеЕсли ТипЗначенияОписания = Тип("Соответствие") Тогда
        ОбработатьПодчиненнуюПодсистему(Описание, СписокПодсистем, БазоваяПодсистема, СписокИсключаемыхПодсистем);
    Иначе // BSLLS:EmptyCodeBlock-off
        // Пустой блок
    КонецЕсли;
    Возврат СтрСоединить(СписокПодсистем, ",");
КонецФункции

Процедура ОбработатьПодчиненнуюПодсистему(Описание, СписокПодсистем, БазоваяПодсистема, СписокИсключаемыхПодсистем);
    ПараметрыОписания = Описание.Получить("Subsystem");
    Если ПараметрыОписания = Неопределено Тогда
        Возврат;
    КонецЕсли;
    НазваниеПодсистемы = ПараметрыОписания._Значение;
    Если СписокИсключаемыхПодсистем.Найти(НазваниеПодсистемы) = Неопределено Тогда
        СписокПодсистем.Добавить(БазоваяПодсистема + "." + НазваниеПодсистемы);        
    КонецЕсли;
КонецПроцедуры

Процедура СкорректироватьОбъектыОсновнойПодсистемы(КонфигурацияПодсистемы, ОбъектыПоставки)
    // Очищаем список подчиненных подсистем
	ОбъектКонфигурации = Новый ОбъектКонфигурации();
	Ветка = ОбъектКонфигурации.НайтиВеткуКонфигурации(КонфигурацияПодсистемы, "MetaDataObject,Subsystem");
    Описание = Ветка._Элементы;
    Описание.Вставить("ChildObjects", Неопределено);
    
    // Добавляем объекты
	Описание = ОбъектКонфигурации.НайтиВеткуКонфигурации(КонфигурацияПодсистемы, "MetaDataObject,Subsystem,Properties");

    ОбъектыПоИмени = СортированныеОбъектыПоИмени(ОбъектыПоставки);
    ОбъектыПодсистемы = Новый Массив;
    
    Для Каждого Элемент Из ОбъектыПоИмени Цикл
        Если Элемент = "Subsystem." + ОсновнаяПодсистема Тогда
            Продолжить;
        КонецЕсли;
        ОбъектПоставки = ОбъектыПоставки.Получить(Элемент);
        Если ОбъектПоставки.Тип = "Language" Тогда
            Продолжить;
        КонецЕсли;
        ОбъектПодсистемы = Новый Соответствие;
        ОписаниеОбъекта = Новый Структура;
        ОписаниеОбъекта.Вставить("_Значение", Элемент);
        
        ОбъектПодсистемы.Вставить("xr:Item", ОписаниеОбъекта);
        ОбъектыПодсистемы.Добавить(ОбъектПодсистемы);
    КонецЦикла;

    Описание.Вставить("Content", ОбъектыПодсистемы);
КонецПроцедуры

Процедура СкорректироватьОбъектыКонфигурации(Конфигурация, ОбъектыПоставки)
	ОбъектКонфигурации = Новый ОбъектКонфигурации();
	Коллекция = ОбъектКонфигурации.НайтиВеткуКонфигурации(Конфигурация, "MetaDataObject,Configuration,ChildObjects");

    Коллекция.Очистить();
    ОбъектыПоИмени = СортированныеОбъектыПоИмени(ОбъектыПоставки);
    Для Каждого ИмяОбъекта Из ОбъектыПоИмени Цикл
        ЭлементКоллекции = ОбъектыПоставки.Получить(ИмяОбъекта);
        Объект = Новый Соответствие;
        Объект.Вставить(ЭлементКоллекции.Тип, ЭлементКоллекции.Имя);
        Коллекция.Добавить(Объект);
    КонецЦикла;
КонецПроцедуры

Функция СортированныеОбъектыПоИмени(Объекты)
    Результат = Новый Массив;
    СписокЗначений = Новый СписокЗначений;
    Для Каждого ЭлементКоллекции Из Объекты Цикл
        СписокЗначений.Добавить(ЭлементКоллекции.Ключ);
    КонецЦикла;
    СписокЗначений.СортироватьПоЗначению();
    Результат = СписокЗначений.ВыгрузитьЗначения();
    
    Возврат Результат;
КонецФункции

#КонецОбласти

Подсистемы = Неопределено;

ПутьККаталогуТекущегоСценария = ТекущийСценарий().Каталог;
Константы = ЗагрузитьСценарий(ОбъединитьПути(
	ПутьККаталогуТекущегоСценария,
	"..",
	"Ресурсы",
	"Константы.os"
));
