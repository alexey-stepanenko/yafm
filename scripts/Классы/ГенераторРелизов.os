// BSLLS:MissingVariablesDescription-off
// BSLLS:NestedFunctionInParameters-off

#Использовать fs

Перем Константы;

Перем КаталогИсходников;
Перем КаталогРелизов;
Перем ВариантыПоставок;
Перем СоздатьКаталогНомераРелиза;

#Область ПрограммныйИнтерфейс

Функция НовыйПараметрыСозданияРелизов() Экспорт
	Результат = Новый Структура;

	Результат.Вставить("КаталогИсходников", Неопределено);
	Результат.Вставить("КаталогРелизов", Неопределено);
	Результат.Вставить("ВариантыПоставок", Неопределено);
	Результат.Вставить("СоздатьКаталогНомераРелиза", Неопределено);

	Возврат Результат;
КонецФункции

Процедура Создать(ПараметрыСозданияРелизов, ОбъектыРелизов = Неопределено) Экспорт
	ОбработатьПараметрыСозданияРелизов(ПараметрыСозданияРелизов);
	ПроверитьПараметрыСозданияРелизов();

	НомерРелиза = СтрЗаменить(НомерРелиза(КаталогИсходников), ".", "_");
	Если ПустаяСтрока(НомерРелиза) Тогда
		ТекстИсключения = "Отсутствует номер версии";

		ВызватьИсключение ТекстИсключения; 
	КонецЕсли;

	Если СоздатьКаталогНомераРелиза Тогда
		КаталогРелиза = ОбъединитьПути(КаталогРелизов, НомерРелиза);
	Иначе
		КаталогРелиза = КаталогРелизов;
	КонецЕсли;
	ФС.ОбеспечитьКаталог(КаталогРелиза);

	ВременныйКаталогИсходников = ОбъединитьПути(КаталогРелиза, "src");
	СформироватьВременныйКаталогИсходников(ВременныйКаталогИсходников, КаталогИсходников);

	ПараметрыКонструктора = НовыйПараметрыКонструктора();
	ПараметрыКонструктора.НомерРелиза = НомерРелиза;

	Если (ОбъектыРелизов <> Неопределено) И (ТипЗнч(ОбъектыРелизов) <> Тип("Соответствие")) Тогда
		ОбъектыРелизов = Новый Соответствие;
	КонецЕсли;

	Для Каждого ВариантПоставки Из ВариантыПоставок Цикл
		КонструкторРелиза = Новый(ВариантПоставки);

		КонструкторРелиза.Инициализировать(ПараметрыКонструктора);
		Каталог = ОбъединитьПути(КаталогРелиза, КонструкторРелиза.ИмяРелиза());
		ФС.ОбеспечитьПустойКаталог(Каталог);
   		ФС.КопироватьСодержимоеКаталога(ВременныйКаталогИсходников, Каталог);

		КонструкторРелиза.УстановитьКаталогИсходников(Каталог);

		ОбъектыПоставки = ОбъектыПоставки(
			Каталог, 
			КонструкторРелиза.Подсистемы(), 
			КонструкторРелиза.ИсключаемыеПодсистемы(),
			КонструкторРелиза.ОсновнаяПодсистема()
		);

		Если ОбъектыРелизов <> Неопределено Тогда
			ОбъектыРелизов.Вставить(ВариантПоставки, ОбъектыПоставки);
		КонецЕсли;

		УдалитьБлоки(Каталог, КонструкторРелиза.БлокиКУдалению());

		КонструкторРелиза.СкорректироватьМодули(ОбъектыПоставки);
		КонструкторРелиза.СкорректироватьФайлОсновнойПодсистемы(ОбъектыПоставки);
		КонструкторРелиза.СкорректироватьФайлКонфигурации(ОбъектыПоставки);
		КонструкторРелиза.ВыполнитьДополнительныеДействия();
		
		ПутьКФайлуПоставки = ОбъединитьПути(
			КаталогРелиза, 
			КонструкторРелиза.ИмяРелиза() + КонструкторРелиза.РасширениеФайлаРелиза()
		);
		КонструкторРелиза.СоздатьФайлПоставки(ПутьКФайлуПоставки);
		
	 	УдалитьФайлы(Каталог);
	КонецЦикла;

	УдалитьФайлы(ВременныйКаталогИсходников);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

#Область ПараметрыСозданияРелизов

Процедура ОбработатьПараметрыСозданияРелизов(ПараметрыСозданияРелизов)
	КаталогИсходников = ПараметрыСозданияРелизов.КаталогИсходников;
	КаталогРелизов = ПараметрыСозданияРелизов.КаталогРелизов;
	ВариантыПоставок = Новый Массив;
	Если ПустаяСтрока(ПараметрыСозданияРелизов.ВариантыПоставок) Тогда
		ПараметрыСозданияРелизов.ВариантыПоставок = Константы.ВариантыПоставокПоУмолчанию;
	КонецЕсли;
	Для Каждого ВариантПоставки Из СтрРазделить(ПараметрыСозданияРелизов.ВариантыПоставок, ",") Цикл
		ВариантыПоставок.Добавить(ВариантПоставки);
	КонецЦикла;
	СоздатьКаталогНомераРелиза = ПараметрыСозданияРелизов.СоздатьКаталогНомераРелиза;
КонецПроцедуры

Процедура ПроверитьПараметрыСозданияРелизов()
	Ошибки = Новый Массив;
	Если КаталогИсходников = "" Тогда
		Ошибки.Добавить("Не указан каталог исходников");
	КонецЕсли;
	Если КаталогРелизов = "" Тогда
		Ошибки.Добавить("Не указан каталог релизов");
	КонецЕсли;
	ВывестиОшибки(Ошибки);

	Если Не ФС.КаталогСуществует(КаталогИсходников) Тогда
		Ошибки.Добавить("Каталог исходников не существует");
	КонецЕсли;
	Если Не ФС.КаталогСуществует(КаталогРелизов) Тогда
		Ошибки.Добавить("Каталог релизов не существует");
	КонецЕсли;
	ВывестиОшибки(Ошибки);

	ВариантыПоставокКСозданию = Новый Массив;
	Для Каждого ВариантПоставки Из ВариантыПоставок Цикл
		Попытка
			КонструкторРелиза = Новый(ВариантПоставки);
		Исключение
			Сообщить(СтрШаблон(
				"Обработчик варианта поставки ""%1"" не найден",
				ВариантПоставки
			));
			Продолжить;
		КонецПопытки;
		Попытка
			КонструкторРелиза.ЭтоКонструкторРелиза();
			ВариантыПоставокКСозданию.Добавить(ВариантПоставки);
		Исключение
			Сообщить(СтрШаблон(
				"Обработчик варианта поставки ""%1"" не является обработчиком релиза",
				ВариантПоставки
			));
		КонецПопытки;
	КонецЦикла;
	ВариантыПоставок = ВариантыПоставокКСозданию;
	Если ВариантыПоставок.Количество() = 0 Тогда
		Ошибки.Добавить("Не указан ни один вариант поставки к созданию");
		ВывестиОшибки(Ошибки);
	КонецЕсли;
КонецПроцедуры

Процедура ВывестиОшибки(Ошибки)
	Если Ошибки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ТекстИсключения = "";
	Если Ошибки.Количество() = 1 Тогда
		ТекстИсключения = "Обнаружена ошибка: " + Ошибки[0];
	Иначе
		СоставТекста = Новый Массив;
		СоставТекста.Добавить("Обнаружены ошибки:");
		Для Каждого Ошибка Из Ошибки Цикл
			СоставТекста.Добавить(СтрШаблон("  * %1", Ошибка));
		КонецЦикла;

		ТекстИсключения = СтрСоединить(СоставТекста, Символы.ПС);
	КонецЕсли;
		
	ВызватьИсключение ТекстИсключения; 
КонецПроцедуры

#КонецОбласти

#Область СформироватьВременныйКаталогИсходников

Процедура СформироватьВременныйКаталогИсходников(ВременныйКаталогИсходников, КаталогИсходников)
	ФС.ОбеспечитьПустойКаталог(ВременныйКаталогИсходников);
    ФС.КопироватьСодержимоеКаталога(КаталогИсходников, ВременныйКаталогИсходников);

	УдалитьФайлы(ОбъединитьПути(ВременныйКаталогИсходников, Константы.ИмяФайлаДампКонфигурации));

	НайденныеФайлы = НайтиФайлы(ВременныйКаталогИсходников, "*.bsl", Истина);
	РегулярноеВыражение = РегЭкспПоискСлужебныхКомментариев(Константы.ПрефиксыУдаляемыхКомментариев);
	Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
		УдалитьСлужебныеКомментарииИзТекстовМодулей(НайденныйФайл.ПолноеИмя, РегулярноеВыражение);
	КонецЦикла;

	ПутьКФайлуКонфигурации = ОбъединитьПути(ВременныйКаталогИсходников, Константы.ИмяФайлаКонфигурации);
	СкорректироватьФайлКонфигурации(ПутьКФайлуКонфигурации);
КонецПроцедуры

Процедура УдалитьСлужебныеКомментарииИзТекстовМодулей(ПолноеИмяФайла, РегулярноеВыражение)
	ИсходныйТекст = Новый ТекстовыйДокумент;
	ИсходныйТекст.Прочитать(ПолноеИмяФайла);
	Если РегулярноеВыражение.Совпадает(ИсходныйТекст.ПолучитьТекст()) Тогда
		ЦелевойТекст = Новый ТекстовыйДокумент;
		Для Сч = 1 По ИсходныйТекст.КоличествоСтрок() Цикл
			ИсходнаяСтрока = ИсходныйТекст.ПолучитьСтроку(Сч);
			Если РегулярноеВыражение.Совпадает(ИсходнаяСтрока) Тогда
				ПозицияКомментария = СтрНайти(ИсходнаяСтрока, "//");
				СтрокаБезКомментария = СокрП(Лев(ИсходнаяСтрока, ПозицияКомментария - 1));
				Если Не ПустаяСтрока(СтрокаБезКомментария) Тогда
					ЦелевойТекст.ДобавитьСтроку(СтрокаБезКомментария);
				КонецЕсли;
			Иначе
				ЦелевойТекст.ДобавитьСтроку(ИсходнаяСтрока);
			КонецЕсли;
		КонецЦикла;
		ЦелевойТекст.Записать(ПолноеИмяФайла);
	КонецЕсли;
КонецПроцедуры

Функция РегЭкспПоискСлужебныхКомментариев(Маркеры)
	ТекстРегулярногоВыражения = СтрШаблон(
		"\/\/[ \t]*(%1)",
		СтрСоединить(Маркеры, "|")
	);

	Результат = Новый РегулярноеВыражение(ТекстРегулярногоВыражения);
	Результат.Многострочный = Истина;
	Возврат Результат;
КонецФункции

Процедура СкорректироватьФайлКонфигурации(ПолноеИмяФайла)
    ПроцессорXML = Новый СериализацияДанныхXML();
    КонфигурацияПодсистемы = ПроцессорXML.ПрочитатьИзФайла(ПолноеИмяФайла);
	ОчищаемыеВетки = Новый Массив;
	ОчищаемыеВетки.Добавить("MetaDataObject,Configuration,Properties,DefaultCollaborationSystemUsersChoiceForm");
	ОчищаемыеВетки.Добавить("MetaDataObject,Configuration,Properties,ReportsVariantsStorage");
	ОчищаемыеВетки.Добавить("MetaDataObject,Configuration,Properties,DefaultConstantsForm");
	ОчищаемыеВетки.Добавить("MetaDataObject,Configuration,Properties,UsedMobileApplicationFunctionalities");
	ОчищаемыеВетки.Добавить("MetaDataObject,Configuration,Properties,AllowedIncomingShareRequestTypes");
	
	ОбъектКонфигурации = Новый ОбъектКонфигурации();
	ОбъектКонфигурации.ОчиститьВетки(КонфигурацияПодсистемы, ОчищаемыеВетки);
	ПроцессорXML.ЗаписатьВФайл(КонфигурацияПодсистемы, ПолноеИмяФайла);
КонецПроцедуры

#КонецОбласти

#Область ОбъектыПоставки

Функция ОбъектыПоставки(КаталогКонфигурации, ИменаПодсистем, ИменаИсключаемыхПодсистем, ОсновнаяПодсистема)
    Результат = Новый Соответствие;

    Подсистемы = НовыйПодсистемы(ИменаПодсистем);
    ИсключаемыеПодсистемы = НовыйПодсистемы(ИменаИсключаемыхПодсистем);

    Результат = ОбъектыПодсистем(Подсистемы, КаталогКонфигурации);
    ОбъектыИсключаемыхПодсистем = ОбъектыПодсистем(ИсключаемыеПодсистемы, КаталогКонфигурации);

    Для Каждого ОбъектКонфигурации Из ОбъектыИсключаемыхПодсистем Цикл
        Если Результат.Получить(ОбъектКонфигурации.Ключ) = Неопределено Тогда
            Продолжить;
        КонецЕсли;
        Результат.Удалить(ОбъектКонфигурации.Ключ);
    КонецЦикла;

    ДобавитьОбъект(Результат, "Language.Русский");
	СоставОсновнойПодсистемы = СтрРазделить(ОсновнаяПодсистема, ".");
    ДобавитьОбъект(Результат, "Subsystem." + СоставОсновнойПодсистемы[СоставОсновнойПодсистемы.Количество() - 1]);

	Возврат Результат;
КонецФункции

Функция НовыйПодсистемы(ИменаПодсистем)
    Результат = Новый Массив;

    ТипПараметра = ТипЗнч(ИменаПодсистем);
    Если ТипПараметра = Тип("Строка") Тогда
        Разделители = ",";
        Результат = СтрРазделить(ИменаПодсистем, Разделители);
    ИначеЕсли ТипПараметра = Тип("Массив") Тогда
        Для Каждого Элемент Из ИменаПодсистем Цикл
            Результат.Добавить(Элемент);
        КонецЦикла;
    Иначе // BSLLS:EmptyCodeBlock-off
        // Пустой блок
    КонецЕсли;

    Возврат Результат;
КонецФункции

Функция ОбъектыПодсистем(Подсистемы, КаталогКонфигурации)
    Результат = Новый Соответствие;

    Для Каждого Подсистема Из Подсистемы Цикл
        СформироватьОбъектыПодсистемы(Результат, Подсистема, КаталогКонфигурации);
    КонецЦикла;

    Возврат Результат;
КонецФункции

Процедура ДобавитьОбъект(ОбъектыПодсистемы, ПолноеИмяОбъекта)
	СоставИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
	// Количество компонентов должно быть равным 2, например "DataProcessor.МФРаботаСМакетом"
	КоличествоКомпонентов = 2;
	Если СоставИмени.Количество() <> КоличествоКомпонентов Тогда
		Возврат;
	КонецЕсли;

    ТипОбъекта = СоставИмени[0];
    ИмяОбъекта = СоставИмени[1];
    Описание = Новый Структура(
        "Имя, Тип",
        ИмяОбъекта,
        ТипОбъекта
    );

    ОбъектыПодсистемы.Вставить(ПолноеИмяОбъекта, Описание);
КонецПроцедуры

Процедура СформироватьОбъектыПодсистемы(ОбъектыПодсистемы, Подсистема, КаталогКонфигурации)
	Если ПустаяСтрока(Подсистема) Тогда
		Возврат;
	КонецЕсли;
	СоставПути = СтрРазделить(Подсистема, ".");
	Если СоставПути.Количество() <> 1 Тогда
		СоставПути.Вставить(СоставПути.Количество() - 1, "Subsystems");
	КонецЕсли;
	СоставПути.Вставить(0, "Subsystems");
	СоставПути.Вставить(0, КаталогКонфигурации);
	ИмяФайлаОписанияПодсистемы = СтрСоединить(СоставПути, ПолучитьРазделительПути()) + ".xml";
    Если Не ФС.ФайлСуществует(ИмяФайлаОписанияПодсистемы) Тогда
        ТекстИсключения = СтрШаблон(
            НСтр("ru='Отсутствует описание подсистемы ""%1""'"),
            Подсистема
        );

        ВызватьИсключение ТекстИсключения;
    КонецЕсли;

    ПроцессорXML = Новый СериализацияДанныхXML(Ложь);
    КонфигурацияПодсистемы = ПроцессорXML.ПрочитатьИзФайла(ИмяФайлаОписанияПодсистемы);
    ОбработатьПодчиненныеПодсистемы(
        ОбъектыПодсистемы, 
        КонфигурацияПодсистемы, 
        Подсистема,
        КаталогКонфигурации
    );
    ОбработатьОбъектыПодсистемы(ОбъектыПодсистемы, КонфигурацияПодсистемы);
КонецПроцедуры

Процедура ОбработатьПодчиненныеПодсистемы(ОбъектыПодсистемы, КонфигурацияПодсистемы, Подсистема, КаталогКонфигурации)
	ОбъектКонфигурации = Новый ОбъектКонфигурации();
	Ветка = ОбъектКонфигурации.НайтиВеткуКонфигурации(КонфигурацияПодсистемы, "MetaDataObject,Subsystem,ChildObjects");
	Описание = Ветка._Элементы;
    ТипЗначенияОписания = ТипЗнч(Описание);
    Если ТипЗначенияОписания = Тип("Массив") Тогда
        Для Каждого Элемент Из Описание Цикл
            ОбработатьПодчиненнуюПодсистему(Элемент, ОбъектыПодсистемы, Подсистема, КаталогКонфигурации);
        КонецЦикла;
    ИначеЕсли ТипЗначенияОписания = Тип("Соответствие") Тогда
        ОбработатьПодчиненнуюПодсистему(Описание, ОбъектыПодсистемы, Подсистема, КаталогКонфигурации);
    Иначе // BSLLS:EmptyCodeBlock-off
        // Пустой блок
    КонецЕсли;
КонецПроцедуры

Процедура ОбработатьПодчиненнуюПодсистему(Элемент, ОбъектыПодсистемы, Подсистема, КаталогКонфигурации)
    Описание = Элемент.Получить("Subsystem");
    Если Описание = Неопределено Тогда
        Возврат;
    КонецЕсли;
    ИмяПодчиненнойПодсистемы = Подсистема + "." + Описание._Значение;
    СформироватьОбъектыПодсистемы(ОбъектыПодсистемы, ИмяПодчиненнойПодсистемы, КаталогКонфигурации);
КонецПроцедуры

Процедура ОбработатьОбъектыПодсистемы(ОбъектыПодсистемы, КонфигурацияПодсистемы)
	ОбъектКонфигурации = Новый ОбъектКонфигурации();
	Ветка = ОбъектКонфигурации.НайтиВеткуКонфигурации(КонфигурацияПодсистемы, "MetaDataObject,Subsystem,Properties,Content"); // BSLLS:LineLength-off
	Описание = Ветка._Элементы;
    ТипЗначенияОписания = ТипЗнч(Описание);
    Если ТипЗначенияОписания = Тип("Массив") Тогда
        Для Каждого Элемент Из Описание Цикл
            ОбработатьОбъектПодсистемы(Элемент, ОбъектыПодсистемы);
        КонецЦикла;
    ИначеЕсли ТипЗначенияОписания = Тип("Соответствие") Тогда
        ОбработатьОбъектПодсистемы(Описание, ОбъектыПодсистемы);
    Иначе // BSLLS:EmptyCodeBlock-off
        // Пустой блок
    КонецЕсли;
КонецПроцедуры

Процедура ОбработатьОбъектПодсистемы(Элемент, ОбъектыПодсистемы)
    Описание = Элемент.Получить("xr:Item");
    Если ТипЗнч(Описание) <> Тип("Структура") Тогда
        Возврат;
    КонецЕсли;

    ИмяОбъекта = Описание._Значение;
    ДобавитьОбъект(ОбъектыПодсистемы, ИмяОбъекта);
КонецПроцедуры

#КонецОбласти

#Область УдалитьБлоки

Процедура УдалитьБлоки(Каталог, УдаляемыеБлоки)
	НайденныеФайлы = НайтиФайлы(Каталог, "*.bsl", Истина);
	УдаляемыеБлокиСтрокой = СтрСоединить(УдаляемыеБлоки, "|");
	ТекстРегулярногоВыражения = ТекстРегулярногоВыраженияУдаляемогоБлока(УдаляемыеБлокиСтрокой);
	РегулярноеВыражение = Новый РегулярноеВыражение(ТекстРегулярногоВыражения);
	РегулярноеВыражение.Многострочный = Истина;
	Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(НайденныйФайл.ПолноеИмя);
		ТекстФайла = ТекстовыйДокумент.ПолучитьТекст();
		Если Не РегулярноеВыражение.Совпадает(ТекстФайла) Тогда
			Продолжить;
		КонецЕсли;
		СтрокиФайла = СтрРазделить(ТекстФайла, Символы.ПС);
		ТекстИсправленногоФайла = ТекстИсправленногоФайла(СтрокиФайла, РегулярноеВыражение);

		ТекстовыйДокумент.УстановитьТекст(ТекстИсправленногоФайла);
		ТекстовыйДокумент.Записать(НайденныйФайл.ПолноеИмя);
	КонецЦикла;
КонецПроцедуры

Функция ТекстИсправленногоФайла(СтрокиФайла, РегулярноеВыражение)
	Результат = "";
	СоставИсправленногоФайла = Новый Массив;
	НашлиУдаляемыйБлок = Ложь;

	Для Каждого СтрокаФайла Из СтрокиФайла Цикл
		Совпадения = РегулярноеВыражение.НайтиСовпадения(СтрокаФайла);
		Если Совпадения.Количество() = 1 Тогда
			ЗначениеВСтроке = Совпадения[0].Группы[3].Значение;
			Если ЗначениеВСтроке = Константы.НачалоБлока Тогда
				НашлиУдаляемыйБлок = Истина;
			ИначеЕсли ЗначениеВСтроке = Константы.КонецБлока Тогда
				НашлиУдаляемыйБлок = Ложь;
			Иначе // BSLLS:EmptyCodeBlock-off
				// Пустой блок
			КонецЕсли;
		ИначеЕсли Не НашлиУдаляемыйБлок Тогда
			СоставИсправленногоФайла.Добавить(СтрокаФайла);
		Иначе // BSLLS:EmptyCodeBlock-off
			// Пустой блок
		КонецЕсли;
	КонецЦикла;
	Результат = СтрСоединить(СоставИсправленногоФайла, Символы.ПС);

	Возврат Результат;
КонецФункции

Функция ТекстРегулярногоВыраженияУдаляемогоБлока(УдаляемыйБлок)
	Возврат СтрШаблон(Константы.ШаблонРегулярногоВыраженияПоискаБлока, УдаляемыйБлок);
КонецФункции

#КонецОбласти

Функция НомерРелиза(_КаталогИсходников)
	Результат = "";
    ПроцессорXML = Новый СериализацияДанныхXML(Ложь);
	ПутьКФайлуКонфигурации = ОбъединитьПути(_КаталогИсходников, Константы.ИмяФайлаКонфигурации);
	ИсходнаяКонфигурация = ПроцессорXML.ПрочитатьИзФайла(ПутьКФайлуКонфигурации);
	ОбъектКонфигурации = Новый ОбъектКонфигурации();
	Ветка = ОбъектКонфигурации.НайтиВеткуКонфигурации(ИсходнаяКонфигурация, "MetaDataObject,Configuration,Properties,Version"); // BSLLS:LineLength-off
	Результат = Ветка._Значение;

	Возврат Результат;
КонецФункции

Функция НовыйПараметрыКонструктора()
	Результат = Новый Структура;

	Результат.Вставить("НомерРелиза", Неопределено);

	Возврат Результат;
КонецФункции

#КонецОбласти

ПутьККаталогуТекущегоСценария = ТекущийСценарий().Каталог;
// BSLLS:NestedFunctionInParameters-off
Константы = ЗагрузитьСценарий(ОбъединитьПути(
	ПутьККаталогуТекущегоСценария,
	"..",
	"Ресурсы",
	"Константы.os"
));
