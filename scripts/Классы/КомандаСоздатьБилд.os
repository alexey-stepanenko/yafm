// BSLLS:MissingVariablesDescription-off
// BSLLS:NestedFunctionInParameters-off

#Использовать fs
#Использовать v8runner

Перем Константы;

Перем СтрокаСоединения;
Перем ПользовательИнформационойБазы;
Перем ПарольПользователяИнформационойБазы;
Перем ВерсияПлатформы;
Перем КаталогИсходников;
Перем УвеличитьНомерБилда;
Перем Конфигуратор;
Перем ОписаниеКонфигурации;
Перем ИсходнаяВерсия;
Перем НоваяВерсия;
Перем ИсходнаяСтрока;
Перем Отступ;

#Область ПрограммныйИнтерфейс

Процедура НастроитьКоманду(Знач Команда, Знач Парсер) Экспорт
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраПутьКИнформационнойБазе,
		"Путь к информационной базе"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраПользовательИнформационойБазы,
		"Пользователь информационной базы"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраПарольПользователяИнформационойБазы,
		"Пароль пользователя информационной базы"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраКаталогИсходников,
		"Путь к каталогу с исходными кодами"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраВерсияПлатформы,
		"Версия платформы 1С"
	);
	Парсер.ДобавитьПараметрФлагКоманды(
		Команда,
		Константы.ИмяПараметраУвеличитьНомерБилда,
		"Увеличить номер билда"
	);
КонецПроцедуры

Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач Приложение) Экспорт
	ПутьКИнформационнойБазе = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраПутьКИнформационнойБазе);
	ПользовательИнформационойБазы = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраПользовательИнформационойБазы); // BSLLS:LineLength-off
	ПарольПользователяИнформационойБазы = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраПарольПользователяИнформационойБазы); // BSLLS:LineLength-off
	ВерсияПлатформы = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраВерсияПлатформы);
	КаталогИсходников = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраКаталогИсходников);
	УвеличитьНомерБилда = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраУвеличитьНомерБилда);

	Если ПустаяСтрока(ПользовательИнформационойБазы) Тогда
		ПарольПользователяИнформационойБазы = "";
	КонецЕсли;

	ФС.ОбеспечитьПустойКаталог(КаталогИсходников);
	СтрокаСоединения = СтрШаблон(
		"/IBConnectionString""%1""",
		СтрЗаменить(ПутьКИнформационнойБазе, """", """""")
	);

	Конфигуратор = Новый УправлениеКонфигуратором();

	ВыгрузитьКонфигурациюВИсходныеКоды();
	ОбработатьСтруктуруИсходныхКодов();
	СформироватьШаблонРасширенияДляТестов();
	ПолучитьИсходнуюВерсиюКонфигурации();
	СформироватьНовуюВерсиюКонфигурации();
	СохранитьНовуюВерсиюКонфигурации();
	ЗагрузитьКонфигурациюИзИсходныхКодов();

	// При успешном выполнении возвращает код успеха
	Возврат Приложение.РезультатыКоманд().Успех;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Функция ЗначениеПараметра(Параметры, ИмяПараметра, ЗначениеПоУмолчанию = "")
	Результат = Параметры.Получить(ИмяПараметра);
	Если Результат = Неопределено Тогда
		Результат = ЗначениеПоУмолчанию;
	КонецЕсли;

	Возврат Результат;
КонецФункции

Процедура ВыгрузитьКонфигурациюВИсходныеКоды()
	Конфигуратор.УстановитьКонтекст(СтрокаСоединения, ПользовательИнформационойБазы, ПарольПользователяИнформационойБазы);
	Конфигуратор.ИспользоватьВерсиюПлатформы(ВерсияПлатформы);
	Конфигуратор.ВыгрузитьКонфигурациюВФайлы(КаталогИсходников);
КонецПроцедуры

Процедура ОбработатьСтруктуруИсходныхКодов()
	ФС.УдалитьФайлы(ОбъединитьПути(КаталогИсходников, Константы.ИмяФайлаДампКонфигурации));
КонецПроцедуры

Процедура СформироватьШаблонРасширенияДляТестов()
	ГенераторРелизов = Новый ГенераторРелизов;
	ПараметрыСоздания = ГенераторРелизов.НовыйПараметрыСозданияРелизов();
	ПараметрыСоздания.КаталогИсходников = КаталогИсходников;
	ПараметрыСоздания.КаталогРелизов = "";
	ПараметрыСоздания.КаталогРелизов = "/home/alexey/repo/yafm/tmp/2";
	ПараметрыСоздания.ВариантыПоставок = "ШаблонРасширенияДляТестов";
	ПараметрыСоздания.СоздатьКаталогНомераРелиза = Ложь;

	ФС.ОбеспечитьПустойКаталог(ПараметрыСоздания.КаталогРелизов);

	ГенераторРелизов.Создать(ПараметрыСоздания);
	Файлы = НайтиФайлы(ПараметрыСоздания.КаталогРелизов, "*.cfe");
	ПутьКНовомуШаблону = Файлы[0].ПолноеИмя;
	ХешНовогоШаблона = ХешФайла(ПутьКНовомуШаблону);

	СоставПутиКШаблонуРасширенияСТестами = Новый Массив;
	СоставПутиКШаблонуРасширенияСТестами.Добавить(КаталогИсходников);
	СоставПутиКШаблонуРасширенияСТестами.Добавить("DataProcessors");
	СоставПутиКШаблонуРасширенияСТестами.Добавить("МФГенерацияАвтотестов");
	СоставПутиКШаблонуРасширенияСТестами.Добавить("Templates");
	СоставПутиКШаблонуРасширенияСТестами.Добавить("ШаблонРасширенияДляТестов");
	СоставПутиКШаблонуРасширенияСТестами.Добавить("Ext");
	СоставПутиКШаблонуРасширенияСТестами.Добавить("Template.bin");
	ПутьКТекущемуШаблону = СтрСоединить(СоставПутиКШаблонуРасширенияСТестами, ПолучитьРазделительПути());
	ХешТекущегоШаблона = ХешФайла(ПутьКТекущемуШаблону);

	Если ХешНовогоШаблона <> ХешТекущегоШаблона Тогда
		УдалитьФайлы(ПутьКТекущемуШаблону);
		КопироватьФайл(ПутьКНовомуШаблону, ПутьКТекущемуШаблону);
	КонецЕсли;
	УдалитьФайлы(ПараметрыСоздания.КаталогРелизов);
КонецПроцедуры

Функция ХешФайла(ПутьКФайлу)
	Если Не ФС.ФайлСуществует(ПутьКФайлу) Тогда
		Возврат "";
	КонецЕсли;
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
	Хеш.ДобавитьФайл(ПутьКФайлу);

	Возврат ПолучитьHexСтрокуИзДвоичныхДанных(Хеш.ХешСумма);
КонецФункции

Процедура ПолучитьИсходнуюВерсиюКонфигурации()
	ОписаниеКонфигурации = Новый ТекстовыйДокумент;
	ОписаниеКонфигурации.Прочитать(ОбъединитьПути(КаталогИсходников, Константы.ИмяФайлаКонфигурации));
	РегЭкспВерсия = Новый РегулярноеВыражение("^([ \t]*)<Version>(.*)</Version>[ \t]*$");
	ИсходнаяВерсия = "";
	ИсходнаяСтрока = "";
	Отступ = "";
	Для Сч = 1 По ОписаниеКонфигурации.КоличествоСтрок() Цикл
		ИсходнаяСтрока = ОписаниеКонфигурации.ПолучитьСтроку(Сч);
		Если РегЭкспВерсия.Совпадает(ИсходнаяСтрока) Тогда
			СоставСтроки = РегЭкспВерсия.Разделить(ИсходнаяСтрока);
			Отступ = СоставСтроки[1];
			ИсходнаяВерсия = СоставСтроки[2];
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ПустаяСтрока(ИсходнаяВерсия) Тогда
		ВызватьИсключение "Отсутствует номер версии конфигурации";
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьНовуюВерсиюКонфигурации()
	Если Не УвеличитьНомерБилда Тогда
		НоваяВерсия = ИсходнаяВерсия;
		Возврат;
	КонецЕсли;

	СоставВерсии = СтрРазделить(ИсходнаяВерсия, ".");
	ПозицияБилда = СоставВерсии.Количество() - 1;
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)); // BSLLS:MagicNumber-off
	Билд = ТипЧисло.ПривестиЗначение(СоставВерсии[ПозицияБилда]);
	СоставВерсии[ПозицияБилда] = Формат(Билд + 1, "ЧГ=");
	НоваяВерсия = СтрСоединить(СоставВерсии, ".");
КонецПроцедуры

Процедура СохранитьНовуюВерсиюКонфигурации()
	Конфигурация = ОписаниеКонфигурации.ПолучитьТекст();
	Конфигурация = СтрЗаменить(Конфигурация, ИсходнаяСтрока, Отступ + "<Version>" + НоваяВерсия + "</Version>");
	ОписаниеКонфигурации.УстановитьТекст(Конфигурация);
	ОписаниеКонфигурации.Записать(ОбъединитьПути(КаталогИсходников, Константы.ИмяФайлаКонфигурации));

	Модули = Новый Массив;
	Модули.Добавить("CommonModules,МФСлужебный,Ext,Module.bsl");
	Для Каждого Модуль Из Модули Цикл
		СоставПутиКФайлуМодуля = СтрРазделить(Модуль, ",");
		СоставПутиКФайлуМодуля.Вставить(0, КаталогИсходников);
		ПутьКФайлуМодуля = СтрСоединить(СоставПутиКФайлуМодуля, ПолучитьРазделительПути());
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ПутьКФайлуМодуля);
		Состав = Новый Массив;
		ИдентификаторСтрокиСВерсией = "// ВЕРСИЯ";
		Для Сч = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
			СтрокаМодуля = ТекстовыйДокумент.ПолучитьСтроку(Сч);
			Если СтрНайти(СтрокаМодуля, ИдентификаторСтрокиСВерсией) > 0 Тогда
				ПрефиксСтроки = Лев(СтрокаМодуля, СтрДлина(СтрокаМодуля) - СтрДлина(СокрЛ(СтрокаМодуля)));
				Состав.Добавить(СтрШаблон(
					"%1Возврат ""%2""; %3",
					ПрефиксСтроки,
					НоваяВерсия,
					ИдентификаторСтрокиСВерсией
				));
			Иначе
				Состав.Добавить(СтрокаМодуля);
			КонецЕсли;
		КонецЦикла;
		ТекстМодуля = СтрСоединить(Состав, Символы.ПС);
		ТекстовыйДокумент.УстановитьТекст(ТекстМодуля);
		ТекстовыйДокумент.Записать(ПутьКФайлуМодуля);
	КонецЦикла;
КонецПроцедуры

Процедура ЗагрузитьКонфигурациюИзИсходныхКодов()
	Конфигуратор.ЗагрузитьКонфигурациюИзФайлов(КаталогИсходников);
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ПутьККаталогуТекущегоСценария = ТекущийСценарий().Каталог;
// BSLLS:NestedFunctionInParameters-off
Константы = ЗагрузитьСценарий(ОбъединитьПути(
	ПутьККаталогуТекущегоСценария,
	"..",
	"Ресурсы",
	"Константы.os"
));

#КонецОбласти
