// BSLLS:MissingVariablesDescription-off
// BSLLS:NestedFunctionInParameters-off

#Использовать fs
#Использовать v8runner
#Использовать gitrunner

Перем Константы;

Перем СтрокаСоединения;
Перем ПутьКИнформационнойБазе;
Перем ПользовательИнформационойБазы;
Перем ПарольПользователяИнформационойБазы;
Перем ВерсияПлатформы;
Перем ИмяКаталогаИсходников;
Перем КаталогИсходников;
Перем КаталогРепозитория;
Перем УвеличитьНомерБилда;
Перем Конфигуратор;
Перем ОписаниеКонфигурации;
Перем ИсходнаяВерсия;
Перем НоваяВерсия;
Перем ИсходнаяСтрока;
Перем Отступ;

#Область ПрограммныйИнтерфейс

Процедура НастроитьКоманду(Знач Команда, Знач Парсер) Экспорт
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраПутьКИнформационнойБазе,
		"Путь к информационной базе"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраПользовательИнформационойБазы,
		"Пользователь информационной базы"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраПарольПользователяИнформационойБазы,
		"Пароль пользователя информационной базы"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраКаталогИсходников,
		"Имя каталога с исходными кодами относительно каталога репозитория"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраКаталогРепозитория,
		"Путь к каталогу репозитория"
	);
	Парсер.ДобавитьИменованныйПараметрКоманды(
		Команда,
		Константы.ИмяПараметраВерсияПлатформы,
		"Версия платформы 1С"
	);
	Парсер.ДобавитьПараметрФлагКоманды(
		Команда,
		Константы.ИмяПараметраУвеличитьНомерБилда,
		"Увеличить номер билда"
	);
КонецПроцедуры

Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач Приложение) Экспорт
	ПутьКИнформационнойБазе = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраПутьКИнформационнойБазе);
	ПользовательИнформационойБазы = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраПользовательИнформационойБазы); // BSLLS:LineLength-off
	ПарольПользователяИнформационойБазы = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраПарольПользователяИнформационойБазы); // BSLLS:LineLength-off
	ВерсияПлатформы = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраВерсияПлатформы);
	ИмяКаталогаИсходников = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраКаталогИсходников);
	УвеличитьНомерБилда = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраУвеличитьНомерБилда);
	КаталогРепозитория = ЗначениеПараметра(ПараметрыКоманды, Константы.ИмяПараметраКаталогРепозитория);

	КаталогИсходников = ОбъединитьПути(КаталогРепозитория, ИмяКаталогаИсходников);

	Если ПустаяСтрока(ПользовательИнформационойБазы) Тогда
		ПарольПользователяИнформационойБазы = "";
	КонецЕсли;

	ФС.ОбеспечитьПустойКаталог(КаталогИсходников);
	СтрокаСоединения = СтрШаблон(
		"/IBConnectionString""%1""",
		СтрЗаменить(ПутьКИнформационнойБазе, """", """""")
	);

	Конфигуратор = Новый УправлениеКонфигуратором();

	ВыгрузитьКонфигурациюВИсходныеКоды();
	ОбработатьСтруктуруИсходныхКодов();
	СформироватьШаблонРасширенияДляТестов();
	ПолучитьИсходнуюВерсиюКонфигурации();
	СформироватьНовуюВерсиюКонфигурации();
	СохранитьНовуюВерсиюКонфигурации();
	ЗагрузитьКонфигурациюИзИсходныхКодов();

	Возврат Приложение.РезультатыКоманд().Успех;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Функция ЗначениеПараметра(Параметры, ИмяПараметра, ЗначениеПоУмолчанию = "")
	Результат = Параметры.Получить(ИмяПараметра);
	Если Результат = Неопределено Тогда
		Результат = ЗначениеПоУмолчанию;
	КонецЕсли;

	Возврат Результат;
КонецФункции

Процедура ВыгрузитьКонфигурациюВИсходныеКоды()
	Сообщить("== Выгрузка конфигурации из информационной базы в каталог исходных кодов ==");
	Сообщить("   Информационная база: " + ПутьКИнформационнойБазе);
	Сообщить("   Каталог исходных файлов: " + ИмяКаталогаИсходников);
	Конфигуратор.УстановитьКонтекст(СтрокаСоединения, ПользовательИнформационойБазы, ПарольПользователяИнформационойБазы);
	Конфигуратор.ИспользоватьВерсиюПлатформы(ВерсияПлатформы);
	Конфигуратор.ВыгрузитьКонфигурациюВФайлы(КаталогИсходников);
	Сообщить("   УСПЕШНО!");
КонецПроцедуры

Процедура ОбработатьСтруктуруИсходныхКодов()
	Сообщить("== Обработка структуры исходных кодов ==");
	ФС.УдалитьФайлы(ОбъединитьПути(КаталогИсходников, Константы.ИмяФайлаДампКонфигурации));
	Сообщить("   УСПЕШНО!");
КонецПроцедуры

Процедура СформироватьШаблонРасширенияДляТестов()
	Сообщить("== Формирование шаблона расширения для тестов ==");
	Сообщить("   -- Получение списка измененных файлов текущего билда --");
	ГитРепозиторий = Новый ГитРепозиторий();
	ГитРепозиторий.УстановитьРабочийКаталог(КаталогРепозитория);
	ИмяНастройкиЭкранироватьСимволы = "core.quotepath";
	НастройкаЭкранироватьСимволы = ГитРепозиторий.ПолучитьНастройку(
		ИмяНастройкиЭкранироватьСимволы,
		РежимУстановкиНастроекGit.Локально
	);
	ГитРепозиторий.УстановитьНастройку(
		ИмяНастройкиЭкранироватьСимволы,
		"false",
		РежимУстановкиНастроекGit.Локально
	);
	СтатусРепозитория = ГитРепозиторий.Статус();
	ГитРепозиторий.УстановитьНастройку(
		ИмяНастройкиЭкранироватьСимволы,
		НастройкаЭкранироватьСимволы,
		РежимУстановкиНастроекGit.Локально
	);
	СоставСтатуса = СтрРазделить(СтатусРепозитория, Символы.ПС);
	ИзмененныеФайлы = Новый Массив;
	ТекстРегулярногоВыражения = "^[ \t]*изменено:[ \t]*(.*)";
	РегулярноеВыражение = Новый РегулярноеВыражение(ТекстРегулярногоВыражения);
	Для Каждого Элемент Из СоставСтатуса Цикл
		Совпадения = РегулярноеВыражение.НайтиСовпадения(Элемент);
		Если Совпадения.Количество() <> 1 Тогда
			Продолжить;
		КонецЕсли;
		ИзмененныеФайлы.Добавить(Совпадения[0].Группы[1].Значение);
	КонецЦикла;
	Если ИзмененныеФайлы.Количество() = 0 Тогда
		Сообщить("      Измененных файлов нет");
	Иначе
		Сообщить("      Измененные файлы:");
		Для Каждого ИзмененныйФайл Из ИзмененныеФайлы Цикл
			Сообщить("      * " + ИзмененныйФайл);
		КонецЦикла;
	КонецЕсли;
	СписокФайлов = СтрСоединить(ИзмененныеФайлы, Символы.ПС);

	Сообщить("   -- Получение списка файлов шаблона расширения для тестов --");
	ВременныйКаталог = ПолучитьИмяВременногоФайла();
	ГенераторРелизов = Новый ГенераторРелизов;
	ПараметрыСоздания = ГенераторРелизов.НовыйПараметрыСозданияРелизов();
	ПараметрыСоздания.КаталогИсходников = КаталогИсходников;
	ПараметрыСоздания.КаталогРелизов = ВременныйКаталог;
	ПараметрыСоздания.ВариантыПоставок = "ШаблонРасширенияДляТестов";
	ПараметрыСоздания.СоздатьКаталогНомераРелиза = Ложь;

	ФС.ОбеспечитьПустойКаталог(ВременныйКаталог);

	ОбъектыШаблонаРасширения = Новый Соответствие;
	ГенераторРелизов.Создать(ПараметрыСоздания, ОбъектыШаблонаРасширения);

	ФайлыШаблонаРасширения = Новый Массив;
	СоответствиеФайлов = Новый Соответствие;
	СоответствиеФайлов.Вставить("CommonModule", "CommonModules");
	СоответствиеФайлов.Вставить("CommonTemplate", "CommonTemplates");
	СоответствиеФайлов.Вставить("Language", "Languages");
	СоответствиеФайлов.Вставить("Subsystem", "Subsystems");
	Для Каждого Элемент Из ОбъектыШаблонаРасширения.Получить(ПараметрыСоздания.ВариантыПоставок) Цикл
		Состав = СтрРазделить(Элемент.Ключ, ".");
		ИмяКаталога = СоответствиеФайлов.Получить(Состав[0]);
		Если ИмяКаталога = Неопределено Тогда
			ВызватьИсключение "Неизвестный тип объекта шаблона расширения: " + Состав[0];
		КонецЕсли;
		ФайлыШаблонаРасширения.Добавить(ОбъединитьПути(ИмяКаталогаИсходников, ИмяКаталога, Состав[1]));
	КонецЦикла;
	Сообщить("      Файлы шаблона расширения:");
	Для Каждого ФайлШаблонаРасширения Из ФайлыШаблонаРасширения Цикл
		Сообщить("      * " + ФайлШаблонаРасширения);
	КонецЦикла;
	
	Сообщить("   -- Поиск файлов шаблона расширения среди измененных файлов текущего билда --");
	ЗаменитьШаблонРасширения = Ложь;
	Для Каждого ФайлШаблонаРасширения Из ФайлыШаблонаРасширения Цикл
		Сообщить("      Анализируется файл: " + ФайлШаблонаРасширения);
		ШаблонПоиска = СтрЗаменить(ФайлШаблонаРасширения, "\", "\\");
		ШаблонПоиска = "^" + СтрЗаменить(ШаблонПоиска, "/", "\/") + ".*$";
		РегулярноеВыражение = Новый РегулярноеВыражение(ШаблонПоиска);
 		Если РегулярноеВыражение.Совпадает(СписокФайлов) Тогда
			Сообщить("        НАЙДЕН!");
			ЗаменитьШаблонРасширения = Истина;
			Прервать;
		КонецЕсли;
		Сообщить("        Не найден");
	КонецЦикла;

	Если ЗаменитьШаблонРасширения Тогда
		Сообщить("   -- Создание новой версии шаблона расширения для тестов --");
		Файлы = НайтиФайлы(ВременныйКаталог, "*.cfe");
		ПутьКНовомуШаблону = Файлы[0].ПолноеИмя;
		Сообщить("      Новая версия шаблона расширения для тестов: " + ПутьКНовомуШаблону);

		СоставПутиКШаблонуРасширенияСТестами = Новый Массив;
		СоставПутиКШаблонуРасширенияСТестами.Добавить(КаталогИсходников);
		СоставПутиКШаблонуРасширенияСТестами.Добавить("DataProcessors");
		СоставПутиКШаблонуРасширенияСТестами.Добавить("МФГенерацияАвтотестов");
		СоставПутиКШаблонуРасширенияСТестами.Добавить("Templates");
		СоставПутиКШаблонуРасширенияСТестами.Добавить("ШаблонРасширенияДляТестов");
		СоставПутиКШаблонуРасширенияСТестами.Добавить("Ext");
		СоставПутиКШаблонуРасширенияСТестами.Добавить("Template.bin");
		ПутьКТекущемуШаблону = СтрСоединить(СоставПутиКШаблонуРасширенияСТестами, ПолучитьРазделительПути());

		Сообщить("      Текущая версия шаблона расширения для тестов: " + ПутьКТекущемуШаблону);
		УдалитьФайлы(ПутьКТекущемуШаблону);
		КопироватьФайл(ПутьКНовомуШаблону, ПутьКТекущемуШаблону);
		Сообщить("      УСПЕШНО!");
	Иначе
		Сообщить("      Создание новой версии шаблона расширения для тестов не требуется");
	КонецЕсли;

	УдалитьФайлы(ВременныйКаталог);
КонецПроцедуры

Процедура ПолучитьИсходнуюВерсиюКонфигурации()
	Сообщить("== Получение текущей версии конфигурации ==");
	ОписаниеКонфигурации = Новый ТекстовыйДокумент;
	ОписаниеКонфигурации.Прочитать(ОбъединитьПути(КаталогИсходников, Константы.ИмяФайлаКонфигурации));
	РегЭкспВерсия = Новый РегулярноеВыражение("^([ \t]*)<Version>(.*)</Version>[ \t]*$");
	ИсходнаяВерсия = "";
	ИсходнаяСтрока = "";
	Отступ = "";
	Для Сч = 1 По ОписаниеКонфигурации.КоличествоСтрок() Цикл
		ИсходнаяСтрока = ОписаниеКонфигурации.ПолучитьСтроку(Сч);
		Если РегЭкспВерсия.Совпадает(ИсходнаяСтрока) Тогда
			СоставСтроки = РегЭкспВерсия.Разделить(ИсходнаяСтрока);
			Отступ = СоставСтроки[1];
			ИсходнаяВерсия = СоставСтроки[2];
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ПустаяСтрока(ИсходнаяВерсия) Тогда
		ВызватьИсключение "Отсутствует номер версии конфигурации";
	КонецЕсли;
	Сообщить("   Текущая версия конфигурации: " + ИсходнаяВерсия);
КонецПроцедуры

Процедура СформироватьНовуюВерсиюКонфигурации()
	Сообщить("== Формирование новой версии конфигурации ==");
	Если Не УвеличитьНомерБилда Тогда
		Сообщить("   Формирование новой версии конфигурации не требуется");
		НоваяВерсия = ИсходнаяВерсия;
		Возврат;
	КонецЕсли;


	СоставВерсии = СтрРазделить(ИсходнаяВерсия, ".");
	ПозицияБилда = СоставВерсии.Количество() - 1;
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)); // BSLLS:MagicNumber-off
	Билд = ТипЧисло.ПривестиЗначение(СоставВерсии[ПозицияБилда]);
	СоставВерсии[ПозицияБилда] = Формат(Билд + 1, "ЧГ=");
	НоваяВерсия = СтрСоединить(СоставВерсии, ".");
	Сообщить("   Новая версия конфигурации: " + НоваяВерсия);
КонецПроцедуры

Процедура СохранитьНовуюВерсиюКонфигурации()
	Конфигурация = ОписаниеКонфигурации.ПолучитьТекст();
	Конфигурация = СтрЗаменить(Конфигурация, ИсходнаяСтрока, Отступ + "<Version>" + НоваяВерсия + "</Version>");
	ОписаниеКонфигурации.УстановитьТекст(Конфигурация);
	ОписаниеКонфигурации.Записать(ОбъединитьПути(КаталогИсходников, Константы.ИмяФайлаКонфигурации));

	Модули = Новый Массив;
	Модули.Добавить("CommonModules,МФСлужебный,Ext,Module.bsl");
	Для Каждого Модуль Из Модули Цикл
		СоставПутиКФайлуМодуля = СтрРазделить(Модуль, ",");
		СоставПутиКФайлуМодуля.Вставить(0, КаталогИсходников);
		ПутьКФайлуМодуля = СтрСоединить(СоставПутиКФайлуМодуля, ПолучитьРазделительПути());
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ПутьКФайлуМодуля);
		Состав = Новый Массив;
		ИдентификаторСтрокиСВерсией = "// ВЕРСИЯ";
		Для Сч = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
			СтрокаМодуля = ТекстовыйДокумент.ПолучитьСтроку(Сч);
			Если СтрНайти(СтрокаМодуля, ИдентификаторСтрокиСВерсией) > 0 Тогда
				ПрефиксСтроки = Лев(СтрокаМодуля, СтрДлина(СтрокаМодуля) - СтрДлина(СокрЛ(СтрокаМодуля)));
				Состав.Добавить(СтрШаблон(
					"%1Возврат ""%2""; %3",
					ПрефиксСтроки,
					НоваяВерсия,
					ИдентификаторСтрокиСВерсией
				));
			Иначе
				Состав.Добавить(СтрокаМодуля);
			КонецЕсли;
		КонецЦикла;
		ТекстМодуля = СтрСоединить(Состав, Символы.ПС);
		ТекстовыйДокумент.УстановитьТекст(ТекстМодуля);
		ТекстовыйДокумент.Записать(ПутьКФайлуМодуля);
	КонецЦикла;
КонецПроцедуры

Процедура ЗагрузитьКонфигурациюИзИсходныхКодов()
	Конфигуратор.ЗагрузитьКонфигурациюИзФайлов(КаталогИсходников);
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ПутьККаталогуТекущегоСценария = ТекущийСценарий().Каталог;
// BSLLS:NestedFunctionInParameters-off
Константы = ЗагрузитьСценарий(ОбъединитьПути(
	ПутьККаталогуТекущегоСценария,
	"..",
	"Ресурсы",
	"Константы.os"
));

#КонецОбласти
